SUBROUTINE MGN2MECH(KDATE, PLAT, PEF, PPFT, PCFNO, PCFNOG, PCFSPEC, &
                    KSPMH_MAP, KMECH_MAP, PCONV_FAC, OCONVERSION, PFLUX)

!***********************************************************************
!   THIS PROGRAM DOES CHEMICAL SPECIATION AND MECHANISM CONVERSION.
!   THE OUTPUT FROM MEGAN.F IS CONVERTED FROM 20 TO 150 SPECIES WHICH
!   ARE THEN LUMPED ACCORDING TO THE MECHANISM ASSIGNED IN THE RUN SCRIPT.  
!   THE PROGRAM LOOPS THROUGH ALL TIMESTEPS OF THE INPUT FILE.
!
!   PROCEDURE
!   1) FILE SET UP AND ASSIGN I/O PARAMETERS
!   2) CONVERSION FROM MGN 20 TO SPECIATED 150
!   3) CONVERSION FROM SPECIATED SPECIES TO MECHANISM SPECIES
!   4) CONVERT TO TONNE/HOUR IF NEEDED
!
!   THE INPUT FILE GIVES VARIABLES IN UNITS OF G-SPECIES/SEC.
!   ALL OUTPUTS ARE IN MOLE/SEC OR TONNE/HR DEPENDING ON ASSIGNMENT.
!
!
!   INPUT:
!           1) MEGAN OUTPUT (NETCDF-IOAPI)
!
!   OUTPUT:
!           1) MEGAN SPECIATION OR MECHANISM SPECIES (NETCDF-IOAPI)
!
!   REQUIREMENT:
!      REQUIRES LIBNETCDF.A AND LIBIOAPI.A TO COMPILE
!
!      SETENV MGERFILE    <DEFANGED_INPUT MEGAN OUTPUT FOR EMISSION ACTIVITY FACTORS>  
!      SETENV OUTPFILE    <OUTPUT SPECIATED EMISSION>
!
!   CALLS:  CHECKMEM
!
!   ORIGINALLY CREATED BY JACK CHEN 11/04 FOR MEGAN V.0
!   FOR MEGAN V2.0 CREATED BY TAN 12/01/06
!   FOR MEGAN V2.1 CREATED BY XUEMEI WANG 11/04/07
!   FOR MEGAN V2.1 TO USE 150 SPECIES CREATED BY XUEMEI WANG 09/30/09
!
!  HISTORY:
!  08/14/07 TAN    - MOVE TO MEGANV2.02 WITH NO UPDATE
!  08/29/07 MODIFIED BY A. GUENTHER TO CORRECT ERROR IN ASSIGNING   
!           EMISSION FACTOR. THIS VERSION IS CALLED MEGANV2.03
!  10/29/07 MODIFIED BY A. GUENTHER TO CORRECT OMISSION OF DIURNAL VARIATION    
!           FACTOR. THIS VERSION IS CALLED MEGANV2.04
!  11/04/07 MODIFIED BY XUEMEI WANG TO GIVE TWO OPTIONS FOR MAP OR LOOKUP TABLE FOR
!           THE EMISSION FACTORS. ALSO GIVES OPTIONS FOR DIFFERENT CHEMICAL MECHANISMS
!           IN THE CODE: USER MODIFIES THE EXTERNAL SCRIPT TO ASSIGN MECHANISM.
!           THIS VERSION IS CALLED MEGANV2.1.0
!  06/04/08 MODIFIED BY J. LEE-TAYLOR TO ACCEPT VEGETATION-DEPENDENT SPECIATION FACTORS
!           IN TABLE FORMAT (RESHAPE TABLES) RATHER THAN FROM DATA STATEMENTS.
!  09/30/08  MODIFIED BY XUEMEI WANG TO GIVE OPTIONS FOR INPUT FILE AND TEST DIFFERENT MECHANISMS
!  09/27/11  TAN&XUEMEI MEGANV2.10 INCLUDES SOIL NOX ADJUSTMENT AND A LOT OF UPDATES
!  20/12/14  P. TULET - ON-LINE COUPLING IN THE ISBA/SURFEX SCHEME. ALL INIT VARIABLES HAS BEEN 
!            MOVED IN INIT_MEGANN.F90.
!***********************************************************************

USE MODD_MGN2MECH
USE MODD_MEGAN

USE MODE_SOILNOX

USE MODI_INDEX1

IMPLICIT NONE

INTEGER, INTENT(IN) :: KDATE        !  DATE YYYYDDD
REAL, DIMENSION(:), INTENT(IN)  :: PLAT   !I LATITUDE OF GRID CELL
REAL, DIMENSION(:,:),INTENT(IN) :: PPFT    !I PFT FACTOR ARRAY (NRTYP 1-16 IN THE FIRST DIM)
REAL, DIMENSION(:,:),INTENT(IN) :: PEF !I PFT FACTOR ARRAY (NRTYP 1-16 IN THE FIRST DIM)
REAL, DIMENSION(:), INTENT(IN)  :: PCFNO    !I NO CORRECTION FACTOR
REAL, DIMENSION(:), INTENT(IN)  :: PCFNOG   !I NO CORRECTION FACTOR FOR GRASS
REAL, DIMENSION(:,:), INTENT(IN) :: PCFSPEC
LOGICAL, INTENT(IN) :: OCONVERSION
INTEGER, DIMENSION(:), INTENT(IN) :: KSPMH_MAP
INTEGER, DIMENSION(:), INTENT(IN) :: KMECH_MAP
REAL, DIMENSION(:), INTENT(IN) :: PCONV_FAC
REAL, DIMENSION(:,:),INTENT(INOUT)  :: PFLUX   !IO  EMISSION FLUX IN MOL/M2/S

!***********************************************************************
!   THIS PROGRAM DOES CHEMICAL SPECIATION AND MECHANISM CONVERSION.
!...  PROGRAM I/O FILES
! PROGRAM NAME
! INPUT MEGAN ER FILE
!      CHARACTER*16 :: MGNERS   = 'MGNERS'    ! INPUT MEGAN ER FILE LOGICAL NAME
! NETCDF FILE
!      CHARACTER*16 :: EFMAPS   = 'EFMAPS'    !  EFMAP INPUT FILE  NAME
!      CHARACTER*16 :: PFTS16   = 'PFTS16'    ! INPUT PFT FILE LOGICAL
! OUTPUT FILE
!      CHARACTER*16 :: MGNOUT   = 'MGNOUT'    ! OUTPUT FILE LOGICAL NAME
! PARAMETERS FOR FILE UNITS
!      INTEGER :: LOGDEV                      ! LOGFILE UNIT NUMBER

!...  PROGRAM I/O PARAMETERS
!...  EXTERNAL PARAMETERS

REAL, DIMENSION(N_SPCA_SPC,SIZE(PFLUX,2)) :: ZTMPER     ! TEMP EMISSION BUFFER
REAL, DIMENSION(SIZE(PFLUX,1),SIZE(PFLUX,2)) :: ZOUTER     ! OUTPUT EMISSION BUFFER
REAL, DIMENSION(SIZE(PLAT)) :: ZTMP1, ZTMP2, ZTMP3, ZTMP4
REAL :: ZTMO1, ZTMO2, ZTMO3
REAL :: Z2CRATIO

!...  INTERNAL PARAMETERS
! INTERNAL PARAMTERS (STATUS AND BUFFER)
INTEGER, DIMENSION(SIZE(PLAT)) :: ILEN, IDAY
INTEGER :: JS, JJ, JI, JM, JN        ! COUNTERS
INTEGER :: JMPMG, JMPSP, JMPMC       ! COUNTERS
INTEGER :: INO
INTEGER :: INP, IN_SCON_SPC

!***********************************************************************

!=======================================================================
!...  BEGIN PROGRAM
!=======================================================================

INP = SIZE(PLAT)
IN_SCON_SPC = SIZE(KSPMH_MAP)

! CHANGE THE UNIT ACCORDING TO TONPHR FLAG
!      IF ( TONPHR ) THEN
!         UNITS3D(1:NVARS3D) = 'TONS/HR'
!      ELSE
!         UNITS3D(1:NVARS3D) = 'MG/M*M/H'
!      ENDIF
!
!      DO S = 1, NVARS3D
!         PRINT*,'OUTPUT VARIABLE:',VNAME3D(S),UNITS3D(S)
!      ENDDO

!      CALL NAMEVAL ( MGNERS , MESG )  ! GET INPUT FILE NAME AND PATH
!      FDESC3D( 2 ) = 'INPUT MEGAN FILE: '//TRIM(MESG)

!...  ALLOCATE MEMORY

!.....2) CONVERSION FROM MGN 20 TO SPECIATED 150
!-----------------------------------------------------------------------
ZTMPER = 0.
ZOUTER = 0.

INO = INDEX1('NO',CMGN_SPC)

!...  LOOP THROUGH TIME
DO JS = 1, N_SMAP_SPC

  JMPMG = NMG20_MAP(JS)
  JMPSP = NSPCA_MAP(JS)
!          PRINT*,'CONVERT '//MGN_SPC(NMPMG)//' TO '//SPCA_SPC(NMPSP)

  IF ( JMPMG.NE.INO ) THEN

    !...  NOT NO
    IF ( XEF_ALL(1,JMPMG).LT.0. ) THEN

      !... USE EFMAPS
      ZTMP1(:) = 0.
      ZTMP2(:) = 0.
      DO JM = 1,N_MGN_PFT
        ZTMP1 = ZTMP1 + PPFT(JM,:)
        ZTMP2 = ZTMP2 + XEFFS_ALL(JM,JMPSP) * PPFT(JM,:)
      ENDDO
      WHERE( ZTMP1(:).EQ.0. )
        ZTMPER(JMPSP,:) = 0.
      ELSEWHERE
        ZTMPER(JMPSP,:) = PCFSPEC(JMPMG,:) * PEF(JMPMG,:) * ZTMP2(:)/ZTMP1(:)
      ENDWHERE

    ELSE

      !... USE PFT-EF
      ZTMP3(:) = 0.0
      ZTMP4(:) = 0.0
      DO JM = 1,N_MGN_PFT
        !ZTMP3 = ZTMP3 + XEF_ALL(JM,JMPMG) * XEFFS_ALL(JM,JMPSP) * PPFT(JM,:)/100.
        ZTMP4(:) = ZTMP4(:) + PPFT(JM,:) 
        ZTMP3(:) = ZTMP3(:) + XEF_ALL(JM,JMPMG) * XEFFS_ALL(JM,JMPSP) * PPFT(JM,:)  ! bug S. Oumami
      ENDDO
      WHERE( ZTMP4(:).EQ.0. )
        ZTMPER(JMPSP,:) = 0.
      ELSEWHERE
        ZTMPER(JMPSP,:) = PCFSPEC(JMPMG,:) * ZTMP3(:) / ZTMP4(:)
      ENDWHERE


    ENDIF

  ELSE IF ( JMPMG.EQ.INO ) THEN

!!-----------------NO STUFF-----------------------

    CALL GROWSEASON(KDATE, PLAT, IDAY, ILEN)

    DO JJ = 1,SIZE(PPFT,2)

      ! CHECK FOR GROWING SEASON
      IF ( IDAY(JJ).EQ.0 ) THEN

        ! NON GROWING SEASON
        ! CFNOG FOR EVERYWHERE
        ! OVERRIDE CROP WITH GRASS WARM = 14
        IF ( XEF_ALL(1,INO).LT.0. ) THEN

          ! WITH EFMAPS
          ZTMO1 = 0.
          ZTMO2 = 0.
          DO JM = 1,14
            ZTMO1 = ZTMO1 + PPFT(JM,JJ)
            ZTMO2 = ZTMO2 + XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ)
          ENDDO
          DO JM = 15,N_MGN_PFT
            ZTMO1 = ZTMO1 + PPFT(JM,JJ)
            Z2CRATIO = XEF_ALL(14,INO)/XEF_ALL(JM,INO)
            ZTMO2 = ZTMO2 + XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ) * Z2CRATIO
          ENDDO
          IF ( ZTMO1.EQ.0. ) THEN
            ZTMPER(JMPSP,JJ) = 0.
          ELSE
            !ZTMPER(JMPSP,JJ) = &
            !   PCFSPEC(INO,JJ) * PEF(INO,JJ) * PCFNOG(JJ) * ZTMO2/ZTMO1
            ZTMPER(JMPSP,JJ) = &
               PCFSPEC(INO,JJ) * PEF(INO,JJ) * PCFNOG(JJ) * ZTMO2/ZTMO1 * XN2NO 
          ENDIF

        ELSE

          ! WITHOUT EFMAPS
          ZTMO3 = 0.0
          DO JM = 1,14
            ZTMO3 = ZTMO3 + XEF_ALL(JM,INO) * XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ)/100.
          ENDDO
          DO JM = 15,N_MGN_PFT
            ZTMO3 = ZTMO3 + XEF_ALL(14,INO) * XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ)/100.
          ENDDO
          !ZTMPER(JMPSP,JJ) = PCFSPEC(INO,JJ) * PCFNOG(JJ) * ZTMO3
          ZTMPER(JMPSP,JJ) = PCFSPEC(INO,JJ) * PCFNOG(JJ) * ZTMO3 * XN2NO
            
        ENDIF

      ELSE IF ( IDAY(JJ).GT.0 .AND. IDAY(JJ).LE.366 ) THEN
                
        ! GROWING SEASON
        ! CFNOG FOR EVERYWHERE EXCEPT CROPS
        ! CFNO FOR CROP AND CORN
        IF ( XEF_ALL(1,INO).LT.0. ) THEN

          ! WITH EFMAPS
          ZTMO1 = 0.
          ZTMO2 = 0.
          DO JM = 1,14
            ZTMO1 = ZTMO1 + PPFT(JM,JJ)
            ZTMO2 = ZTMO2 + XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ) * PCFNOG(JJ)
          ENDDO
          DO JM = 15,N_MGN_PFT
            ZTMO1 = ZTMO1 + PPFT(JM,JJ)
            ZTMO2 = ZTMO2 + XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ) * PCFNO(JJ)
          ENDDO
          IF ( ZTMO1.EQ.0. ) THEN
            ZTMPER(JMPSP,JJ) = 0.
          ELSE
            !ZTMPER(JMPSP,JJ) = PCFSPEC(INO,JJ) * PEF(INO,JJ) * ZTMO2/ZTMO1
            ZTMPER(JMPSP,JJ) = PCFSPEC(INO,JJ) * PEF(INO,JJ) * ZTMO2/ZTMO1 * XN2NO
          ENDIF

        ELSE
                  
          ! WITHOUT EFMAPS
          ZTMO3 = 0.0
          DO JM = 1,14
            ZTMO3 = ZTMO3 + &
              XEF_ALL(JM,INO) * XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ)/100. * PCFNOG(JJ)
          ENDDO
          DO JM = 15,N_MGN_PFT
            ZTMO3 = ZTMO3 + &
              XEF_ALL(JM,INO) * XEFFS_ALL(JM,JMPSP) * PPFT(JM,JJ)/100. * PCFNO(JJ)
          ENDDO
          !ZTMPER(JMPSP,JJ) = PCFSPEC(INO,JJ) * ZTMO3
          ZTMPER(JMPSP,JJ) = PCFSPEC(INO,JJ) * ZTMO3 * XN2NO
        ENDIF

      ELSE

        WRITE(*,*) "MGN2MECH: BAD IDAY"
        STOP

      ENDIF

    ENDDO  !DO R = 1,NROWS

!-----------------END OF NO----------------------
  ENDIF     !IF ( NMPMG .NE. INO ) THEN

ENDDO ! END SPECIES LOOP

!-----------------------------------------------------------------------
!.....3) CONVERSION FROM SPECIATED SPECIES TO MECHANISM SPECIES    
!-----------------------------------------------------------------------
!       !  CONVERT FROM UG/M^2/HR TO MOL/M^2/S USING THEIR MW

DO JS = 1, N_SPCA_SPC
  ZTMPER(JS,:) = ZTMPER(JS,:) / XSPCA_MWT(JS) * XUG2G / XHR2SEC
ENDDO
!
   ! LUMPING TO MECHANISM SPECIES
!
IF ( OCONVERSION ) THEN

  DO JS = 1, IN_SCON_SPC

    JMPSP = KSPMH_MAP(JS)         ! MAPPING VALUE FOR SPCA
    JMPMC = KMECH_MAP(JS)         ! MAPPING VALUE FOR MECHANISM
    ZOUTER(JMPMC,:) = ZOUTER(JMPMC,:) + ( ZTMPER(JMPSP,:) * PCONV_FAC(JS) )
!            ! UNITS OF THESE SPECIES ARE IN MOLE/S     ------> MOLE/MÂ²/S

  ENDDO ! END SPECIES LOOP

ELSE
  !          ! GET ALL 150 SPECIES INTO THE OUTPUT ARRAY
  ZOUTER(:,:) = ZTMPER(:,:)
  !          ! UNITS OF THESE SPECIES ARE IN MOLE/M2/S

ENDIF
PFLUX(:,:) = ZOUTER(:,:)

END SUBROUTINE MGN2MECH
