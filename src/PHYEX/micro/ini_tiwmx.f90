SUBROUTINE INI_TIWMX
  USE PARKIND1, ONLY  : JPRB
  USE MODD_CST, ONLY  :  XALPW,XBETAW,XGAMW,XALPI,XBETAI,XGAMI,  &
       &  XTT,XRV,XLVTT,XLSTT
! Include function definitions
  USE MODE_TIWMX_FUN, ONLY : ESATW,DESDTW,ESATI,DESDTI,AA2,AA2W,AM3,AF3,BB3,BB3W,REDIN
! Only the tables, _NOT_ the functions!
  USE MODE_TIWMX, ONLY : XNDEGR,NSTART,NSTOP,ESTABW,DESTABW,ESTABI,DESTABI, &
       & A2TAB,BB3TAB,AM3TAB,AF3TAB,A2WTAB,BB3WTAB,REDINTAB
  USE YOMHOOK, ONLY : LHOOK, DR_HOOK
  IMPLICIT NONE

  INTEGER :: JK
  REAL :: ZTEMP

  REAL(KIND=JPRB) :: ZHOOK_HANDLE

  IF (LHOOK) CALL DR_HOOK('INI_TIWMX',0,ZHOOK_HANDLE)

  DO JK=NSTART,NSTOP
     ZTEMP = REAL(JK)/XNDEGR

     ESTABW(JK) = ESATW(ZTEMP)
     DESTABW(JK) = DESDTW(ZTEMP)

     ESTABI(JK) = ESATI(ZTEMP)
     DESTABI(JK) = DESDTI(ZTEMP)

!    Functions for ice crystals or water droplets:
     A2TAB(JK) = AA2(ZTEMP)
     A2WTAB(JK) = AA2W(ZTEMP)
     AM3TAB(JK) = AM3(ZTEMP)
     AF3TAB(JK) = AF3(ZTEMP)
     BB3TAB(JK) = BB3(ZTEMP)
     BB3WTAB(JK) = BB3W(ZTEMP)
     REDINTAB(JK) = REDIN(ZTEMP)

     IF (ZTEMP.GT.273.15) THEN
!       Avoid ice calculation above freezing level:
        ESTABI(JK) = ESATW(ZTEMP)
        DESTABI(JK) = DESDTW(ZTEMP)

!       Functions for ice crystal growth:  
        A2TAB(JK) = AA2(ZTEMP)
        A2WTAB(JK) = AA2W(ZTEMP)
        AM3TAB(JK) = 0.
        AF3TAB(JK) = 0.
        BB3TAB(JK) = BB3(ZTEMP)
        BB3WTAB(JK) = BB3W(ZTEMP)
     ENDIF
  END DO

  IF (LHOOK) CALL DR_HOOK('INI_TIWMX',1,ZHOOK_HANDLE)

END SUBROUTINE INI_TIWMX
