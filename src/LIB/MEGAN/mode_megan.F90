MODULE MODE_MEGAN
!
USE MODD_MEGAN
!
USE MODI_SOLARANGLE
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
!
!   INPUT AND OUTPUT FILES MUST BE SELECTED BEFORE STARTING THE PROGRAM
!
!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
!!
!   INPUT VARIBLES
!
!   DAY                  JULIAN DAY
!   LAT                  LATITUDE
!   HOUR                 HOUR OF THE DAY
!   TC                   TEMPERATURE [C]
!   PPFD           INCOMING PHOTOSYNTHETIC ACTIVE RADIATION [UMOL/M2/S1]
!   WIND                 WIND SPEED [M S-1]
!   HUMIDITY             RELATIVE HUMIDITY [%]
!   CANTYPYE             DEFINES SET OF CANOPY CHARACTERISTICS
!   LAI                  LEAF AREA INDEX [M2 PER M2 GROUND AREA]
!   DI                   ???
!   PRES                 PRESSURE [PA]
!
!   USED VARIABLES:
!
!   PPFDFRAC             FRACTION OF TOTAL SOLAR RADIATION THAT IS PPFD
!   SOLAR                SOLAR RADIATION [W/M2]
!   MAXSOLAR             MAXIMUM OF SOLAR RADIATION
!   BETA                 SIN OF SOLAR ANGLE ABOVE HORIZON
!   SINBETA              SOLAR ANGLE ABOVE HORIZON
!   TAIRK0               ABOVE CANOPY AIR TEMPERATURE [K]
!       TAIRK            ARRAY OF CANOPY AIR TEMPERATURE [K]
!   WS0                  ABOVE CANOPY WIND SPEED [M/S]
!   WS                   ARRAY OF CANOPY WIND SPEED [M/S]
!   HUMIDAIRPA0          ABOVE CANOPY AMBIENT HUMIDITY [PA]
!       HUMIDAIRPA       ARRAY OF CANOPY AMBIENT HUMIDITY IN [PA]
!   STOMATADI            INDEX FOR WATER STATUS OF LEAVES. USED TO MODIFY STOMATAL CONDUCTANCE
!   TRANSMIS             TRANSMISSION OF PPFD THAT IS DIFFUSE
!   DIFFFRAC             FRACTION OF PPFD THAT IS DIFFUSE
!   PPFDFRAC             FRACTION OF SOLAR RAD THAT IS PPFD
!   TRATE                STABILITY OF BOUNDARY ???
!   SH                   SENSIBLE HEAT FLUX ???
!       VPGAUSWT         ARRAY OF GAUSSIAN WEIGHTING FACTORS
!   VPGAUSDIS            ARRAY OF GAUSSIAN WEIGHTING FACTORS
!   VPSLWWT              ARRAY OF GAUSSIAN WEIGHTING FACTORS
!   SUNFRAC              ARRAY OF THE FRACTION OF SUN LEAVES. I = 1 IS THE TOP CANOPY LAYER, 2 IS THE NEXT LAYER, ETC.
!   SUNPPFD              ARRAY OF INCOMING (NOT ABSORBED) PPFD ON A SUN LEAF [UMOL/M2/S]
!   SHADEPPFD            ARRAY OF INCOMING (NOT ABSORBED) PPFD ON A SHADE LEAF [UMOL/M2/S]
!   SUNQV                ARRAY OF VISIBLE RADIATION (IN AND OUT) FLUXES ON SUN LEAVES
!   SHADEQV              ARRAY OF ABSORBED VISIBLE RADIATION (IN AND OUT) FLUXES ON SHADE LEAVES
!   SUNQN                ARRAY OF ABSORBED NEAR IR RADIATION (IN AND OUT) FLUXES ON SUN LEAVES
!   SHADEQN              ARRAY OF ABSORBED NEAR IR RADIATION (IN AND OUT) FLUXES ON SHADE LEAVES
!   SUNLEAFTK            ARRAY OF LEAF TEMPERATURE FOR SUN LEAVES [K]
!   SUNLEAFSH            ARRAY OF SENSIBLE HEAT FLUX FOR SUN LEAVES [W/M2]
!   SUNLEAFLH            ARRAY OF LATENT HEAT FLUX FOR SUN LEAVES [W/M2]
!   SUNLEAFIR            ARRAY OF INFRARED FLUX FOR SUN LEAVES [W/M2]
!   SHADELEAFTK          ARRAY OF LEAF TEMPERATURE FOR SHADE LEAVES [K]
!   SHADELEAFSH          ARRAY OF SENSIBLE HEAT FLUX FOR SHADE LEAVES [W/M2]
!   SHADELEAFLH          ARRAY OF LATENT HEAT FLUX FOR SHADE LEAVES [W/M2]
!   SHADELEAFIR          ARRAY OF INFRARED FLUX FOR SHADE LEAVES [W/M2]
!   QBABSV, QBABSN       ABSORBED DIRECT BEAM LIGHT FOR VISIBLE AND NEAR INFRA RED
!   QDABSV, QDABSN       ARRAY OF ABSORBED DIFFUSE LIGHT FOR VISIBLE AND NEAR INFRA RED
!   QSABSV, QSABSN       ARRAY OF ABSORBED SCATTERED LIGHT FOR VISIBLE AND NEAR INFRA RED
!   QBEAMV, QBEAMN       ABOVE CANOPY BEAM (DIRECT) LIGHT FOR VISIBLE AND NEAR INFRA RED
!   QDIFFV, QDIFFN       ABOVE CANOPY DIFFUSE LIGHT FOR VISIBLE AND NEAR INFRA RED
!       EA1PLAYER        ARRAY OF EMISSION ACTIVITY OF LIGHT PER LAYER
!       EA1TLAYER        ARRAY OF EMISSION ACTIVITY OF TEMPERATURE PER LAYER
!       EA1LAYER         ARRAY OF COMPANIED EMISSION ACTIVITY
!       EA1PCANOPY       TOTAL EMISSION ACTIVITY OF LIGHT
!       EATILAYER        ARRAY OF EMISSION ACTIVITY OF TEMPERATURE INDENDENT PER LAYER
!   EA1TCANOPY           TOTAL EMISSION ACTIVITY OF TEMPERATURE DEPEDENT FACTOR
!   PEA1CANOPY            TOTAL COMPANIED EMISSION ACTIVITY
!   PEATICANOPY           TOTAL EMISSION ACTIVITY OF TEMPERATURE INDEPEDENT FACTOR
!   CALCBETA             FUNCTION: CALCULATION OF SOLAR ZENITH ANGLE
!   WATERVAPPRES         FUNCTION: CONVERT WATER MIXING RATIO (KG/KG) TO WATER VAPOR PRESSURE
!   STABILITY            FUNCTION: TEMPERATURE LAPSE RATE
!   EA1T99               FUNCTION: TEMPERATURE DEPENDENCE ACTIVITY FACTOR FOR EMISSION TYPE 1
!   EA1P99               FUNCTION: LIGHT DEPENDENCE ACTIVITY FACTOR FOR EMISSION
!   EALTI                FUNCTION: TEMPERATURE INDEPENDENCE ACTIVITY FACTOR FOR EMISSION
!   DISTOMATA            FUNCTION:
!   CALCECCENTRICITY     FUNCTION:
!
!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
!
CONTAINS
!
SUBROUTINE GAMME_CE(KDATE, KTIME, PCANOPYCHAR, KCANTYPE, HSPCNAME, &
                    PPFD24, PPFD240, PT24, PT240, PDI,             &
                    PPFD0, PLAT, PLONG, PTC, PWIND, PHUMIDITY,     &
                    PLAI, PRES, PEA1CANOPY, PEATICANOPY )
!
IMPLICIT NONE
! INPUT
INTEGER,INTENT(IN) :: KDATE, KTIME, KCANTYPE
REAL,DIMENSION(:,:),INTENT(IN) :: PCANOPYCHAR
CHARACTER(LEN=16), INTENT(IN) :: HSPCNAME
!
REAL, INTENT(IN) :: PPFD24, PPFD240
REAL, INTENT(IN) :: PT24, PT240, PDI
!
REAL, DIMENSION(:), INTENT(IN) :: PPFD0
REAL, DIMENSION(:), INTENT(IN) :: PLONG, PLAT
REAL, DIMENSION(:), INTENT(IN) :: PTC, PRES, PWIND, PHUMIDITY, PLAI
! ARRAY OF CANOPY CHARACTERISTICS FOR KRTYP OF CANOPY TYPE
! OUTPUT
REAL, DIMENSION(:), INTENT(OUT) ::  PEA1CANOPY, PEATICANOPY
!
! LOCAL VARIABLES
REAL, DIMENSION(NLAYERS) :: ZVPGAUSWT, ZVPGAUSDIS2, ZVPGAUSDIS
!
REAL, DIMENSION(SIZE(PLONG),NLAYERS) :: ZEA1LAYER, ZEATILAYER, ZVPSLWWT
REAL, DIMENSION(SIZE(PLONG),NLAYERS) :: ZSUNFRAC, ZSUNQV, ZSHADEQV, ZSUNQN, ZSHADEQN, &
                                        ZSUNPPFD, ZSHADEPPFD, ZSUNLEAFTK, ZSHADELEAFTK, &
                                        ZSUNLEAFSH, ZSHADELEAFSH
!
REAL, DIMENSION(SIZE(PLONG)) :: ZHOUR, ZSINBETA, ZSOLAR,    &
                                ZMAXSOLAR, ZQDIFFV, ZQBEAMV, ZQDIFFN, ZQBEAMN, &
                                ZHUMIDAIRPA0, ZTRATE
!
REAL :: ZSTOMATADI
INTEGER, DIMENSION(SIZE(PLONG)) :: IDAY
INTEGER :: JI, JJ
!
!---------------------------HEADER OVER--------------------------------
!
IDAY(:) = MOD(KDATE,1000)
! CONVERT FROM XXXXXX FORMAT TO XX.XX (SOLAR HOUR)
! HOUR = 0 -> 23.XX
! SOLAR HOUR
ZHOUR(:) = KTIME/10000. + PLONG(:)/15.
!
WHERE ( ZHOUR(:).LT.0. ) 
  ZHOUR(:) = ZHOUR(:) + 24.
  IDAY (:) = IDAY (:) - 1
ELSEWHERE ( ZHOUR.GT.24. )
  ZHOUR(:) = ZHOUR(:) - 24.
  IDAY (:) = IDAY (:)  + 1
END WHERE
!
CALL SOLARANGLE(IDAY, ZHOUR, PLAT, ZSINBETA)
!
ZSOLAR   (:) = PPFD0(:)/2.25
ZMAXSOLAR(:) = ZSINBETA(:) * XSOLARCONSTANT * CALCECCENTRICITY(IDAY(:))
CALL SOLARFRACTIONS(ZSOLAR, ZMAXSOLAR, ZQDIFFV, ZQBEAMV, ZQDIFFN, ZQBEAMN)
!
CALL GAUSSIANINTEGRATION(ZVPGAUSWT, ZVPGAUSDIS, ZVPGAUSDIS2)
!
CALL CANOPYRAD(KCANTYPE, PCANOPYCHAR, ZVPGAUSDIS, &
               PLAI, ZSINBETA, ZQBEAMV, ZQDIFFV, ZQBEAMN, ZQDIFFN, &
               ZSUNFRAC, ZSUNQV, ZSHADEQV, ZSUNQN, ZSHADEQN, &
               ZSUNPPFD, ZSHADEPPFD)
!
ZTRATE      (:) = STABILITY(PCANOPYCHAR, KCANTYPE, ZSOLAR)
!
ZSTOMATADI = DISTOMATA(PDI)
!
ZHUMIDAIRPA0(:) = WATERVAPPRES(XWATERAIRRATIO, PHUMIDITY, PRES)
!
CALL CANOPYEB(KCANTYPE, PCANOPYCHAR, ZVPGAUSDIS, ZSTOMATADI,            &
              PTC, PWIND, ZTRATE, ZHUMIDAIRPA0,                         &
              ZSUNQV, ZSHADEQV, ZSUNQN, ZSHADEQN, ZSUNPPFD, ZSHADEPPFD, &
              ZSUNLEAFTK, ZSHADELEAFTK, ZSUNLEAFSH, ZSHADELEAFSH)

!ZEA1TCANOPY(:) = 0.
!ZEA1PCANOPY(:) = 0.
PEA1CANOPY (:) = 0.
PEATICANOPY(:) = 0.

DO JI = 1,SIZE(ZEA1LAYER,2)   


  !ZEA1TLAYER(:,JI) = EA1T99(ZSUNLEAFTK  (:,JI), PT24, PT240, HSPCNAME) * ZSUNFRAC(:,JI) + &
  !                   EA1T99(ZSHADELEAFTK(:,JI), PT24, PT240, HSPCNAME) *(1.-ZSUNFRAC(:,JI))

! PSTD = 200 FOR SUN LEAVES
! PSTD = 50 FOR SHADE LEAVES
  !ZEA1PLAYER(:,JI) = EA1P99(ZSUNPPFD(:,JI), PPFD24*0.5,  PPFD240*0.5,  XPSTD_SUN)   *     ZSUNFRAC(:,JI) + &
  !                 EA1P99(ZSHADEPPFD(:,JI), PPFD24*0.16, PPFD240*0.16, XPSTD_SHADE) * (1.-ZSUNFRAC(:,JI)) 

  ZEA1LAYER(:,JI) = EA1T99(HSPCNAME   , PT24       , PT240       , ZSUNLEAFTK  (:,JI)) * &
                    EA1P99(XPSTD_SUN  , PPFD24*0.5 , PPFD240*0.5 , ZSUNPPFD    (:,JI)) * ZSUNFRAC(:,JI) +  &
                    EA1T99(HSPCNAME   , PT24       , PT240       , ZSHADELEAFTK(:,JI)) * &
                    EA1P99(XPSTD_SHADE, PPFD24*0.16, PPFD240*0.16, ZSHADEPPFD  (:,JI) ) * (1.-ZSUNFRAC(:,JI))
   
  ZEATILAYER(:,JI) = EALTI99(HSPCNAME, ZSUNLEAFTK  (:,JI)) * ZSUNFRAC(:,JI) +  &
                     EALTI99(HSPCNAME, ZSHADELEAFTK(:,JI)) * (1-ZSUNFRAC(:,JI))

ENDDO

CALL WEIGHTSLW(ZVPGAUSDIS, PLAI, ZVPSLWWT)
!
DO JJ = 1,SIZE(PEA1CANOPY)
!  ZEA1PCANOPY(JJ) = SUM(ZEA1PLAYER(JJ,:) * ZVPSLWWT(JJ,:) * ZVPGAUSWT(:) )
!  ZEA1TCANOPY(JJ) = SUM(ZEA1TLAYER(JJ,:) * ZVPSLWWT(JJ,:) * ZVPGAUSWT(:) )
  PEA1CANOPY (JJ) = SUM(ZEA1LAYER (JJ,:) * ZVPSLWWT(JJ,:) * ZVPGAUSWT(:) )
  PEATICANOPY(JJ) = SUM(ZEATILAYER(JJ,:) * ZVPSLWWT(JJ,:) * ZVPGAUSWT(:) )
! THIS QUANTITY IS APPARENTLY NOT PASSED OUT OF THE SUBROUTINE
!  ZSH(JJ) = SUM( ( ZSUNLEAFSH  (JJ,:) * ZSUNFRAC(:,JJ) +  &
!                   ZSHADELEAFSH(JJ,:) * (1 - ZSUNFRAC(:,JJ))) * PLAI(:) * ZVPGAUSWT(:) )  
ENDDO

PEA1CANOPY(:)  = PEA1CANOPY(:) * XCCE * PLAI(:)

END SUBROUTINE  GAMME_CE

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE GAUSSIANINTEGRATION
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE GAUSSIANINTEGRATION(PWEIGHTGAUSS, PDISTGAUSS, PDISTGAUSS2)
!
IMPLICIT NONE
!
REAL,DIMENSION(:),INTENT(OUT) :: PWEIGHTGAUSS, PDISTGAUSS, PDISTGAUSS2
!
! LOCAL VARIABLES
INTEGER ::  JI
!--------------------------------------------------------------------
!
IF ( NLAYERS.EQ.1 ) THEN
  PWEIGHTGAUSS(1) = 1
  PDISTGAUSS  (1) = 0.5
  PDISTGAUSS2 (1) = 1
ELSEIF ( NLAYERS.EQ.3 ) THEN
  PWEIGHTGAUSS(1) = 0.277778
  PWEIGHTGAUSS(2) = 0.444444
  PWEIGHTGAUSS(3) = 0.277778
  PDISTGAUSS(1)   = 0.112702
  PDISTGAUSS(2)   = 0.5
  PDISTGAUSS(3)   = 0.887298
  PDISTGAUSS2(1)  = 0.277778
  PDISTGAUSS2(2)  = 0.722222
  PDISTGAUSS2(3)  = 1
ELSEIF ( NLAYERS.EQ.5 ) THEN
  PWEIGHTGAUSS(1) = 0.1184635
  PWEIGHTGAUSS(2) = 0.2393144
  PWEIGHTGAUSS(3) = 0.284444444
  PWEIGHTGAUSS(4) = 0.2393144
  PWEIGHTGAUSS(5) = 0.1184635
  PDISTGAUSS(1)   = 0.0469101
  PDISTGAUSS(2)   = 0.2307534
  PDISTGAUSS(3)   = 0.5
  PDISTGAUSS(4)   = 0.7692465
  PDISTGAUSS(5)   = 0.9530899
  PDISTGAUSS2(1)  = 0.1184635
  PDISTGAUSS2(2)  = 0.3577778
  PDISTGAUSS2(3)  = 0.6422222
  PDISTGAUSS2(4)  = 0.881536
  PDISTGAUSS2(5)  = 1.0
ELSE
  DO JI = 1,NLAYERS
    PWEIGHTGAUSS(JI) = 1. / NLAYERS
    PDISTGAUSS  (JI) = (JI - 0.5) / NLAYERS
    PDISTGAUSS2 (JI) = JI / NLAYERS
  ENDDO
ENDIF

END SUBROUTINE GAUSSIANINTEGRATION

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE WEIGHTSLW
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE WEIGHTSLW(PDISTGAUSS, PLAI, PSLW)

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN) ::  PLAI
REAL, DIMENSION(:), INTENT(IN) :: PDISTGAUSS

REAL, DIMENSION(:,:), INTENT(OUT) :: PSLW

! LOCAL VARIABLES
INTEGER :: JI
!--------------------------------------------------          

DO JI = 1,NLAYERS
  PSLW(:,JI) = 0.63 + 0.37 * EXP(-((PLAI(:) * PDISTGAUSS(JI)) - 1.))
ENDDO

END SUBROUTINE WEIGHTSLW

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE SOLARFRACTIONS
!   TRANSMISSION, FRACTION OF PPFD THAT IS DIFFUSE,
!        FRACTION OF SOLAR RAD THAT IS PPFD
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE SOLARFRACTIONS(PSOLAR, PMAXSOLAR, PQDIFFV, PQBEAMV, PQDIFFN, PQBEAMN)
!
IMPLICIT NONE
!
!      INTEGER,INTENT(IN) :: TIMEPERIOD
REAL, DIMENSION(:), INTENT(IN) :: PSOLAR, PMAXSOLAR
!
REAL, DIMENSION(:), INTENT(OUT) :: PQDIFFV, PQBEAMV, PQDIFFN, PQBEAMN
!
! INTERNAL VARIABLES
REAL :: ZFRACDIFF, ZPPFDFRAC, ZPPFDDIFFRAC, ZQV, ZQN
REAL ::  ZTRANSMIS
INTEGER :: JJ
!-----------------------------------------------------
!      IF (TIMEPERIOD .EQ. 1) THEN     ! DAILY TRANSMISSION
!        TRANSMIN   = 0.26
!        TRANSSLOPE= 1.655
!      ELSE                       ! HOURLY TRANSMISSION
!        TRANSMIN   = 0.26
!        TRANSSLOPE = 1.655
!      ENDIF 
DO JJ = 1,SIZE(PSOLAR)

  IF (PMAXSOLAR(JJ)<=0) THEN
    ZTRANSMIS = 0.5
  ELSEIF (PMAXSOLAR(JJ)<PSOLAR(JJ)) THEN
    ZTRANSMIS = 1.0
  ELSE
    ZTRANSMIS = PSOLAR(JJ) / PMAXSOLAR(JJ)
  ENDIF

! ESTIMATE DIFFUSE FRACTION BASED ON DAILY TRANSMISSION (RODERICK 1999, GOUDRIANN AND VAN LAAR 1994- P.33)

!      IF (TRANSMIS  > 0.81) THEN
!        FRACDIFF  = 0.05
!      ELSEIF (TRANSMIS  > TRANSMIN) THEN
!        FRACDIFF  = 0.96-TRANSSLOPE * (TRANSMIS  - TRANSMIN)
!      ELSE
!        FRACDIFF  = 0.96
!      ENDIF

! THE FRACTION OF TOTAL SOLAR RADIATION THAT IS PPFD (43% TO 55%)
! G. AND L. 84
!      PPFDFRAC  = 0.43 + FRACDIFF  * 0.12

!FRACDIFF IS BASED ON LIZASO 2005
!MODIFIED BY XUEMEI 2010-01-26 ACCORDING TO ALEX'S DOCUMENT
  ZFRACDIFF = 0.156 + 0.86/(1 + EXP(11.1*(ZTRANSMIS -0.53)))

!PPFDFRAC IS BASED ON G.L. 84
!MODIFIED BY XUEMEI 2010-01-26 ACCORDING TO ALEX'S DOCUMENT
  ZPPFDFRAC = 0.55 -ZTRANSMIS*0.12

!PPFDDIFFRAC IS BASED ON DATA IN JACOVIDES 2007
!MODIFIED BY XUEMEI 2010-01-26 ACCORDING TO ALEX'S DOCUMENT
  ZPPFDDIFFRAC = ZFRACDIFF * (1.06 + ZTRANSMIS*0.4) 

! CALCULTE  QDIFFV,QBEAMV, QDIFFN, QBEAMN IN THE SUBROUTINE
! MODIFIED BY XUEMEI 2010-01-26 ACCORDING TO ALEX'S DOCUMENT
  IF (ZPPFDDIFFRAC > 1.0) ZPPFDDIFFRAC = 1.0

  ZQV = ZPPFDFRAC * PSOLAR(JJ)
  PQDIFFV(JJ) = ZQV * ZPPFDDIFFRAC
  PQBEAMV(JJ) = ZQV - PQDIFFV(JJ)
  ZQN = PSOLAR(JJ) - ZQV
  PQDIFFN(JJ) =  ZQN * ZFRACDIFF
  PQBEAMN(JJ) =  ZQN - PQDIFFN(JJ)

ENDDO

END SUBROUTINE SOLARFRACTIONS

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE CANOPYRAD
!
!   CANOPY LIGHT ENVIRONMENT MODEL
!   CODE DEVELOPED BY ALEX GUENTHER, BASED ON SPITTERS ET AL. (1986),
!   GOUDRIAN AND LAAR (1994), LEUNING (1997)
!   INITIAL CODE 8-99, MODIFIED 7-2000 AND 12-2001
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
SUBROUTINE CANOPYRAD(KCANTYPE, PCANOPYCHAR, PDISTGAUSS,        &
           PLAI, PSINBETA, PQBEAMV, PQDIFFV, PQBEAMN, PQDIFFN, &
           PSUNFRAC, PSUNQV, PSHADEQV, PSUNQN, PSHADEQN,       &
           PSUNPPFD, PSHADEPPFD,                               &
           PQDABSV, PQDABSN, PQSABSV, PQSABSN, PQBABSV, PQBABSN)

IMPLICIT NONE

! INPUT
INTEGER, INTENT(IN) :: KCANTYPE
REAL, DIMENSION(:,:), INTENT(IN) :: PCANOPYCHAR
REAL, DIMENSION(:), INTENT(IN) :: PDISTGAUSS
!
REAL, DIMENSION(:), INTENT(IN) :: PLAI, PSINBETA, PQBEAMV, PQDIFFV,  PQBEAMN, PQDIFFN
! OUTPUT
REAL, DIMENSION(:,:), INTENT(OUT) :: PSUNFRAC, PSUNQV, PSHADEQV, &
                                     PSUNQN, PSHADEQN, PSHADEPPFD, PSUNPPFD
REAL, DIMENSION(:,:), INTENT(OUT), OPTIONAL :: PQDABSV, PQDABSN, PQSABSV, PQSABSN
REAL, DIMENSION(:), INTENT(OUT), OPTIONAL :: PQBABSV, PQBABSN

! INTERNAL VARIABLES
REAL, DIMENSION(SIZE(PQBEAMV)) :: ZKB, ZLAIDEPTH, ZQDABSVL, ZQSABSVL, ZQDABSNL, ZQSABSNL, &
                                  ZREFLBV, ZREFLBN, ZKBPV, ZKBPN, ZKDPV, ZKDPN
REAL, DIMENSION(SIZE(PQBEAMV)) :: ZQBABSV, ZQBABSN
REAL :: ZSCATV, ZSCATN, ZREFLDV, ZREFLDN,  ZKD, ZCLUSTER        
!
INTEGER :: JI, JJ
!
!---------------------------------------------------------------------
 

! SCATTERING COEFFICIENTS (SCATV,SCATN), DIFFUSE AND BEAM REFLECTION
! COEFFICIENTS (REF..) FOR VISIBLE OR NEAR IR
ZSCATV   = PCANOPYCHAR(5,KCANTYPE)
ZSCATN   = PCANOPYCHAR(6,KCANTYPE)
ZREFLDV  = PCANOPYCHAR(7,KCANTYPE)
ZREFLDN  = PCANOPYCHAR(8,KCANTYPE)
ZCLUSTER = PCANOPYCHAR(9,KCANTYPE)
!
! EXTINCTION COEFFICIENTS FOR BLACK LEAVES FOR BEAM (KB) OR DIFFUSE (KD)
ZKB(:) = ZCLUSTER * 0.5 / MAX(0.00002,PSINBETA(:))
! (0.5 ASSUMES A SPHERICAL LEAF ANGLE DISTRIBUTION (0.5 = COS (60 DEG))
ZKD = 0.8 * ZCLUSTER             
! (0.8 ASSUMES A SPHERICAL LEAF ANGLE DISTRIBUTION)

CALL CALCEXTCOEFF(ZSCATV,ZKD,PQBEAMV,ZKB,ZREFLBV,ZKBPV,ZKDPV,ZQBABSV)
CALL CALCEXTCOEFF(ZSCATN,ZKD,PQBEAMN,ZKB,ZREFLBN,ZKBPN,ZKDPN,ZQBABSN)

PSUNFRAC(:,:) = 0.
DO JI = 1,NLAYERS

! PLAI DEPTH AT THIS LAYER
  ZLAIDEPTH(:) = PLAI(:) * PDISTGAUSS(JI) 
!FRACTION OF LEAVES THAT ARE SUNLIT
  PSUNFRAC(:,JI) = EXP(-ZKB(:) * ZLAIDEPTH(:))

  CALL CALCRADCOMPONENTS(ZSCATV, ZREFLDV, PQDIFFV, PQBEAMV, ZKDPV, ZKBPV, ZKB, &
                         ZREFLBV, ZLAIDEPTH, ZQDABSVL, ZQSABSVL)

  CALL CALCRADCOMPONENTS(ZSCATN, ZREFLDN, PQDIFFN, PQBEAMN, ZKDPN, ZKBPN, ZKB, &
                         ZREFLBN, ZLAIDEPTH, ZQDABSNL, ZQSABSNL)

  PSHADEPPFD(:,JI) = (ZQDABSVL(:) + ZQSABSVL(:))    * XCONVERTSHADEPPFD / (1. - ZSCATV)
  PSUNPPFD  (:,JI) = PSHADEPPFD(:,JI) + (ZQBABSV(:) * XCONVERTSUNPPFD   / (1. - ZSCATV))
  PSHADEQV  (:,JI) = ZQDABSVL(:)    + ZQSABSVL(:)
  PSUNQV    (:,JI) = PSHADEQV(:,JI) + ZQBABSV(:)
  PSHADEQN  (:,JI) = ZQDABSNL(:)    + ZQSABSNL(:)
  PSUNQN    (:,JI) = PSHADEQN(:,JI) + ZQBABSN(:)
  IF (PRESENT(PQDABSV)) PQDABSV   (:,JI) = ZQDABSVL(:)
  IF (PRESENT(PQSABSV)) PQSABSV   (:,JI) = ZQSABSVL(:)
  IF (PRESENT(PQDABSN)) PQDABSN   (:,JI) = ZQDABSNL(:)
  IF (PRESENT(PQSABSN)) PQSABSN   (:,JI) = ZQSABSNL(:)  

ENDDO

DO JJ = 1,SIZE(PQBEAMV)

  IF ( (PQBEAMV(JJ)+PQDIFFV(JJ))<=0.001 .OR. PSINBETA(JJ)<=0.00002 .OR. PLAI(JJ)<=0.001 ) THEN
          ! DAYTIME
    ZQBABSV(JJ) = 0.
    ZQBABSN(JJ) = 0.

    PSUNFRAC  (JJ,:) = 0.2
    PSUNQN    (JJ,:) = 0.
    PSHADEQN  (JJ,:) = 0.
    PSUNQV    (JJ,:) = 0.
    PSHADEQV  (JJ,:) = 0.
    PSUNPPFD  (JJ,:) = 0.
    PSHADEPPFD(JJ,:) = 0.
    IF (PRESENT(PQDABSV)) PQDABSV(JJ,:) = 0.
    IF (PRESENT(PQSABSV)) PQSABSV(JJ,:) = 0.
    IF (PRESENT(PQDABSN)) PQDABSN(JJ,:) = 0.
    IF (PRESENT(PQSABSN)) PQSABSN(JJ,:) = 0.
  
  ENDIF

END DO

IF (PRESENT(PQBABSV)) PQBABSV(:) = ZQBABSV(:)
IF (PRESENT(PQBABSN)) PQBABSN(:) = ZQBABSN(:)

END SUBROUTINE CANOPYRAD

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE CALCEXTCOEFF
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE CALCEXTCOEFF(PSCAT, PKD, PQBEAM, PKB, PREFLB, PKBP, PKDP, PQBEAMABSORB)
!
IMPLICIT NONE
!
REAL, INTENT(IN) :: PSCAT, PKD
REAL, DIMENSION(:), INTENT(IN) :: PQBEAM, PKB
REAL, DIMENSION(:), INTENT(OUT) :: PREFLB, PKBP, PKDP, PQBEAMABSORB

! LOCAL VARIABLES
REAL :: ZP
INTEGER :: JJ
!-------------------------------------------------------------------

ZP = (1.-PSCAT)**0.5

DO JJ = 1,SIZE(PKB)

  PREFLB(JJ) = 1. - EXP((-2. * ((1.-ZP)/(1.+ZP)) * PKB(JJ)) / (1. + PKB(JJ)))

  ! EXTINCTION COEFFICIENTS
  PKBP(JJ) = PKB(JJ) * ZP
  PKDP(JJ) = PKD * ZP
  ! ABSORBED BEAM RADIATION
  PQBEAMABSORB(JJ) = PKB(JJ) * PQBEAM(JJ) * (1 - PSCAT)

ENDDO

END SUBROUTINE CALCEXTCOEFF

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE CALCRADCOMPONENTS
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE CALCRADCOMPONENTS(PSCAT, PREFLD, PQDIFF, PQBEAM, PKDP, PKBP, PKB,   &
                             PREFLB, PLAIDEPTH, PQDABS, PQSABS)

IMPLICIT NONE

REAL, INTENT(IN) :: PSCAT, PREFLD
REAL, DIMENSION(:), INTENT(IN) :: PQDIFF, PQBEAM, PKDP, PKBP, PKB, PREFLB, PLAIDEPTH
REAL, DIMENSION(:), INTENT(OUT) :: PQDABS, PQSABS
!-------------------------------------------------------------------

PQDABS(:) = PQDIFF(:) *   PKDP(:) * (1. - PREFLD)    * EXP(-PKDP(:) * PLAIDEPTH(:))
PQSABS(:) = PQBEAM(:) * ((PKBP(:) * (1. - PREFLB(:)) * EXP(-PKBP(:) * PLAIDEPTH(:))) &
                        - (PKB(:) * (1. - PSCAT)     * EXP(-PKB (:) * PLAIDEPTH(:))))

END SUBROUTINE CALCRADCOMPONENTS

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE CANOPYEB
!
!   CANOPY ENERGY BALANCE MODEL FOR ESTIMATING LEAF TEMPERATURE
!   CODE DEVELOPED BY ALEX GUENTHER, BASED ON GOUDRIAN AND LAAR (1994),
!    LEUNING (1997)
!   INITIAL CODE 8-99, MODIFIED 7-2000 AND 12-2001
!
!   NOTE: I DENOTES AN ARRAY CONTAINING A VERTICAL PROFILE THROUGH THE
!         CANOPY WITH 0
!         (ABOVE CANOPY CONDITIONS) PLUS 1 TO NUMBER OF CANOPY LAYERS
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE CANOPYEB(KCANTYPE, PCANOPYCHAR, PDISTGAUSS, PSTOMATADI,    &
            PTAIRK0, PWS0, PTRATE, PHUMIDAIRPA0,                      &
            PSUNQV, PSHADEQV, PSUNQN, PSHADEQN, PSUNPPFD, PSHADEPPFD, &
            PSUNLEAFTK, PSHADELEAFTK, PSUNLEAFSH, PSHADELEAFSH,       &
            PTAIRK, PHUMIDAIRPA, PWS,                                 &                       
            PSUNLEAFLH, PSUNLEAFIR, PSHADELEAFLH, PSHADELEAFIR)

IMPLICIT NONE

! INPUTS
INTEGER, INTENT(IN) :: KCANTYPE
REAL, DIMENSION(:,:), INTENT(IN) :: PCANOPYCHAR
REAL, DIMENSION(:), INTENT(IN) ::  PDISTGAUSS
REAL, INTENT(IN) :: PSTOMATADI
!
REAL, DIMENSION(:), INTENT(IN) :: PTRATE, PTAIRK0, PWS0, PHUMIDAIRPA0
REAL, DIMENSION(:,:), INTENT(IN) :: PSUNQV, PSHADEQV, &
                                    PSUNQN, PSHADEQN, PSUNPPFD, PSHADEPPFD

! OUTPUTS
REAL, DIMENSION(:,:), INTENT(OUT) :: PSUNLEAFTK, PSHADELEAFTK, PSUNLEAFSH, PSHADELEAFSH
!
REAL, DIMENSION(:,:), INTENT(OUT), OPTIONAL :: PTAIRK, PHUMIDAIRPA, PWS, &
                                               PSUNLEAFLH, PSHADELEAFLH,&
                                               PSUNLEAFIR, PSHADELEAFIR
! LOCAL VARIABLES
REAL :: ZLDEPTH, ZWSH
REAL, DIMENSION(SIZE(PTRATE)) :: ZTAIRK, ZHUMIDAIRPA, ZWS, &
                                 ZSUNLEAFLH, ZSHADELEAFLH, ZSUNLEAFIR, ZSHADELEAFIR
!
REAL, DIMENSION(SIZE(PTRATE)) :: ZDELTAH, ZIRIN, ZIROUT
REAL :: ZCDEPTH, ZLWIDTH, ZLLENGTH, ZCHEIGHT, ZEPS, ZTRANSPIRETYPE
INTEGER :: JI
!
!-----------------------------------------------------------------------
  
ZCDEPTH        = PCANOPYCHAR(1, KCANTYPE)
!ZLWIDTH        = PCANOPYCHAR(2, KCANTYPE)
ZLLENGTH       = PCANOPYCHAR(3, KCANTYPE)
ZCHEIGHT       = PCANOPYCHAR(4, KCANTYPE)
ZEPS           = PCANOPYCHAR(10,KCANTYPE)
ZTRANSPIRETYPE = PCANOPYCHAR(11,KCANTYPE)

WHERE ( PTAIRK0(:) >288. )
! PA M-1  (PHUMIDITY PROFILE FOR T < 288)
  ZDELTAH(:) = PCANOPYCHAR(14,KCANTYPE) / ZCHEIGHT
ELSEWHERE ( PTAIRK0(:)>278. )
  ZDELTAH(:) = ( PCANOPYCHAR(14,KCANTYPE) - ( (288.-PTAIRK0(:))/10.) *   &         
               ( PCANOPYCHAR(14,KCANTYPE) - PCANOPYCHAR(15,KCANTYPE)) ) / ZCHEIGHT
ELSEWHERE
! PA M-1  (PHUMIDITY PROFILE FOR T <278)
  ZDELTAH(:) = PCANOPYCHAR(15,KCANTYPE) / ZCHEIGHT
END WHERE

DO JI = 1,SIZE(PDISTGAUSS)

  ZLDEPTH = ZCDEPTH * PDISTGAUSS(JI)
  ZWSH    = ( ZCHEIGHT - ZLDEPTH ) - ( PCANOPYCHAR(16,KCANTYPE) * ZCHEIGHT )

  ZTAIRK     (:) = PTAIRK0     (:) + (PTRATE (:) * ZLDEPTH)      ! CHECK THIS
  ZHUMIDAIRPA(:) = PHUMIDAIRPA0(:) + (ZDELTAH(:) * ZLDEPTH)
  IF ( ZWSH.GT.1E-3 ) THEN
    ZWS(:) = ( PWS0(:) * LOG(ZWSH) / LOG(ZCHEIGHT-PCANOPYCHAR(16,KCANTYPE)*ZCHEIGHT) )
  ELSE 
    ZWS(:) =  0.05
  END IF
  
  ZIRIN(:) = UNEXPOSEDLEAFIRIN(ZEPS, ZTAIRK)

  ZSUNLEAFIR(:) = 0.5 * EXPOSEDLEAFIRIN(PHUMIDAIRPA0,PTAIRK0) + 1.5*ZIRIN(:)

! SUN
  CALL LEAFEB(ZEPS, ZTRANSPIRETYPE, ZLLENGTH, PSTOMATADI,     &
              PSUNPPFD(:,JI), PSUNQV(:,JI)+PSUNQN(:,JI),      &
              ZSUNLEAFIR, ZTAIRK, ZHUMIDAIRPA, ZWS,           &
              PSUNLEAFTK(:,JI), PSUNLEAFSH(:,JI), ZSUNLEAFLH, &
              ZIROUT )
!
  IF (PRESENT(PSUNLEAFIR)) PSUNLEAFIR(:,JI) = ZSUNLEAFIR(:) - ZIROUT(:)

! SHADE
  ZSHADELEAFIR(:) = 2. * ZIRIN(:)

  CALL LEAFEB(ZEPS, ZTRANSPIRETYPE, ZLLENGTH, PSTOMATADI,           &
              PSHADEPPFD(:,JI), PSHADEQV(:,JI)+PSHADEQN(:,JI),      &
              ZSHADELEAFIR, ZTAIRK, ZHUMIDAIRPA, ZWS,               &
              PSHADELEAFTK(:,JI), PSHADELEAFSH(:,JI), ZSHADELEAFLH, &
              ZIROUT )
!
  IF (PRESENT(PSHADELEAFIR)) PSHADELEAFIR(:,JI) = ZSHADELEAFIR(:) - ZIROUT(:)

  IF (PRESENT(PTAIRK))       PTAIRK      (:,JI) = ZTAIRK      (:)
  IF (PRESENT(PHUMIDAIRPA))  PHUMIDAIRPA (:,JI) = ZHUMIDAIRPA (:)
  IF (PRESENT(PWS))          PWS         (:,JI) = ZWS         (:)
  IF (PRESENT(PSUNLEAFLH))   PSUNLEAFLH  (:,JI) = ZSUNLEAFLH  (:)
  IF (PRESENT(PSHADELEAFLH)) PSHADELEAFLH(:,JI) = ZSHADELEAFLH(:)

ENDDO
!
END SUBROUTINE CANOPYEB

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   SUBROUTINE LEAFEB
!
!   LEAF ENERGY BALANCE
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

SUBROUTINE LEAFEB(PEPS, PTRANSPIRETYPE, PLLENGTH, PSTOMATADI, &
                  PPFD, PQ, PIRIN, PTAIRK, PHUMIDAIRPA, PWS,  &
                  PTLEAF, PSH, PLH, PIROUT)

IMPLICIT NONE

REAL, INTENT(IN) :: PEPS, PTRANSPIRETYPE, PLLENGTH, PSTOMATADI
REAL, DIMENSION(:), INTENT(IN) :: PPFD, PQ, PIRIN, PTAIRK, PHUMIDAIRPA, PWS
REAL, DIMENSION(:), INTENT(OUT) :: PTLEAF, PSH, PLH, PIROUT

! LOCAL VARIABLES
REAL, DIMENSION(SIZE(PPFD)) :: ZHUMIDAIRKGM3, ZGHFORCED, ZSTOMRES, ZIROUTAIRT, ZLATHV, &
                               ZLHAIRT, ZTDELT, ZBALANCE, ZGH1, ZSH1, ZLH1, ZE1, ZIROUT1, ZGH, &
                               ZTAIRK, ZVAPDEFICIT
INTEGER :: JI
!----------------------------------------------------

! AIR VAPOR DENSITY KG M-3
ZHUMIDAIRKGM3(:) = CONVERTHUMIDITYPA2KGM3(PHUMIDAIRPA, PTAIRK)

! LATENT HEAT OF VAPORIZATION (J KG-1)
ZLATHV(:) = LHV(PTAIRK)
!
! HEAT CONVECTION COEFFICIENT (W M-2 K-1) FOR FORCED CONVECTION.
! NOBEL PAGE 366
ZGHFORCED(:) = 0.0259 / (0.004 * ((PLLENGTH / PWS(:))**0.5))
!
! STOMATAL RESISTENCE S M-1
ZSTOMRES  (:) = RESSC(PSTOMATADI, PPFD)
!
! LATENT HEAT FLUX
ZVAPDEFICIT(:) = SVDTK(PTAIRK(:)) - ZHUMIDAIRKGM3(:)
ZLHAIRT(:) = LEAFLE(PTRANSPIRETYPE, ZVAPDEFICIT, ZLATHV, ZGHFORCED, ZSTOMRES)
!
ZIROUTAIRT(:) = LEAFIROUT(PEPS, PTAIRK)
ZE1(:) = (PQ(:) + PIRIN(:) - ZIROUTAIRT(:) - ZLHAIRT(:))
WHERE ( ZE1(:).EQ.0. ) ZE1(:) = -1.
!
ZTDELT  (:) = 1.
ZBALANCE(:) = 10.
DO JI = 1, 10
  !
  WHERE ( ABS(ZBALANCE(:))>2. )
    !
    ZTAIRK  (:) = PTAIRK(:) + ZTDELT(:)  
    !    
    ! LATENT HEAT OF VAPORIZATION (J KG-1)
    ZLATHV(:) = LHV(ZTAIRK)          
    ! BOUNDARY LAYER CONDUCTANCE
    ZGH1  (:) = LEAFBLC(PLLENGTH, ZGHFORCED, ZTDELT)
    !
    ZVAPDEFICIT(:) = SVDTK(ZTAIRK(:)) - ZHUMIDAIRKGM3(:)
    PLH   (:) = LEAFLE(PTRANSPIRETYPE, ZVAPDEFICIT, ZLATHV, ZGH1, ZSTOMRES)
    !
    PIROUT (:) = LEAFIROUT(PEPS, PTAIRK+ZTDELT)
    ZIROUT1(:) = PIROUT(:) - ZIROUTAIRT(:)
    !
    ! CONVECTIVE HEAT FLUX
    ZSH1(:) = LEAFH(ZTDELT, ZGH1)           
    ZLH1(:) = PLH(:) - ZLHAIRT(:)   
    ! 
    ZTDELT  (:) = ZE1(:) / ((ZSH1(:) + ZLH1(:) + ZIROUT1(:)) / ZTDELT(:))
    ZBALANCE(:) = PQ(:) + PIRIN(:) - PIROUT(:) - ZSH1(:) - PLH(:)
  END WHERE
  !
  IF (ALL(ZBALANCE(:)<=2.)) EXIT
  !
ENDDO
!
ZTDELT(:) = MAX(-10.,MIN(ZTDELT(:),10.))
!
PTLEAF(:) = PTAIRK(:) + ZTDELT(:)
!
ZGH(:)    = LEAFBLC(PLLENGTH, ZGHFORCED, ZTDELT)
PSH(:)    = LEAFH  (ZTDELT, ZGH)
!
ZVAPDEFICIT(:) = SVDTK(PTLEAF(:)) - ZHUMIDAIRKGM3(:)
PLH(:)    = LEAFLE (PTRANSPIRETYPE, ZVAPDEFICIT, ZLATHV, ZGH, ZSTOMRES)
PIROUT(:) = LEAFIROUT(PEPS, PTLEAF)
!
END SUBROUTINE LEAFEB

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION DISTOMATA
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION DISTOMATA(PDI) RESULT(PDISTOMATA)

IMPLICIT NONE

REAL, INTENT(IN) :: PDI
REAL :: PDISTOMATA
INTEGER :: JJ
! > -.5 INCIPIENT,  MILD OR NO DROUGHT; < -4 EXTREME DROUGHT
!--------------------------------------------------------------------

IF ( PDI>XDIHIGH ) THEN
  PDISTOMATA = 1.  ! NO DROUGHT
ELSEIF ( PDI>XDILOW ) THEN
  ! INTERPOLATE
  PDISTOMATA = 1. - (0.9 * ((PDI - XDIHIGH) / (XDILOW - XDIHIGH)))
ELSE
  PDISTOMATA = 0.  ! MAXIMUM DROUGHT, MAXIMUM STOMATAL RESISTANCE
ENDIF

END FUNCTION DISTOMATA

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION CALCECCENTRICITY
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION CALCECCENTRICITY(KDAY) RESULT(PCALCECCENTRICITY)

IMPLICIT NONE

INTEGER, DIMENSION(:), INTENT(IN) :: KDAY
!
REAL, DIMENSION(SIZE(KDAY)) :: PCALCECCENTRICITY
!
!--------------------------------------------------------------------

PCALCECCENTRICITY(:) = 1. + 0.033 * COS(2*3.14*(KDAY(:)-10)/365)
  
END FUNCTION CALCECCENTRICITY

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION UNEXPOSEDLEAFIRIN
!
!   CALCULATE IR INTO LEAF THAT IS NOT EXPOSED TO THE SKY
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION UNEXPOSEDLEAFIRIN(PEPS, PTK) RESULT(PUNEXPOSEDLEAFIRIN)

IMPLICIT NONE

REAL, INTENT(IN) :: PEPS
REAL, DIMENSION(:), INTENT(IN) :: PTK
REAL, DIMENSION(SIZE(PTK)) :: PUNEXPOSEDLEAFIRIN
!--------------------------------------------------------------------

PUNEXPOSEDLEAFIRIN(:) = PEPS * XSB * (PTK(:)**4.)

END FUNCTION UNEXPOSEDLEAFIRIN

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION EXPOSEDLEAFIRIN
!
!   CALCULATE IR INTO LEAF THAT IS EXPOSED TO THE SKY
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION EXPOSEDLEAFIRIN(PHUMIDPA, PTK) RESULT(PEXPOSEDLEAFIRIN)

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN) :: PTK, PHUMIDPA
REAL, DIMENSION(SIZE(PTK)) :: PEXPOSEDLEAFIRIN
REAL :: ZEMISSATM
INTEGER :: JJ
!--------------------------------------------------------------------

! APPARENT ATMOSPHERIC EMISSIVITY FOR CLEAR SKIES:
! FUNCTION OF WATER VAPOR PRESSURE (PA)
! AND AMBIENT TEMPERATURE (K) BASED ON BRUTSAERT(1975)
! REFERENCED IN LEUNING (1997)

DO JJ = 1,SIZE(PTK)
  ZEMISSATM            = 0.642 * (PHUMIDPA(JJ) / PTK(JJ))**(1./7.)
  PEXPOSEDLEAFIRIN(JJ) = ZEMISSATM * XSB * (PTK(JJ)**4.)
ENDDO

END FUNCTION EXPOSEDLEAFIRIN

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION WATERVAPPRES
!
!   CONVERT WATER MIXING RATIO (KG/KG) TO WATER VAPOR PRESSURE
!   (PA OR KPA DEPENDING ON UNITS OF INPUT )
!   MIXING RATIO (KG/KG), TEMP (C), PRESSURE (KPA)
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION WATERVAPPRES(PWATERAIRRATIO, PDENS, PRES) RESULT(PWATERVAPPRES)

IMPLICIT NONE

REAL, INTENT(IN) :: PWATERAIRRATIO
REAL, DIMENSION(:), INTENT(IN) :: PDENS, PRES
REAL, DIMENSION(SIZE(PDENS)) :: PWATERVAPPRES
!--------------------------------------------------------------------

PWATERVAPPRES(:) = (PDENS(:) / (PDENS(:) + PWATERAIRRATIO)) * PRES(:)

END FUNCTION WATERVAPPRES

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION STABILITY
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION STABILITY(PCANOPYCHAR, KCANTYPE, PSOLAR) RESULT(PSTABILITY)

IMPLICIT NONE
!
REAL, DIMENSION(:,:), INTENT(IN)  :: PCANOPYCHAR
INTEGER, INTENT(IN) :: KCANTYPE
REAL, DIMENSION(:), INTENT(IN) :: PSOLAR 
REAL, DIMENSION(SIZE(PSOLAR)) :: PSTABILITY
REAL :: ZTRATEBOUNDARY
INTEGER :: JJ
!--------------------------------------------------------------------

ZTRATEBOUNDARY = 500

DO JJ = 1,SIZE(PSOLAR)
  IF ( PSOLAR(JJ)>ZTRATEBOUNDARY ) THEN
    ! DAYTIME TEMPERATURE LAPSE RATE
    PSTABILITY(JJ) = PCANOPYCHAR(12,KCANTYPE)
  ELSEIF ( PSOLAR(JJ)>0. ) THEN
    PSTABILITY(JJ) = PCANOPYCHAR(12,KCANTYPE) - &
                     ( (ZTRATEBOUNDARY - PSOLAR(JJ)) / ZTRATEBOUNDARY ) *   & 
                       (PCANOPYCHAR(12,KCANTYPE) - PCANOPYCHAR(13,KCANTYPE))
  ELSE
    ! NIGHTIME TEMPERATURE LAPSE RATE
    PSTABILITY = PCANOPYCHAR(13,KCANTYPE)
  ENDIF
ENDDO

END FUNCTION STABILITY

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION CONVERTHUMIDITYPA2KGM3
!
!   SATURATION VAPOR DENSITY  (KG/M3)
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION CONVERTHUMIDITYPA2KGM3(PA, PTK) RESULT(PCONVERTHUMIDITYPA2KGM3)

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN) :: PA, PTK
REAL, DIMENSION(SIZE(PA)) :: PCONVERTHUMIDITYPA2KGM3
!--------------------------------------------------------------------

PCONVERTHUMIDITYPA2KGM3(:) = 0.002165 * PA(:) / PTK(:)

END FUNCTION CONVERTHUMIDITYPA2KGM3

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION RESSC
!
!   LEAF STOMATAL COND. RESISTANCE S M-1
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION RESSC(PSTOMATADI, PAR) RESULT(PRESSC)

IMPLICIT NONE

REAL, INTENT(IN) :: PSTOMATADI
REAL, DIMENSION(:), INTENT(IN) :: PAR
REAL, DIMENSION(SIZE(PAR)) :: PRESSC
REAL, DIMENSION(SIZE(PAR)) :: ZSCADJ
INTEGER :: JJ
!--------------------------------------------------------------------

ZSCADJ(:) = PSTOMATADI * &
          ( (0.0027*1.066*PAR(:)) / ((1 + 0.0027*0.0027*PAR(:)**2.)**0.5) )
!
WHERE (ZSCADJ(:)<0.1)
  PRESSC(:) = 2000.
ELSE WHERE
  PRESSC(:) = 200./ZSCADJ(:)
END WHERE

END FUNCTION RESSC

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION LEAFIROUT
!
!   IR THERMAL RADIATION ENERGY OUTPUT BY LEAF
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION LEAFIROUT(PEPS, PTLEAF) RESULT(PLEAFIROUT)

IMPLICIT NONE

REAL, INTENT(IN) :: PEPS
REAL, DIMENSION(:), INTENT(IN) :: PTLEAF
REAL, DIMENSION(SIZE(PTLEAF)) :: PLEAFIROUT
!--------------------------------------------------------------------

!      PRINT*,'EPS, SB, TLEAF =', EPS, SB, TLEAF
PLEAFIROUT(:) = PEPS * XSB * (2 * (PTLEAF(:)**4.))

END FUNCTION LEAFIROUT

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION LHV
!
!   LATENT HEAT OF VAPORIZATION(J KG-1) FROM STULL P641
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION LHV(PTK) RESULT(PLHV)

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN) :: PTK
REAL, DIMENSION(SIZE(PTK)) :: PLHV
!--------------------------------------------------------------------

PLHV(:) = 2501000. - (2370. * (PTK(:) - 273.))

END FUNCTION LHV

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION LEAFLE
!
!   LATENT ENERGY TERM IN ENERGY BALANCE
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION LEAFLE(PTRANSPIRETYPE, PVAPDEFICIT, PLATHV, PGH, PSTOMRES) RESULT(PLEAFLE)

IMPLICIT NONE

REAL, INTENT(IN) :: PTRANSPIRETYPE
REAL, DIMENSION(:), INTENT(IN) :: PVAPDEFICIT, PLATHV, PGH, PSTOMRES
REAL, DIMENSION(SIZE(PLATHV)) :: PLEAFLE
REAL, DIMENSION(SIZE(PLATHV)) :: ZLEAFRES
!INTEGER :: JJ
!--------------------------------------------------------------------

ZLEAFRES(:)    = (1. / (1.075 * (PGH(:) / 1231.))) + PSTOMRES(:)

! LATENT HEAT OF VAP (J KG-1) * VAP DEFICIT(KG M-3) /
!                 LEAF RESISTENCE (S M-1)
PLEAFLE(:) = PTRANSPIRETYPE * (1./ZLEAFRES(:)) * PLATHV(:) * PVAPDEFICIT(:)
!
PLEAFLE(:) = MAX(PLEAFLE(:),0.)
!
END FUNCTION  LEAFLE

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION LEAFBLC
!
!   BOUNDARY LAYER CONDUCTANCE
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION LEAFBLC(PLLENGTH, PGHFORCED, PTDELTA) RESULT(PLEAFBLC)

IMPLICIT NONE

REAL, INTENT(IN) :: PLLENGTH
REAL, DIMENSION(:), INTENT(IN) :: PGHFORCED, PTDELTA
REAL, DIMENSION(SIZE(PTDELTA)) :: PLEAFBLC
REAL, DIMENSION(SIZE(PTDELTA)) :: ZGHFREE
REAL :: ZLLENGTH3
INTEGER :: JJ
!--------------------------------------------------------------------

! THIS IS BASED ON LEUNING 1995 P.1198 EXCEPT USING MOLECULAR
! CONDUCTIVITY (.00253 W M-1 K-1 STULL P 640) INSTEAD OF MOLECULAR
! DIFFUSIVITY SO THAT YOU END UP WITH A HEAT CONVECTION COEFFICIENT
! (W M-2 K-1) INSTEAD OF A CONDUCTANCE FOR FREE CONVECTION
!
ZLLENGTH3 = PLLENGTH**3
!
WHERE (PTDELTA(:)>=0.)
  ZGHFREE (:) = 0.5 * 0.00253 * ((160000000. * PTDELTA(:) / (ZLLENGTH3))**0.25) / PLLENGTH
  PLEAFBLC(:) = PGHFORCED(:) + ZGHFREE(:)
ELSE WHERE
  PLEAFBLC(:) = PGHFORCED(:)
END WHERE
!
END FUNCTION LEAFBLC

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION LEAFH
!
!   CONVECTIVE ENERGY TERM IN ENERGY BALANCE (W M-2 HEAT FLUX FROM
!      BOTH SIDES OF LEAF)
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION LEAFH(PTDELTA, PGH) RESULT(PLEAFH)

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN) :: PTDELTA, PGH
REAL, DIMENSION(SIZE(PGH)) :: PLEAFH
!--------------------------------------------------------------------

! 2 SIDES X CONDUCTANCE X TEMPERATURE GRADIENT
PLEAFH(:) = 2. * PGH(:) * PTDELTA(:)

END FUNCTION LEAFH

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION SVDTK
!
!   SATURATION VAPOR DENSITY  (KG/M3)
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION SVDTK(PTK) RESULT(PSVDTK)

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN) :: PTK
REAL, DIMENSION(SIZE(PTK)) :: PSVDTK
REAL, DIMENSION(SIZE(PTK)) :: ZSVP
INTEGER :: JJ
!--------------------------------------------------------------------

! SATURATION VAPOR PRESSURE (MILLIBARS)
ZSVP  (:) = 10.**((-2937.4 / PTK(:)) - (4.9283 * LOG10(PTK(:))) + 23.5518) 
PSVDTK(:) = 0.2165 * ZSVP(:) / PTK(:)

END FUNCTION SVDTK

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION EA1T99
!
!   TEMPERATURE DEPENDENCE ACTIVITY FACTOR FOR EMISSION TYPE 1
!          (E.G. ISOPRENE, MBO)
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION EA1T99(HSPC_NAME, PT24, PT240, PT1) RESULT(PEA1T99)

USE MODI_INDEX1

IMPLICIT NONE

CHARACTER(LEN=16),INTENT(IN) :: HSPC_NAME
REAL, INTENT(IN) :: PT24, PT240
REAL, DIMENSION(:), INTENT(IN) :: PT1
REAL, DIMENSION(SIZE(PT1)) :: PEA1T99
REAL :: ZTOPT, ZX, ZEOPT
INTEGER :: ISPCNUM
INTEGER :: JJ
!--------------------------------------------------------------------
    
ISPCNUM = INDEX1(HSPC_NAME, CMGN_SPC)
!
DO JJ = 1,SIZE(PT1)

  IF ( PT1(JJ)<260. ) THEN
    PEA1T99(JJ) = 0.
  ELSE
    ! ENERGY OF ACTIVATION AND DEACTIVATION
    ! TEMPERATURE AT WHICH MAXIMUM EMISSION OCCURS
    ZTOPT = 312.5 + 0.6 * (PT240 - 297)
    ZX    = ((1 / ZTOPT) - (1 / PT1(JJ))) / 0.00831

    ! MAXIMUM EMISSION (RELATIVE TO EMISSION AT 30 C)
    ZEOPT = XCLEO(ISPCNUM) * EXP(0.05 * (PT24 - 297)) * EXP(0.05*(PT240-297))         
    PEA1T99(JJ) = ZEOPT * XCTM2 * EXP(XCTM1(ISPCNUM)*ZX) / &
                (XCTM2 - XCTM1(ISPCNUM) * (1.-EXP(XCTM2*ZX)))
  ENDIF

ENDDO

END FUNCTION  EA1T99

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION EA1PP
!
! PSTD = 200 FOR SUN LEAVES AND 50 FOR SHADE LEAVES
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION EA1P99(PSTD, PPFD24, PPFD240, PPFD1) RESULT(PEA1P99)

IMPLICIT NONE

REAL, INTENT(IN) :: PSTD, PPFD24, PPFD240
REAL, DIMENSION(:), INTENT(IN) :: PPFD1
REAL, DIMENSION(SIZE(PPFD1)) :: PEA1P99
REAL :: ZALPHA, ZC1
INTEGER :: JJ
!--------------------------------------------------------------------

DO JJ = 1,SIZE(PPFD1)

  IF ( PPFD240<0.01 ) THEN
    PEA1P99(JJ) = 0.
  ELSE
    ZALPHA  = 0.004 - 0.0005 * LOG(PPFD240)
    ZC1     = 0.0468 * EXP(0.0005 * (PPFD24 - PSTD)) * (PPFD240**0.6)
    PEA1P99(JJ) = (ZALPHA * ZC1 * PPFD1(JJ)) / ((1 + ZALPHA**2. * PPFD1(JJ)**2.)**0.5)
  ENDIF

ENDDO

END FUNCTION  EA1P99

!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
!   FUNCTION EALTI99
!
! CALCULATE LIGHT INDEPENT ALGORITHMS
! CODED BY XUEMEI WANG 05 NOV. 2007
!--   GAMMA_TLI =  EXP[BETA*(T-TS)]
!           WHERE BETA   = TEMPERATURE DEPENDENT PARAMETER
!                 TS     = STANDARD TEMPERATURE (NORMALLY 303K, 30C)
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO

FUNCTION EALTI99(HSPCNAM, PTEMP) RESULT(PEALTI99)

USE MODI_INDEX1

IMPLICIT NONE

CHARACTER(LEN=16), INTENT(IN) :: HSPCNAM
REAL, DIMENSION(:), INTENT(IN) :: PTEMP
REAL, DIMENSION(SIZE(PTEMP)) :: PEALTI99
!
INTEGER :: ISPCNUM                             ! SPECIES NUMBER
!--------------------------------------------------------------------
ISPCNUM = INDEX1(HSPCNAM, CMGN_SPC)
PEALTI99(:) = EXP( XTDF_PRM(ISPCNUM)*(PTEMP(:)-XTS) )

END FUNCTION EALTI99
!
!OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
!
END MODULE MODE_MEGAN
