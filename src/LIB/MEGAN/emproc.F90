
SUBROUTINE EMPROC(KTIME, KDATE, PPFD_D, PTEMP_D, PDI, PRECADJ, &
                  PLAT, PLONG, PLAIP, PLAIC, PTEMP, PPFD,      &
                  PWIND, PRES, PQV,  KSLTYP, PSOILM, PSOILT,  &
                  PFTF,  OSOIL, PCFNO, PCFNOG, PCFSPEC )

!***********************************************************************
!   THIS PROGRAM COMPUTES BIOGENIC EMISSION USING INPUT EMISSION 
!   CAPACITY MAPS AND MCIP OUTPUT VARIABLES.
!   THE EMISSION CAPACITY MAP (INPNAME) ARE GRIDDED IN NETCDF-IOAPI FORMAT
!   WITH ALL THE DAILY AVERAGE PPFD AND DAILY AVERAGE TEMPERATURE.
!
!   NOTE: THE PROJECTION AND INPUT GRIDS OF THE TWO FILES MUST BE 
!   IDENTICAL.
!
!
!   CALL:
!      CHECKMEM
!      MODULE GAMMA_ETC
!         GAMMA_LAI
!         GAMMA_P
!         GAMMA_TLD
!         GAMMA_TLI
!         GAMMA_A
!         GAMMA_S
!
!   HISTORY:
!   CREATED BY JACK CHEN 11/04
!   MODIFIED BY TAN 11/21/06 FOR MEGAN V2.0
!   MODIFIED BY XUEMEI WANG 11/04/2007 FOR MEGAN2.1
!   MODIFIED BY JULIA LEE-TAYLOR 03/18/2008 FOR MEGAN2.1
!   MODIFIED BY XUEMEI WANG 09/30/2008 FOR MEGAN2.1
!   MODIFIED BY TAN         07/28/2011 FOR MEGAN2.1
!   MODIFIED BY P. TULET    01/11/2014 FOR COUPLING WITH ISBA (MESONH)
!   MODIFIED BY J. PIANEZZEJ 13/02/2019 BUG in FARCE case
!
!***********************************************************************
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!     SCIENTIFIC ALGORITHM
!
!             EMISSION = [EF][GAMMA][RHO]
!           WHERE [EF]    = EMISSION FACTOR (UG/M2H)
!                 [GAMMA] = EMISSION ACTIVITY FACTOR (NON-DIMENSION)
!                 [RHO]   = PRODUCTION AND LOSS WITHIN PLANT CANOPIES
!                           (NON-DIMENSIONAL)
!                 ASSUMPTION: [RHO] = 1  (11/27/06) (SEE PDT_LOT_CP.EXT)
!
!             GAMMA  = [GAMMA_CE][GAMMA_AGE][GAMMA_SM]
!           WHERE [GAMMA_CE]  = CANOPY CORRECTION FACTOR
!                 [GAMMA_AGE] = LEAF AGE CORRECTION FACTOR
!                 [GAMMA_SM]  = SOIL MOISTURE CORRECTION FACTOR
!                 ASSUMPTION: [GAMMA_SM]  = 1  (11/27/06)

!             GAMMA_CE = [GAMMA_LAI][GAMMA_P][GAMMA_T]
!           WHERE [GAMMA_LAI] = LEAF AREA INDEX FACTOR
!                 [GAMMA_P]   = PPFD EMISSION ACTIVITY FACTOR
!                 [GAMMA_T]   = TEMPERATURE RESPONSE FACTOR
!
!             EMISSION = [EF][GAMMA_LAI][GAMMA_P][GAMMA_T][GAMMA_AGE]
!        DERIVATION:
!             EMISSION = [EF][GAMMA](1-LDF) + [EF][GAMMA][LDF][GAMMA_P]
!             EMISSION = [EF][GAMMA]{ (1-LDF) + [LDF][GAMMA_P] }
!             EMISSION = [EF][GAMMA]{ (1-LDF) + [LDF][GAMMA_P] }
!           WHERE LDF = LIGHT DEPENDENT FUNCTION (NON-DIMENSION)
!                               (SEE LD_FCT.EXT)
!
!        FINAL EQUATION
!             EMISSION = [EF][GAMMA_LAI][GAMMA_AGE]*
!    { (1-LDF)[GAMMA_TLI] + [LDF][GAMMA_P][GAMMA_TLD] }  !FOR MEGAN2.1
!         WHERE GAMMA_TLI IS LIGHT INDEPENDENT
!               GAMMA_TLD IS LIGHT DEPENDENT  
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

USE MODD_MEGAN

USE MODI_INDEX1
USE MODI_SOILNOX
!
USE MODE_MEGAN
USE MODE_GAMMA_ETC                 ! MODULE CONTAINING GAMMA FUNCTIONS
!
IMPLICIT NONE

INTEGER,            INTENT(IN)    :: KTIME    !I TIME OF THE DAY HHMMSS
INTEGER,            INTENT(IN)    :: KDATE    !I DATE YYYYDDD
!
REAL, INTENT(IN) :: PPFD_D   !I DAILY PAR (UMOL/M2.S)
REAL, INTENT(IN) :: PTEMP_D  !I DAILY TEMPERATURE (K)
REAL, INTENT(IN) :: PDI      !I DROUGHT INDEX (0 NORMAL, -2  MODERATE DROUGHT, -3 SEVERE DROUGHT, -4 EXTREME DROUGHT)
REAL, INTENT(IN) :: PRECADJ  !I RAIN ADJUSTMENT FACTOR
!
REAL, DIMENSION(:), INTENT(IN)    :: PLAT     !I LATITUDE OF GRID CELL
REAL, DIMENSION(:), INTENT(IN)    :: PLONG    !I LONGITUDE OF GRID CELL
REAL, DIMENSION(:), INTENT(IN)    :: PLAIP    !I PREVIOUS MONTHLY LAI
REAL, DIMENSION(:), INTENT(IN)    :: PLAIC    !I CURRENT MONTHLY LAI
REAL, DIMENSION(:), INTENT(IN)    :: PTEMP    !I TEMPERATURE (K)
REAL, DIMENSION(:), INTENT(INOUT) :: PPFD     !I CALCULATED PAR (UMOL/M2.S)
REAL, DIMENSION(:), INTENT(IN)    :: PWIND    !I WIND VELOCITY (M/S)
REAL, DIMENSION(:), INTENT(IN)    :: PRES     !I ATMOSPHERIC PRESSURE (PA)
REAL, DIMENSION(:), INTENT(IN)    :: PQV      !I AIR HUMIDITY (KG/KG)
INTEGER,DIMENSION(:),INTENT(IN)   :: KSLTYP   !I SOIL CATEGORY (FUNCTION OF SILT, CLAY AND SAND))
REAL, DIMENSION(:), INTENT(IN)    :: PSOILM   !I SOIL MOISTURE (M3/M3)
REAL, DIMENSION(:), INTENT(IN)    :: PSOILT   !I SOIL TEMPERATURE (K)
REAL, DIMENSION(:,:),INTENT(IN)  :: PFTF     ! PFT FACTOR ARRAY (NRTYP 1-16 IN THE FIRST DIM)
LOGICAL,              INTENT(IN)    :: OSOIL    !I LOGICAL FOR ACTIVE NO CORRECTION FACTOR
REAL, DIMENSION(:), INTENT(INOUT)  :: PCFNO    !O NO CORRECTION FACTOR
REAL, DIMENSION(:), INTENT(INOUT)  :: PCFNOG   !O NO CORRECTION FACTOR FOR GRASS
REAL, DIMENSION(:,:),INTENT(INOUT) :: PCFSPEC  !O OUTPUT EMISSION BUFFER

! LOCAL VARIABLES AND THEIR DESCRIPTIONS:
REAL, DIMENSION(SIZE(PSOILM)) :: ZGAM_LHT  ! LAI CORRECTION FACTOR
REAL, DIMENSION(SIZE(PSOILM)) :: ZGAM_AGE  ! LEAF AGE CORRECTION FACTOR
REAL, DIMENSION(SIZE(PSOILM)) :: ZGAM_SMT  ! SOIL MOISTURE CORRECTION FACTOR
REAL, DIMENSION(SIZE(PSOILM)) :: ZER       ! EMISSION BUFFER
! NUMBER OF LAT, LONG, AND PFT FACTOR VARIABLES
REAL, DIMENSION(SIZE(PSOILM)) :: ZGAM_TLD
REAL, DIMENSION(SIZE(PSOILM)) :: ZGAM_TLI
!
CHARACTER(LEN=100), DIMENSION(N_MGN_SPC+7) :: YVNAME3D
!
REAL, DIMENSION(SIZE(PSOILM)) :: ZADJUST_FACTOR_LD, ZADJUST_FACTOR_LI
REAL, DIMENSION(SIZE(PSOILM)) :: ZGAMMA_TD, ZGAMMA_TI, ZTOTALPFT
REAL :: ZLDF             ! LIGHT DEPENDENT FACTOR
REAL :: ZRHO             ! PRODUCTION AND LOSS WITHIN CANOPY
REAL :: ZPFD_D
!
INTEGER :: I_PFT
INTEGER :: ILAIP_DY, ILAIP_HR, ILAIC_DY, ILAIC_HR
INTEGER :: IMXPFT, IMXLAI

! LOOP INDICES
INTEGER :: JT, JS, JI, JJ , JK, JN, INP    ! COUNTERS
INTEGER :: INMAP            ! INDEX
INTEGER :: INVARS3D 

!***********************************************************************

!--=====================================================================
!...  BEGIN PROGRAM
!--=====================================================================

!-----------------------------------------------------------------------
!.....1) INITIALIZATION
!-----------------------------------------------------------------------
!
INVARS3D = N_MGN_SPC + 7
!
DO JS = 1,N_MGN_SPC
  YVNAME3D(JS) = TRIM( CMGN_SPC(JS) )
!         VDESC3D(S) = 'ENVIRONMENTAL ACTIVITY FACTOR FOR '//
!     &                TRIM( MGN_SPC(S) )
!         UNITS3D(S) = 'NON-DIMENSION '
!         VTYPE3D(S) = M3REAL
ENDDO

YVNAME3D(N_MGN_SPC+1) = 'D_TEMP'
!      UNITS3D(N_MGN_SPC+1) = 'K'
!      VTYPE3D(N_MGN_SPC+1) = M3REAL
!      VDESC3D(N_MGN_SPC+1) = 'VARIABLE  '//'K'

YVNAME3D(N_MGN_SPC+2) = 'D_PPFD'
!      UNITS3D(N_MGN_SPC+2) = 'UMOL/M2.S'
!      VTYPE3D(N_MGN_SPC+2) = M3REAL
!      VDESC3D(N_MGN_SPC+2) = 'VARIABLE  '//'UMOL/M2.S'

YVNAME3D(N_MGN_SPC+3) = 'LAT'
!      UNITS3D(N_MGN_SPC+3) = ' '
!      VTYPE3D(N_MGN_SPC+3) = M3REAL
!      VDESC3D(N_MGN_SPC+3) = ' '

YVNAME3D(N_MGN_SPC+4) = 'LONG'
!      UNITS3D(N_MGN_SPC+4) = ' '
!      VTYPE3D(N_MGN_SPC+4) = M3REAL
!      VDESC3D(N_MGN_SPC+4) = ' '

YVNAME3D(N_MGN_SPC+5) = 'CFNO'
!      UNITS3D(N_MGN_SPC+5) = ' '
!      VTYPE3D(N_MGN_SPC+5) = M3REAL
!      VDESC3D(N_MGN_SPC+5) = ' '

YVNAME3D(N_MGN_SPC+6) = 'CFNOG'
!      UNITS3D(N_MGN_SPC+6) = ' '
!      VTYPE3D(N_MGN_SPC+6) = M3REAL
!      VDESC3D(N_MGN_SPC+6) = ' '

YVNAME3D(N_MGN_SPC+7) = 'SLTYP'
!      UNITS3D(N_MGN_SPC+7) = ' '
!      VTYPE3D(N_MGN_SPC+7) = M3INT
!      VDESC3D(N_MGN_SPC+7) = ' '

!-----------------------------------------------------------------------
!.....2) PROCESS EMISSION RATES
!-----------------------------------------------------------------------
!
INP = SIZE(PLAT)
!
! ************************************************************************************************

!       PPFD: SRAD - SHORT WAVE FROM SUN (W/M2)
!       ASSUMING 4.766 (UMOL M-2 S-1) PER (W M-2)
!       ASSUME 1/2 OF SRAD IS IN 400-700NM BAND
!D_PPFD = D_PPFD * 4.766 * 0.5
! UPG PT bug: SURFEX give PAR in UMOL M-2 S-1 : comment the lines above
!ZPFD_D = PPFD_D * 4.5 * 0.5

ZPFD_D = PPFD_D

!PPFD   = PPFD   * 4.5
!UPG PT end bug
! *****************************************************************************************

! GO OVER ALL THE CHEMICAL SPECIES
DO JS = 1, N_MGN_SPC

  ! INITIALIZE VARIABLES
  ZER = 0.
  ZGAM_LHT = 1.
  ZGAM_AGE = 1.
  ZGAM_SMT = 1.
  ZGAM_TLD = 1.
  ZGAM_TLI = 1.
  
  PCFNO    = 1.
  PCFNOG   = 1.

  CALL GAMMA_LAI(PLAIC, ZGAM_LHT)

  CALL GAMMA_A(KDATE, KTIME, NTSTLEN, YVNAME3D(JS), PTEMP_D, PLAIP, PLAIC, ZGAM_AGE)

  CALL GAMMA_S(ZGAM_SMT)

  ZADJUST_FACTOR_LD(:) = 0.0
  ZADJUST_FACTOR_LI(:) = 0.0
  ZGAMMA_TD(:) = 0.0
  ZGAMMA_TI(:) = 0.0
  ZTOTALPFT(:) = 0.0

  DO I_PFT = 1,N_MGN_PFT  !CANOPY TYPES
    ZTOTALPFT(:) = ZTOTALPFT(:) + PFTF(I_PFT,:) * 0.01
  ENDDO   ! ENDDO I_PFT

  DO I_PFT = 1,N_MGN_PFT  !CANOPY TYPES

    CALL GAMME_CE(KDATE, KTIME, XCANOPYCHAR, I_PFT, YVNAME3D(JS), &
                  ZPFD_D, ZPFD_D, PTEMP_D, PTEMP_D, PDI,          &
                  PPFD, PLAT, PLONG, PTEMP, PWIND, PQV, PLAIC,    &
                  PRES,  ZGAMMA_TD, ZGAMMA_TI)

    ZADJUST_FACTOR_LD(:) = ZADJUST_FACTOR_LD(:) + 0.01 * PFTF(I_PFT,:) * ZGAMMA_TD(:)
    ZADJUST_FACTOR_LI(:) = ZADJUST_FACTOR_LI(:) + 0.01 * PFTF(I_PFT,:) * ZGAMMA_TI(:)

  ENDDO   ! ENDDO I_PFT

  WHERE (ZTOTALPFT(:).GT.0.) 
    ZGAM_TLD(:) = ZADJUST_FACTOR_LD(:) / ZTOTALPFT(:)
    ZGAM_TLI(:) = ZADJUST_FACTOR_LI(:) / ZTOTALPFT(:)
  ELSEWHERE
    ZGAM_TLD(:) = 1.
    ZGAM_TLI(:) = 1.
  END WHERE

  INMAP = INDEX1(YVNAME3D(JS), CMGN_SPC)
  ZLDF  = XLDF_FCT(INMAP)
  INMAP = INDEX1(YVNAME3D(JS), CMGN_SPC)
  ZRHO  = XMGN_MWT(INMAP)

!...  CALCULATE EMISSION
  ZER(:) = ZGAM_AGE * ZGAM_SMT * ZRHO * ((1.-ZLDF) * ZGAM_TLI * ZGAM_LHT + ZLDF * ZGAM_TLD)
  WHERE( ZER(:).GT.0. ) 
    PCFSPEC(JS,:) = ZER(:)
  ELSEWHERE
    PCFSPEC(JS,:) = 0.0
  ENDWHERE

ENDDO

!...  ESTIATE CFNO AND CFNOG
CALL SOILNOX(KDATE, KTIME, OSOIL, KSLTYP, PRECADJ, &
             PLAT, PTEMP, PSOILM, PSOILT, PLAIC,  PCFNO, PCFNOG )

!--=====================================================================
END SUBROUTINE EMPROC

