!-----------------------------------------------------------------
!--------------- special set of characters for RCS information
!-----------------------------------------------------------------
! $Source$ $Revision$
! ECMWF_RAD2_OLD2NEW 2003/02/19 13:36:50
!-----------------------------------------------------------------
SUBROUTINE SUAERV &
 &(  KLON,   KLEV  , PETAH &
 &,  PVDAES, PVDAEL, PVDAEU, PVDAED &
 &,  PTRBGA, PVOBGA, PSTBGA, PAEOPS, PAEOPL, PAEOPU &
 &,  PAEOPD, PTRPT , PAEADK, PAEADM, PAEROS &
 &)


!**** *SUAERV* - PARAMETERS FOR THE VERTICAL DISTRIBUTIONS OF AEROSOLS.

!     PURPOSE.
!     --------

!          THIS ROUTINE COMPUTES THE VALUES *PVDAEN* (*N=*S,*L,*U OR *D
!     FOR SEA,LAND,URBAN OR DESERT) OF A SURFACE-NORMALISED VERTICAL
!     DISTRIBUTION OF AEROSOLS' OPTICAL DEPHTS FROM THE ARGUMENT *PETAH*
!     (VERTICAL COORDINATE) AT *KLEVP1* LEVELS. IT ALSO SETS VALUES FOR
!     NON-GEOGRAPHICALLY WEIGHTED TOTAL OPTICAL DEPTHS (AT 0.55 E-06
!     WAVE-LENGTH) *PAEOPN* FOR THE SAME FOUR TYPES AND SIMILEAR OPTICAL
!     DEPHTS DIVIDED BY PRESSURE FOR BACKGROUND WELL-MIXED AEROSOLS
!     OF THREE TYPES *PMNBGA* (*MN*=*TR*,*VO* OR *ST* FOR TROPOSPHERIC,
!     VOLCANIC (STRATOSPHERIC ASHES) OR STRATOSPHERIC (SULFURIC TYPE)).
!     IT FINALLY SET VALUES FOR THE POWER TO BE APPLIED TO A TEMPERATURE
!     RATIO SMALLER THAN ONE IN ORDER TO OBTAIN AN IDEX ONE IN THE
!     STRATOSPHERE AND ZERO IN THE TROPOSPHERE WITH A RELATIVELY SMOOTH
!     TRANSITION (*PTRPT*), AS WELL AS FOR ADSORPTION COEFFICIENTS FOR
!     WATER TO THE THREE TYPE OF TROPOSPHERIC AEROSOLS (*PAEADK*) WITH
!     A MINIMUM VALUE (IN THE WHOLE ATMOSPHERE) FOR THE SUM OF THE
!     PRODUCTS OF *PAEADK* BY THE OPTICAL DEPTHS DIVIDED BY PRESURE
!     THICKNESS: *PAEADM*.

!**   INTERFACE.
!     ----------

!          *SUAERV* IS CALLED FROM *SUECRAD*.
!          THERE ARE SIXTEEN DUMMY ARGUMENTS: *PETAH* IS THE VERTICAL
!     COORDINATE.
!                                             *PVDAEN* (*N=*S,*L,*U OR
!     *D) ARE THE NORMALISED VERTICAL DISTRIBUTIONS.
!                                             *KLEVP1* IS THE NUMBER OF
!     LEVELS.
!                                             *PMNBGA* (*MN*=*TR*,*VO*
!     OR *ST*) ARE THE BACKGROUND OPTICAL DEPTHS DIVIDED BY PRESSURE.
!                                             *PAEOPN* (*N=*S,*L,*U OR
!     *D) ARE THE TOTAL OPTICAL DEPHTS FOR THE VERTICALLY VARYING
!     AEROSOLS.
!                                             *PTRPT* IS THE TEMPERATURE
!     EXPONENT FOR THE STRATOSPHERIC DEFINITION.
!                                             *PAEADK* (1,2,3) AND
!     AND *PAEADM* ARE THE CONSTANTS FOR THE DEFINITION OF THE QUANTITY
!     OF WATER VAPOUR THAT WILL BE ADSORBED TO THE DRY AEROSOLS TO FORM
!     MOIST AEROSOLS.

!     METHOD.
!     -------

!          STRAIGHTFORWARD, EQUIVALENT HEIGTHS ARE GIVEN IN METERS (8434
!     FOR THE ATMOSPHERE) AND TROPOSPHERIC AND STRATOSPHERIC PRESSURE
!     BOUNDARY VALUES ARE SET AT 101325 AND 19330 *PASCAL.

!     EXTERNALS.
!     ----------

!          NONE.

!     REFERENCE.
!     ----------

!          NONE.

!     AUTHOR
!     ------
!     J.-F. GELEYN     E.C.M.W.F.     04/11/82.

!     MODIFICATIONS
!     -------------
!     J.-J. MORCRETTE  E.C.M.W.F.     91/07/14   ADAPTATION TO I.F.S.
!     JJ Morcrette 960903  block data SUAERH moved after end of SUAERV

!     ------------------------------------------------------------------



#include "tsmbkind.h"

IMPLICIT NONE


!     DUMMY INTEGER SCALARS
INTEGER_M :: KLON
INTEGER_M :: KLEV

!     DUMMY REAL SCALARS
REAL_B :: PAEADM
REAL_B :: PAEOPD
REAL_B :: PAEOPL
REAL_B :: PAEOPS
REAL_B :: PAEOPU
REAL_B :: PAEROS
REAL_B :: PSTBGA
REAL_B :: PTRBGA
REAL_B :: PTRPT
REAL_B :: PVOBGA

!modif passage 3D
!     ------------------------------------------------------------------
REAL_B :: PETAH(KLON, KLEV+1)

REAL_B :: PVDAES(KLON,KLEV+1),PVDAEL(KLON,KLEV+1),PVDAEU(KLON,KLEV+1),PVDAED(KLON,KLEV+1)
REAL_B :: PAEADK(3)

!     LOCAL INTEGER SCALARS
INTEGER_M :: JK

!     LOCAL REAL SCALARS
REAL_B :: ZHSD, ZHSL, ZHSS, ZHSU


!     ------------------------------------------------------------------


!*         1.     COMPUTATIONS.
!                 -------------



ZHSS=MAX(_ONE_,8434._JPRB/1000._JPRB)
ZHSL=MAX(_ONE_,8434._JPRB/1000._JPRB)
ZHSU=MAX(_ONE_,8434._JPRB/1000._JPRB)
ZHSD=MAX(_ONE_,8434._JPRB/3000._JPRB)
PVDAES(:,1)=0._JPRB
PVDAEL(:,1)=0._JPRB
PVDAEU(:,1)=0._JPRB
PVDAED(:,1)=0._JPRB
WHERE (PETAH(:,1) /= _ZERO_) 
  PVDAES(:,1)=PETAH(:,1)**ZHSS
  PVDAEL(:,1)=PETAH(:,1)**ZHSL
  PVDAEU(:,1)=PETAH(:,1)**ZHSU
  PVDAED(:,1)=PETAH(:,1)**ZHSD
END WHERE
DO JK=2,KLEV+1
  PVDAES(:,JK)=PETAH(:,JK)**ZHSS
  PVDAEL(:,JK)=PETAH(:,JK)**ZHSL
  PVDAEU(:,JK)=PETAH(:,JK)**ZHSU
  PVDAED(:,JK)=PETAH(:,JK)**ZHSD
ENDDO
PTRBGA= 0.03_JPRB/(101325._JPRB-19330._JPRB)
PVOBGA= 0.007_JPRB/19330._JPRB
PSTBGA= 0.045_JPRB/19330._JPRB
PAEOPS= 0.05_JPRB
PAEOPL= 0.2_JPRB
PAEOPU= 0.1_JPRB
PAEOPD= 1.9_JPRB
PTRPT = 30._JPRB
PAEADK(1)=+.3876E-03_JPRB
PAEADK(2)=+.6693E-02_JPRB
PAEADK(3)=+.8563E-03_JPRB
PAEADM   = 2.6E-10_JPRB

PAEROS   = 0.1462E-16_JPRB

!     ------------------------------------------------------------------

RETURN
END SUBROUTINE SUAERV




