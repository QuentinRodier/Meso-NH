!SURFEX_LIC Copyright 1994-2014 Meteo-France 
!SURFEX_LIC This is part of the SURFEX software governed by the CeCILL-C  licence
!SURFEX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt
!SURFEX_LIC for details. version 1.
!     #########
      SUBROUTINE ZOOM_PGD_OROGRAPHY(HPROGRAM,PSEA,PWATER,HINIFILE,HINIFILETYPE)
!     ###########################################################

!!
!!    PURPOSE
!!    -------
!!   This program prepares the physiographic data fields.
!!
!!    METHOD
!!    ------
!!   
!!    EXTERNAL
!!    --------
!!
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------
!!
!!
!!    REFERENCE
!!    ---------
!!
!!    AUTHOR
!!    ------
!!
!!    V. Masson                   Meteo-France
!!
!!    MODIFICATION
!!    ------------
!!
!!    Original     13/10/03
!----------------------------------------------------------------------------
!
!*    0.     DECLARATION
!            -----------
!
USE MODD_DATA_COVER_PAR,   ONLY : JPCOVER
USE MODD_SURF_ATM_n,       ONLY : XZS
USE MODD_SURF_ATM_GRID_n,  ONLY : XLAT, XLON, CGRID, XGRID_PAR
USE MODD_SURF_ATM_SSO_n,   ONLY : XSSO_STDEV, XAVG_ZS, XSIL_ZS, XMIN_ZS, XMAX_ZS,&
                                    XSSO_ANIS, XSSO_DIR, XSSO_SLOPE,               &
                                    XAOSIP, XAOSIM, XAOSJP, XAOSJM,                &
                                    XHO2IP, XHO2IM, XHO2JP, XHO2JM  
USE MODD_PREP,             ONLY : CINGRID_TYPE, CINTERP_TYPE, LINTERP
USE MODD_SURF_PAR,         ONLY : XUNDEF
!
USE MODI_OPEN_AUX_IO_SURF
USE MODI_READ_SURF
USE MODI_CLOSE_AUX_IO_SURF
USE MODI_PREP_GRID_EXTERN
USE MODI_HOR_INTERPOL
USE MODI_PREP_OUTPUT_GRID
!
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
USE MODI_CLEAN_PREP_OUTPUT_GRID
!
USE MODI_GET_LUOUT
IMPLICIT NONE
!
!*    0.1    Declaration of dummy arguments
!            ------------------------------
!
 CHARACTER(LEN=6),     INTENT(IN)  :: HPROGRAM    ! program calling
REAL, DIMENSION(:),   INTENT(IN)  :: PSEA        ! sea fraction
REAL, DIMENSION(:),   INTENT(IN)  :: PWATER      ! inland water fraction
 CHARACTER(LEN=28),    INTENT(IN)  :: HINIFILE    ! input atmospheric file name
 CHARACTER(LEN=6),     INTENT(IN)  :: HINIFILETYPE! input atmospheric file type
!
!
!*    0.2    Declaration of local variables
!            ------------------------------
!
INTEGER :: IRESP
INTEGER :: ILUOUT
INTEGER :: INI     ! total 1D dimension (input field)
INTEGER :: IL      ! total 1D dimension (output field)
REAL, DIMENSION(:), POINTER :: ZZS
REAL, DIMENSION(:), POINTER :: ZAVG_ZS
REAL, DIMENSION(:), POINTER :: ZSIL_ZS
REAL, DIMENSION(:), POINTER :: ZSSO_STDEV
REAL, DIMENSION(:), POINTER :: ZMIN_ZS
REAL, DIMENSION(:), POINTER :: ZMAX_ZS
REAL, DIMENSION(:), POINTER :: ZSSO_ANIS
REAL, DIMENSION(:), POINTER :: ZSSO_DIR
REAL, DIMENSION(:), POINTER :: ZSSO_SLOPE
REAL, DIMENSION(:), POINTER :: ZAOSIP
REAL, DIMENSION(:), POINTER :: ZAOSIM
REAL, DIMENSION(:), POINTER :: ZAOSJP
REAL, DIMENSION(:), POINTER :: ZAOSJM
REAL, DIMENSION(:), POINTER :: ZHO2IP
REAL, DIMENSION(:), POINTER :: ZHO2IM
REAL, DIMENSION(:), POINTER :: ZHO2JP
REAL, DIMENSION(:), POINTER :: ZHO2JM
 CHARACTER(LEN=12) :: YRECFM         ! Name of the article to be read
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ZOOM_PGD_OROGRAPHY',0,ZHOOK_HANDLE)
 CALL GET_LUOUT(HPROGRAM,ILUOUT)
!
!*      1.     Preparation of IO for reading in the file
!              -----------------------------------------
!
!* Note that all points are read, even those without physical meaning.
!  These points will not be used during the horizontal interpolation step.
!  Their value must be defined as XUNDEF.
!
 CALL OPEN_AUX_IO_SURF(HINIFILE,HINIFILETYPE,'FULL  ')
!
!------------------------------------------------------------------------------
!
!*      2.     Reading of grid
!              ---------------
!
 CALL PREP_GRID_EXTERN(HINIFILETYPE,ILUOUT,CINGRID_TYPE,CINTERP_TYPE,INI)
!
 CALL PREP_OUTPUT_GRID(ILUOUT,CGRID,XGRID_PAR,XLAT,XLON)
!
!------------------------------------------------------------------------------
!
!*      3.     Reading of orographic parameters
!              --------------------------------
!
ALLOCATE(ZZS        (INI))
!
ALLOCATE(ZAVG_ZS    (INI))
ALLOCATE(ZSIL_ZS    (INI))
ALLOCATE(ZSSO_STDEV (INI))
ALLOCATE(ZMIN_ZS    (INI))
ALLOCATE(ZMAX_ZS    (INI))
!
ALLOCATE(ZSSO_ANIS  (INI))
ALLOCATE(ZSSO_DIR   (INI))
ALLOCATE(ZSSO_SLOPE (INI))
!
ALLOCATE(ZAOSIP     (INI))
ALLOCATE(ZAOSIM     (INI))
ALLOCATE(ZAOSJP     (INI))
ALLOCATE(ZAOSJM     (INI))
ALLOCATE(ZHO2IP     (INI))
ALLOCATE(ZHO2IM     (INI))
ALLOCATE(ZHO2JP     (INI))
ALLOCATE(ZHO2JM     (INI))
!
YRECFM='ZS'
 CALL READ_SURF(HPROGRAM,YRECFM,ZZS,IRESP,HDIR='A')
!
YRECFM='AVG_ZS'
 CALL READ_SURF(HPROGRAM,YRECFM,ZAVG_ZS,IRESP,HDIR='A')
YRECFM='SIL_ZS'
 CALL READ_SURF(HPROGRAM,YRECFM,ZSIL_ZS,IRESP,HDIR='A')
YRECFM='SSO_STDEV'
 CALL READ_SURF(HPROGRAM,YRECFM,ZSSO_STDEV,IRESP,HDIR='A')
YRECFM='MIN_ZS'
 CALL READ_SURF(HPROGRAM,YRECFM,ZMIN_ZS,IRESP,HDIR='A')
YRECFM='MAX_ZS'
 CALL READ_SURF(HPROGRAM,YRECFM,ZMAX_ZS,IRESP,HDIR='A')
!
YRECFM='SSO_ANIS'
 CALL READ_SURF(HPROGRAM,YRECFM,ZSSO_ANIS,IRESP,HDIR='A')
YRECFM='SSO_DIR'
 CALL READ_SURF(HPROGRAM,YRECFM,ZSSO_DIR,IRESP,HDIR='A')
YRECFM='SSO_SLOPE'
 CALL READ_SURF(HPROGRAM,YRECFM,ZSSO_SLOPE,IRESP,HDIR='A')
!
YRECFM='AOSIP'
 CALL READ_SURF(HPROGRAM,YRECFM,ZAOSIP,IRESP,HDIR='A')
YRECFM='AOSIM'
 CALL READ_SURF(HPROGRAM,YRECFM,ZAOSIM,IRESP,HDIR='A')
YRECFM='AOSJP'
 CALL READ_SURF(HPROGRAM,YRECFM,ZAOSJP,IRESP,HDIR='A')
YRECFM='AOSJM'
 CALL READ_SURF(HPROGRAM,YRECFM,ZAOSJM,IRESP,HDIR='A')
YRECFM='HO2IP'
 CALL READ_SURF(HPROGRAM,YRECFM,ZHO2IP,IRESP,HDIR='A')
YRECFM='HO2IM'
 CALL READ_SURF(HPROGRAM,YRECFM,ZHO2IM,IRESP,HDIR='A')
YRECFM='HO2JP'
 CALL READ_SURF(HPROGRAM,YRECFM,ZHO2JP,IRESP,HDIR='A')
YRECFM='HO2JM'
 CALL READ_SURF(HPROGRAM,YRECFM,ZHO2JM,IRESP,HDIR='A')
!
 CALL CLOSE_AUX_IO_SURF(HINIFILE,HINIFILETYPE)
!------------------------------------------------------------------------------
!
!*      4.     Interpolations
!              --------------
!
IL = SIZE(XLAT)
!
ALLOCATE(XZS        (IL))
!
ALLOCATE(XAVG_ZS    (IL))
ALLOCATE(XSIL_ZS    (IL))
ALLOCATE(XSSO_STDEV (IL))
ALLOCATE(XMIN_ZS    (IL))
ALLOCATE(XMAX_ZS    (IL))
!
ALLOCATE(XSSO_ANIS  (IL))
ALLOCATE(XSSO_DIR   (IL))
ALLOCATE(XSSO_SLOPE (IL))
!
ALLOCATE(XAOSIP     (IL))
ALLOCATE(XAOSIM     (IL))
ALLOCATE(XAOSJP     (IL))
ALLOCATE(XAOSJM     (IL))
ALLOCATE(XHO2IP     (IL))
ALLOCATE(XHO2IM     (IL))
ALLOCATE(XHO2JP     (IL))
ALLOCATE(XHO2JM     (IL))
!
 CALL ZOOM(ILUOUT,ZZS,XZS)
 CALL ZOOM(ILUOUT,ZAVG_ZS,XAVG_ZS)
 CALL ZOOM(ILUOUT,ZSIL_ZS,XSIL_ZS)
 CALL ZOOM(ILUOUT,ZMIN_ZS,XMIN_ZS)
 CALL ZOOM(ILUOUT,ZMAX_ZS,XMAX_ZS)
!
LINTERP(:)=(PSEA(:)<1.)
 CALL ZOOM(ILUOUT,ZSSO_STDEV,XSSO_STDEV)
 CALL ZOOM(ILUOUT,ZSSO_ANIS,XSSO_ANIS)
 CALL ZOOM(ILUOUT,ZSSO_DIR,XSSO_DIR)
 CALL ZOOM(ILUOUT,ZSSO_SLOPE,XSSO_SLOPE)
 CALL ZOOM(ILUOUT,ZAOSIP,XAOSIP)
 CALL ZOOM(ILUOUT,ZAOSIM,XAOSIM)
 CALL ZOOM(ILUOUT,ZAOSJP,XAOSJP)
 CALL ZOOM(ILUOUT,ZAOSJM,XAOSJM)
 CALL ZOOM(ILUOUT,ZHO2IP,XHO2IP)
 CALL ZOOM(ILUOUT,ZHO2IM,XHO2IM)
 CALL ZOOM(ILUOUT,ZHO2JP,XHO2JP)
 CALL ZOOM(ILUOUT,ZHO2JM,XHO2JM)
!
!* coherence with land sea mask
!
WHERE(PSEA==1.) XZS=0.
WHERE(PSEA(:)==1.) XSSO_STDEV(:) = XUNDEF
WHERE(PWATER(:)==1.) XSSO_STDEV(:) = 0.
WHERE(PSEA(:)>0.) XMIN_ZS(:) = 0.
WHERE(PSEA(:)==1.) XMAX_ZS(:) = 0.
!
WHERE (PSEA(:)==1.)
  XSSO_ANIS (:) = XUNDEF
  XSSO_DIR  (:) = XUNDEF
  XSSO_SLOPE(:) = XUNDEF
END WHERE
!
WHERE (PWATER(:)==1.)
  XSSO_ANIS (:) = 1.
  XSSO_DIR  (:) = 0.
  XSSO_SLOPE(:) = 0.
END WHERE
!
WHERE (PSEA(:)==1.)
  XHO2IP(:) = XUNDEF
  XHO2IM(:) = XUNDEF
  XHO2JP(:) = XUNDEF
  XHO2JM(:) = XUNDEF
  XAOSIP(:) = XUNDEF
  XAOSIM(:) = XUNDEF
  XAOSJP(:) = XUNDEF
  XAOSJM(:) = XUNDEF
END WHERE
!
WHERE (PWATER(:)==1.)
  XHO2IP(:) = 0.
  XHO2IM(:) = 0.
  XHO2JP(:) = 0.
  XHO2JM(:) = 0.
  XAOSIP(:) = 0.
  XAOSIM(:) = 0.
  XAOSJP(:) = 0.
  XAOSJM(:) = 0.
END WHERE
!_______________________________________________________________________________
DEALLOCATE(ZZS        )
!
DEALLOCATE(ZAVG_ZS    )
DEALLOCATE(ZSIL_ZS    )
DEALLOCATE(ZSSO_STDEV )
DEALLOCATE(ZMIN_ZS    )
DEALLOCATE(ZMAX_ZS    )
!
DEALLOCATE(ZSSO_ANIS  )
DEALLOCATE(ZSSO_DIR   )
DEALLOCATE(ZSSO_SLOPE )
!
DEALLOCATE(ZAOSIP     )
DEALLOCATE(ZAOSIM     )
DEALLOCATE(ZAOSJP     )
DEALLOCATE(ZAOSJM     )
DEALLOCATE(ZHO2IP     )
DEALLOCATE(ZHO2IM     )
DEALLOCATE(ZHO2JP     )
DEALLOCATE(ZHO2JM     )
!_______________________________________________________________________________
 CALL CLEAN_PREP_OUTPUT_GRID
!_______________________________________________________________________________
IF (LHOOK) CALL DR_HOOK('ZOOM_PGD_OROGRAPHY',1,ZHOOK_HANDLE)
CONTAINS
!
SUBROUTINE ZOOM(KLUOUT,PFIELDIN,PFIELDOUT)
INTEGER, INTENT(IN)             :: KLUOUT
REAL, DIMENSION(:), POINTER     :: PFIELDIN
REAL, DIMENSION(:), INTENT(OUT) :: PFIELDOUT
REAL, DIMENSION(:,:), POINTER   :: ZFIELDIN
REAL, DIMENSION(:,:), POINTER   :: ZFIELDOUT
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('ZOOM',0,ZHOOK_HANDLE)
ALLOCATE(ZFIELDIN (SIZE(PFIELDIN, 1),1))
ALLOCATE(ZFIELDOUT(SIZE(PFIELDOUT,1),1))
ZFIELDIN(:,1) = PFIELDIN(:)
 CALL HOR_INTERPOL(KLUOUT,ZFIELDIN,ZFIELDOUT)
PFIELDOUT(:) = ZFIELDOUT(:,1)
DEALLOCATE(ZFIELDIN )
DEALLOCATE(ZFIELDOUT)
IF (LHOOK) CALL DR_HOOK('ZOOM',1,ZHOOK_HANDLE)

END SUBROUTINE ZOOM
!
END SUBROUTINE ZOOM_PGD_OROGRAPHY
