SUBROUTINE GET_SIZES_PARALLEL(KPROC,KSIZE,KPROCMIN,KSIZE_TASK)
!
USE MODD_SURFEX_MPI, ONLY : WLOG_MPI
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
INTEGER, INTENT(IN) :: KPROC
INTEGER, INTENT(IN) :: KSIZE
INTEGER, INTENT(IN) :: KPROCMIN
INTEGER, DIMENSION(0:KPROC-1), INTENT(OUT) :: KSIZE_TASK
!
INTEGER :: ISIZE, IRESTE, INRESTE
INTEGER :: J, IPROC
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
IF (LHOOK) CALL DR_HOOK('GET_SIZES_PARALLEL',0,ZHOOK_HANDLE)
!
ISIZE = CEILING(KSIZE*1./KPROC) !nb of points by task
!
INRESTE = KPROC*ISIZE - KSIZE ! nb of tasks containing ireste points
IF (INRESTE>0) THEN
  IRESTE = ISIZE - 1
ELSE
  IRESTE = ISIZE
ENDIF
!
KSIZE_TASK(:) = ISIZE
IF (INRESTE>0) THEN
  DO J = KPROCMIN+KPROC-INRESTE,KPROCMIN+KPROC-1
    KSIZE_TASK(MOD(J,KPROC)) = IRESTE
  ENDDO
ENDIF
!
!so:
!(nproc-nreste)*isize + nreste*ireste = 
!if nreste==1: 
!(nproc-1)*isize + ireste = 
!(nproc-1)*isize + NDIM_FULL - (nproc-1)*isize = NDIM_FULL
!if (ireste==isize-1):
!nproc*isize - nreste*isize + nreste*isize - nerste = 
!nproc*isize - nreste = NDIM_FULL
!
IF (LHOOK) CALL DR_HOOK('GET_SIZES_PARALLEL',1,ZHOOK_HANDLE)
!
END SUBROUTINE GET_SIZES_PARALLEL
