
!     ##########################
      SUBROUTINE BUDGET_COUPL_ROUT(KNI,KFORC_STEP)
!     ##########################
!
!!
!!    PURPOSE
!!    -------
!        
!     
!!**  METHOD
!!    ------
!
!!    EXTERNAL
!!    --------
!!
!!    none
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------ 
!!      
!!    REFERENCE
!!    ---------
!!     
!!    AUTHOR
!!    ------
!!
!!    L. Bouilloud &  B. Vincendon	* Meteo-France *
!!
!!    MODIFICATIONS
!!    -------------
!!
!!      Original  03/2008 
!-------------------------------------------------------------------------------
!
!*       0.     DECLARATIONS
!               ------------
USE MODD_BUDGET_COUPL_ROUT ! contains all useful variables XB_*
USE MODD_TOPODYN,       ONLY : NNCAT, XQTOT, XTOPD_STEP, XQB_DR, XQB_RUN
USE MODD_COUPLING_TOPD, ONLY : XRUNOFF_TOP, XRUN_TOROUT, XDR_TOROUT
!
USE MODD_SURF_PAR
USE MODD_CSTS,            ONLY : XRHOLW
!
USE MODD_ISBA_n,          ONLY : XWG, XDG, XWR, CRUNOFF, XWGI, TSNOW
USE MODD_DIAG_EVAP_ISBA_n,ONLY : XAVG_EVAPC, XAVG_LEGC, XAVG_RUNOFFC, XAVG_DRAINC,&
                                 XAVG_DRAIN, XAVG_RUNOFF, XAVG_EVAP !ludo
USE MODD_ISBA_GRID_n,     ONLY : XMESH_SIZE !ludo
USE MODD_FORC_ATM,        ONLY : XSNOW       ,&! snow precipitation                    (kg/m2/s)
                                 XRAIN         ! liquid precipitation                  (kg/m2/s)
USE MODD_SURF_ATM_n,       ONLY:NR_NATURE
!
USE MODI_UNPACK_SAME_RANK
USE MODI_PACK_SAME_RANK 
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
!*      0.1    declarations of arguments
INTEGER, INTENT(IN)           :: KNI        ! expected physical size of full surface array
INTEGER, INTENT(IN)           :: KFORC_STEP ! time step
!
!*      0.2    declarations of local variables
REAL                          :: ZFACT0, ZFACT1, ZFACT2
INTEGER                       :: JMESH,JCAT
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!-------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('BUDGET_COUPL_ROUT',0,ZHOOK_HANDLE)
!
!*       1.     Budget computation:
!               ---------------
!
XB_RAIN(:)=XRAIN(:)  !kg/m2/s
XB_SNOW(:)=XSNOW(:)  !kg/m2/s
!
 CALL UNPACK_SAME_RANK(NR_NATURE,XWR(:,1), XB_WR)
 CALL UNPACK_SAME_RANK(NR_NATURE,XAVG_EVAPC, XB_EVAP)        
!
 CALL UNPACK_SAME_RANK(NR_NATURE,XAVG_RUNOFFC,XB_RUNOFF_ISBA)
IF (CRUNOFF=='TOPD') THEN
  CALL UNPACK_SAME_RANK(NR_NATURE,XRUNOFF_TOP,XB_RUNOFF_TOPD)
ELSE
  XB_RUNOFF_TOPD = XB_RUNOFF_ISBA
ENDIF
!
 CALL UNPACK_SAME_RANK(NR_NATURE,XAVG_DRAINC, XB_DRAIN)
!
 CALL UNPACK_SAME_RANK(NR_NATURE,XWG(:,2,1)  ,XB_WG2)
 CALL UNPACK_SAME_RANK(NR_NATURE,XWG(:,3,1)  ,XB_WG3)
WHERE ( XB_WG2/=XUNDEF .AND. XB_DG2/=XUNDEF .AND. XB_WG3/=XUNDEF .AND. XB_DG3/=XUNDEF )
  XB_WGTOT(:) = XB_WG2(:)*XB_DG2(:) + XB_WG3(:)*(XB_DG3(:)-XB_DG2(:)) !m3/m2
ELSEWHERE
  XB_WGTOT(:) = XUNDEF
ENDWHERE
!
 CALL UNPACK_SAME_RANK(NR_NATURE,XWGI(:,2,1) ,XB_WGI2)
 CALL UNPACK_SAME_RANK(NR_NATURE,XWGI(:,3,1) ,XB_WGI3)
WHERE ( XB_WGI2/=XUNDEF .AND. XB_DG2/=XUNDEF .AND. XB_WGI3/=XUNDEF .AND. XB_DG3/=XUNDEF )
  XB_WGITOT(:) = XB_WGI2(:)*XB_DG2(:) + XB_WGI3(:)*(XB_DG3(:)-XB_DG2(:)) !m3/m2
ELSEWHERE
  XB_WGTOT(:) = XUNDEF
ENDWHERE
!
 CALL UNPACK_SAME_RANK(NR_NATURE,TSNOW%WSNOW(:,1,1),XB_SWE1)
 CALL UNPACK_SAME_RANK(NR_NATURE,TSNOW%WSNOW(:,2,1),XB_SWE2)
 CALL UNPACK_SAME_RANK(NR_NATURE,TSNOW%WSNOW(:,3,1),XB_SWE3)
XB_SWETOT(:) = XB_SWE1(:)+XB_SWE2(:)+XB_SWE3(:)
!
DO JCAT=1,NNCAT
  XB_QTOT(JCAT) = SUM(XQTOT(JCAT,1:KFORC_STEP))
  XB_QRUN(JCAT) = SUM(XQB_RUN(JCAT,1:KFORC_STEP))
  XB_QDR(JCAT)  = SUM(XQB_DR(JCAT,1:KFORC_STEP))
ENDDO
!
DO JCAT=1,NNCAT
  !
  DO JMESH=1,KNI
    !
    IF ( XB_DG2(JMESH)/=XUNDEF ) THEN
!!    Water going in the system
      ZFACT0 =  XB_ABV_BYMESH(JMESH,JCAT) * XB_MESH_SIZE(JMESH)
      ZFACT1 =  ZFACT0 / XRHOLW
      ZFACT2 =  XTOPD_STEP * ZFACT1
      !
!!      variable =1 : Rain      
      XB_VAR_BV(KFORC_STEP,JCAT,1) = XB_VAR_BV(KFORC_STEP,JCAT,1) + XB_RAIN(JMESH) * ZFACT2
!!      variable =2 : Snow
      XB_VAR_BV(KFORC_STEP,JCAT,2) = XB_VAR_BV(KFORC_STEP,JCAT,2) + XB_SNOW(JMESH) * ZFACT2
!!    Water going out of the system
!!      variable =3 : Incerception 
      XB_VAR_BV(KFORC_STEP,JCAT,3) = XB_VAR_BV(KFORC_STEP,JCAT,3) + (XB_WR(JMESH)-XB_WRM(JMESH)) * ZFACT1
!!      variable =4 : Evaporation
      XB_VAR_BV(KFORC_STEP,JCAT,4) = XB_VAR_BV(KFORC_STEP,JCAT,4) + (XB_EVAP(JMESH)-XB_EVAPM(JMESH)) * ZFACT1
!!      variable =5 : Runoff
      XB_VAR_BV(KFORC_STEP,JCAT,5) = XB_VAR_BV(KFORC_STEP,JCAT,5) + (XB_RUNOFF_TOPD(JMESH)-XB_RUNOFF_TOPDM(JMESH)) * ZFACT1
!!      variable =6 : Drainage
      XB_VAR_BV(KFORC_STEP,JCAT,6) = XB_VAR_BV(KFORC_STEP,JCAT,6) + (XB_DRAIN(JMESH)-XB_DRAINM(JMESH)) * ZFACT1
!!      variable =7 : Variation of liquid water stocked in the ground
      XB_VAR_BV(KFORC_STEP,JCAT,7) = XB_VAR_BV(KFORC_STEP,JCAT,7) + (XB_WGTOT(JMESH)-XB_WGTOTM(JMESH)) * ZFACT0   
!!      variable =8 : Variation of solid water stocked in the ground
      XB_VAR_BV(KFORC_STEP,JCAT,8) = XB_VAR_BV(KFORC_STEP,JCAT,8) + (XB_WGITOT(JMESH)-XB_WGITOTM(JMESH)) * ZFACT0 
!!      variable =9 : Variation of melting snow
      XB_VAR_BV(KFORC_STEP,JCAT,9) = XB_VAR_BV(KFORC_STEP,JCAT,9) + (XB_SWETOT(JMESH)-XB_SWETOTM(JMESH)) * ZFACT0 

 !! bilan hors BV en m3
      ZFACT0 =  (1.-XB_ABV_BYMESH(JMESH,JCAT)) * XB_MESH_SIZE(JMESH)
      ZFACT1 =  ZFACT0 / XRHOLW
      ZFACT2 =  XTOPD_STEP * ZFACT1
      !
      XB_VAR_NOBV(KFORC_STEP,JCAT,1) = XB_VAR_NOBV(KFORC_STEP,JCAT,1) + XB_RAIN(JMESH) * ZFACT2
      XB_VAR_NOBV(KFORC_STEP,JCAT,2) = XB_VAR_NOBV(KFORC_STEP,JCAT,2) + XB_SNOW(JMESH) * ZFACT2
      XB_VAR_NOBV(KFORC_STEP,JCAT,3) = XB_VAR_NOBV(KFORC_STEP,JCAT,3) + (XB_WR(JMESH)-XB_WRM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,4) = XB_VAR_NOBV(KFORC_STEP,JCAT,4) + (XB_EVAP(JMESH)-XB_EVAPM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,5) = XB_VAR_NOBV(KFORC_STEP,JCAT,5) + (XB_RUNOFF_ISBA(JMESH)-XB_RUNOFF_ISBAM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,6) = XB_VAR_NOBV(KFORC_STEP,JCAT,6) + (XB_DRAIN(JMESH)-XB_DRAINM(JMESH)) * ZFACT1
      XB_VAR_NOBV(KFORC_STEP,JCAT,7) = XB_VAR_NOBV(KFORC_STEP,JCAT,7) + (XB_WGTOT(JMESH)-XB_WGTOTM(JMESH)) * ZFACT0   
      XB_VAR_NOBV(KFORC_STEP,JCAT,8) = XB_VAR_NOBV(KFORC_STEP,JCAT,8) + (XB_WGITOT(JMESH)-XB_WGITOTM(JMESH)) * ZFACT0   
      XB_VAR_NOBV(KFORC_STEP,JCAT,9) = XB_VAR_NOBV(KFORC_STEP,JCAT,9) + (XB_SWETOT(JMESH)-XB_SWETOTM(JMESH)) * ZFACT0 
      !
    ENDIF
    !     
  ENDDO
  !
  XB_VAR_BV(KFORC_STEP,JCAT,10) = SUM(XB_VAR_BV(KFORC_STEP,JCAT,1:2)) - SUM(XB_VAR_BV(KFORC_STEP,JCAT,3:9))
  !
  XB_VAR_NOBV(KFORC_STEP,JCAT,10) =  SUM(XB_VAR_NOBV(KFORC_STEP,JCAT,1:2)) - SUM(XB_VAR_NOBV(KFORC_STEP,JCAT,3:9))
  !
  XB_VAR_Q(KFORC_STEP,JCAT,1) = (XB_QTOT(JCAT)-XB_QTOTM(JCAT)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,2) = (XB_QRUN(JCAT)-XB_QRUNM(JCAT)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,3) = (XB_QDR(JCAT)- XB_QDRM(JCAT)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,4) = SUM(XRUN_TOROUT(JCAT,:)) * XTOPD_STEP
  XB_VAR_Q(KFORC_STEP,JCAT,5) = SUM(XDR_TOROUT(JCAT,:)) * XTOPD_STEP
  !
ENDDO   
!
!bilan tot isba (m3)
DO JMESH=1,KNI
  !
  IF (XB_DG2(JMESH)/=XUNDEF) THEN  
    !
    ZFACT0 =  XB_MESH_SIZE(JMESH)
    ZFACT1 =  ZFACT0 / XRHOLW
    ZFACT2 =  XTOPD_STEP * ZFACT1
    !
!!    Water going in the system
!!      variable =1 : Rain
    XB_VAR_TOT(KFORC_STEP,1) = XB_VAR_TOT(KFORC_STEP,1) + XB_RAIN(JMESH) * ZFACT2
!!      variable =2 : Snow
    XB_VAR_TOT(KFORC_STEP,2) = XB_VAR_TOT(KFORC_STEP,2) + XB_SNOW(JMESH) * ZFACT2
!!    Water going out of the system
!!      variable =3 : Incerception 
    XB_VAR_TOT(KFORC_STEP,3) = XB_VAR_TOT(KFORC_STEP,3) + (XB_WR(JMESH)-XB_WRM(JMESH)) * ZFACT1
!!      variable =4 : Evaporation
    XB_VAR_TOT(KFORC_STEP,4) = XB_VAR_TOT(KFORC_STEP,4) + (XB_EVAP(JMESH)-XB_EVAPM(JMESH)) * ZFACT1
!!      variable =5 : Runoff
    XB_VAR_TOT(KFORC_STEP,5) = XB_VAR_TOT(KFORC_STEP,5) + &
       (XB_RUNOFF_ISBA(JMESH)-XB_RUNOFF_ISBAM(JMESH)) * (1-XB_ATOP_BYMESH(JMESH)) * ZFACT1 + &
       (XB_RUNOFF_TOPD(JMESH)-XB_RUNOFF_TOPDM(JMESH)) * XB_ATOP_BYMESH(JMESH) * ZFACT1
!!      variable =6 : Drainage
    XB_VAR_TOT(KFORC_STEP,6) = XB_VAR_TOT(KFORC_STEP,6) + (XB_DRAIN(JMESH)-XB_DRAINM(JMESH)) * ZFACT1
!!      variable =7 : Variation of liquid water stocked in the ground
    XB_VAR_TOT(KFORC_STEP,7) = XB_VAR_TOT(KFORC_STEP,7) + (XB_WGTOT(JMESH)-XB_WGTOTM(JMESH)) * ZFACT0  
!!      variable =8 : Variation of solid water stocked in the ground
    XB_VAR_TOT(KFORC_STEP,8) = XB_VAR_TOT(KFORC_STEP,8) + (XB_WGITOT(JMESH)-XB_WGITOTM(JMESH)) * ZFACT0  
!!      variable =9 : Variation of melting snow
    XB_VAR_TOT(KFORC_STEP,9) = XB_VAR_TOT(KFORC_STEP,9) + (XB_SWETOT(JMESH)-XB_SWETOTM(JMESH)) * ZFACT0  
    !
  ENDIF
  !
ENDDO !JMESH
!
XB_VAR_TOT(KFORC_STEP,10) = SUM(XB_VAR_TOT(KFORC_STEP,1:2)) - SUM(XB_VAR_TOT(KFORC_STEP,3:9)) 
!
XB_WRM  (:) = XB_WR(:)
XB_EVAPM(:) = XB_EVAP(:)
!
XB_RUNOFF_TOPDM(:) = XB_RUNOFF_TOPD(:)
XB_RUNOFF_ISBAM(:) = XB_RUNOFF_ISBA(:)
XB_DRAINM      (:) = XB_DRAIN(:)
!
XB_WG2M   (:) = XB_WG2(:)
XB_WG3M   (:) = XB_WG3(:)
XB_WGTOTM (:) = XB_WGTOT(:)
XB_WGI2M  (:) = XB_WGI2(:)
XB_WGI3M  (:) = XB_WGI3(:)
XB_WGITOTM(:) = XB_WGITOT(:)
XB_SWE1M  (:) = XB_SWE1(:)
XB_SWE2M  (:) = XB_SWE2(:)
XB_SWE3M  (:) = XB_SWE3(:)
XB_SWETOTM(:) = XB_SWETOT(:)
!
XB_QTOTM(:) = XB_QTOT(:)
XB_QRUNM(:) = XB_QRUN(:) 
XB_QDRM (:) = XB_QDR(:)
!
IF (LHOOK) CALL DR_HOOK('BUDGET_COUPL_ROUT',1,ZHOOK_HANDLE)
!
END SUBROUTINE BUDGET_COUPL_ROUT
