!SURFEX_LIC Copyright 1994-2014 Meteo-France 
!SURFEX_LIC This is part of the SURFEX software governed by the CeCILL-C  licence
!SURFEX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt
!SURFEX_LIC for details. version 1.
!     #########
      SUBROUTINE CONVERT_PATCH_ISBA(HISBA,KDECADE,KDECADE2,PCOVER,HPHOTO,&
                                  OAGRIP,OPERM,HSFTYPE,PVEG,PLAI,        &
                                  PRSMIN,PGAMMA,PWRMAX_CF,PRGL,PCV,      &
                                  PSOILGRID,PDG,KWG_LAYER,PDROOT,PDG2,   &
                                  PZ0,PZ0_O_Z0H,                         &
                                  PALBNIR_VEG,PALBVIS_VEG,PALBUV_VEG,    &
                                  PEMIS_ECO,PVEGTYPE,PROOTFRAC,          &
                                  PGMES,PBSLAI,PLAIMIN,PSEFOLD,PGC,      &
                                  PDMAX, PF2I, OSTRESS, PH_TREE, PRE25,  &
                                  PCE_NITRO, PCF_NITRO, PCNA_NITRO,      &
                                  PD_ICE, PWG1,                          &
                                  PALBNIR_SOIL,PALBVIS_SOIL,PALBUV_SOIL, &
                                  TPSEED, TPREAP, PWATSUP, PIRRIG    )
!     ##############################################################
!
!!**** *CONVERT_PATCH_ISBA* 
!!
!!    PURPOSE
!!    -------
!!
!!    METHOD
!!    ------
!!   
!
!!    EXTERNAL
!!    --------
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------
!!
!!    REFERENCE
!!    ---------
!!
!!    AUTHOR
!!    ------
!!
!!    S. Faroux        Meteo-France
!!
!!    MODIFICATION
!!    ------------
!!
!!    Original    16/11/10
!!
!----------------------------------------------------------------------------
!
!*    0.     DECLARATION
!            -----------
!
USE MODD_DATA_COVER_PAR, ONLY : NVEGTYPE, NVT_GRAS
USE MODD_ISBA_GRID_n,    ONLY : NDIM
!
USE MODD_TYPE_DATE_SURF
!
USE MODD_SURF_ATM_n,     ONLY : LGARDEN
! 
USE MODD_ISBA_n,         ONLY : CALBEDO,                                  &
                                XALBNIR_DRY, XALBVIS_DRY, XALBUV_DRY,     &
                                XALBNIR_WET, XALBVIS_WET, XALBUV_WET,     &
                                XWSAT, XPERM
!
USE MODD_DATA_COVER_n,   ONLY : XDATA_VEGTYPE
USE MODD_DATA_COVER,     ONLY : XDATA_LAI, XDATA_H_TREE,  &
                                XDATA_VEG, XDATA_Z0, XDATA_Z0_O_Z0H,    &
                                XDATA_EMIS_ECO, XDATA_GAMMA, XDATA_CV,  &
                                XDATA_RGL, XDATA_RSMIN,                 &
                                XDATA_ALBNIR_VEG, XDATA_ALBVIS_VEG,     &
                                XDATA_ALBUV_VEG,                        &
                                XDATA_ALB_VEG_NIR, XDATA_ALB_VEG_VIS,   &
                                XDATA_ALB_SOIL_NIR, XDATA_ALB_SOIL_VIS, &
                                XDATA_GMES, XDATA_BSLAI, XDATA_LAIMIN,  &
                                XDATA_SEFOLD, XDATA_GC, XDATA_WRMAX_CF, &
                                XDATA_STRESS,                           &
                                XDATA_DMAX, XDATA_F2I, XDATA_RE25,      &
                                XDATA_CE_NITRO, XDATA_CF_NITRO,         &
                                XDATA_CNA_NITRO, XDATA_DICE,            &
                                XDATA_GMES_ST, XDATA_BSLAI_ST,          &
                                XDATA_SEFOLD_ST, XDATA_GC_ST,           &
                                XDATA_DMAX_ST, XDATA_WATSUP,            &
                                TDATA_SEED, TDATA_REAP,XDATA_IRRIG,     &
                                XDATA_GARDEN, XDATA_NATURE,             &
                                XDATA_ROOT_DEPTH, XDATA_GROUND_DEPTH,   &
                                XDATA_ROOT_EXTINCTION, XDATA_ROOT_LIN
!   
USE MODD_DATA_ISBA_n,   ONLY : XPAR_VEGTYPE,  XPAR_LAI, XPAR_H_TREE, XPAR_DG, XPAR_ROOTFRAC,     &
                                XPAR_VEG, XPAR_Z0, XPAR_EMIS,                                    &
                                XPAR_RSMIN, XPAR_GAMMA, XPAR_WRMAX_CF, XPAR_RGL,                 &
                                XPAR_CV, XPAR_Z0_O_Z0H,                                          &
                                XPAR_ALBNIR_VEG, XPAR_ALBVIS_VEG, XPAR_ALBUV_VEG,                &
                                XPAR_ALBNIR_SOIL, XPAR_ALBVIS_SOIL, XPAR_ALBUV_SOIL,             &
                                XPAR_GMES, XPAR_BSLAI, XPAR_SEFOLD, XPAR_GC, XPAR_DMAX,          &
                                XPAR_RE25, XPAR_LAIMIN, XPAR_F2I,                                &
                                XPAR_CE_NITRO,XPAR_CF_NITRO,XPAR_CNA_NITRO,XPAR_DICE,            &
                                XPAR_GROUND_DEPTH, XPAR_ROOT_DEPTH,                              &
                                XPAR_ROOT_EXTINCTION, XPAR_ROOT_LIN,                             &
                                LPAR_STRESS, XPAR_IRRIG, XPAR_WATSUP,                            &
                                LDATA_VEGTYPE, LDATA_LAI, LDATA_H_TREE, LDATA_DG, LDATA_ROOTFRAC,&  
                                LDATA_VEG, LDATA_Z0, LDATA_EMIS,                                 &
                                LDATA_RSMIN, LDATA_GAMMA, LDATA_WRMAX_CF, LDATA_RGL,             &
                                LDATA_CV, LDATA_Z0_O_Z0H,                                        &
                                LDATA_ALBNIR_VEG, LDATA_ALBVIS_VEG, LDATA_ALBUV_VEG,             &
                                LDATA_ALBVIS_SOIL, LDATA_ALBNIR_SOIL, LDATA_ALBUV_SOIL,          &
                                LDATA_GMES, LDATA_BSLAI, LDATA_SEFOLD, LDATA_GC, LDATA_DMAX,     &
                                LDATA_RE25, LDATA_LAIMIN, LDATA_F2I,                             &
                                LDATA_CE_NITRO,LDATA_CF_NITRO, LDATA_CNA_NITRO, LDATA_DICE,      &
                                LDATA_STRESS, LDATA_IRRIG, LDATA_WATSUP,                         &
                                LDATA_GROUND_DEPTH, LDATA_ROOT_DEPTH,                            &
                                LDATA_ROOT_EXTINCTION, LDATA_ROOT_LIN
!
USE MODI_AV_PGD_PARAM
USE MODI_AV_PGD
USE MODI_SOIL_ALBEDO
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB
!
IMPLICIT NONE
!
!*    0.1    Declaration of arguments
!            ------------------------
!
 CHARACTER(LEN=*),       INTENT(IN)    :: HISBA   ! type of soil (Force-Restore OR Diffusion)
INTEGER,                INTENT(IN)    :: KDECADE
INTEGER,                INTENT(IN)    :: KDECADE2
REAL, DIMENSION(:,:),   INTENT(IN)    :: PCOVER
 CHARACTER(LEN=*),       INTENT(IN)    :: HPHOTO  ! type of photosynthesis
LOGICAL,                INTENT(IN)    :: OAGRIP
LOGICAL,                INTENT(IN)    :: OPERM
 CHARACTER(LEN=*),       INTENT(IN)    :: HSFTYPE ! nature / garden
!
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(IN)   :: PWG1
!
REAL, DIMENSION(:)  ,   OPTIONAL, INTENT(IN)    :: PSOILGRID
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PVEG
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PLAI
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PRSMIN
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PGAMMA
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PWRMAX_CF
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PRGL
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PCV
REAL, DIMENSION(:,:,:), OPTIONAL, INTENT(OUT)   :: PDG
INTEGER, DIMENSION(:,:),OPTIONAL, INTENT(OUT)   :: KWG_LAYER
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PDROOT
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PDG2
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PZ0
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PZ0_O_Z0H
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PALBNIR_VEG
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PALBVIS_VEG
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PALBUV_VEG
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PEMIS_ECO
!
REAL, DIMENSION(:,:), OPTIONAL, INTENT(OUT)   :: PVEGTYPE
REAL, DIMENSION(:,:,:), OPTIONAL, INTENT(OUT)   :: PROOTFRAC
!
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PGMES
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PBSLAI
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PLAIMIN
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PSEFOLD
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PGC
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PDMAX
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PF2I
LOGICAL, DIMENSION(:,:),OPTIONAL, INTENT(OUT)   :: OSTRESS
!
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PH_TREE
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PRE25
!
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PCE_NITRO
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PCF_NITRO
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PCNA_NITRO
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PD_ICE
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PALBNIR_SOIL
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PALBVIS_SOIL
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PALBUV_SOIL
!
TYPE(DATE_TIME), DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: TPSEED
TYPE(DATE_TIME), DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: TPREAP
!
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PWATSUP
REAL, DIMENSION(:,:),   OPTIONAL, INTENT(OUT)   :: PIRRIG
!
!*    0.2    Declaration of local variables
!            ------------------------------
!
 CHARACTER(LEN=3)  :: YTREE, YNAT, YLAI, YVEG, YBAR, YDIF
!
INTEGER               :: JLAYER    ! loop counter on layers
INTEGER               :: JVEGTYPE  ! loop counter on vegtypes
!
!*    0.3    Declaration of namelists
!            ------------------------
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!-------------------------------------------------------------------------------
!
!*    1.      Initializations
!             ---------------
!
IF (LHOOK) CALL DR_HOOK('CONVERT_PATCH_ISBA',0,ZHOOK_HANDLE)
!
IF (HSFTYPE=='NAT') THEN
  YNAT='NAT'
  YTREE='TRE'
  YLAI='LAI'
  YVEG='VEG'
  YBAR='BAR'
  YDIF='DVG'
ELSEIF (HSFTYPE=='GRD') THEN
  YNAT='GRD'
  YTREE='GRT'
  YLAI='GRL'
  YVEG='GRV'
  YBAR='GRB'
  YDIF='GDV'
ENDIF
!
!
! vegtypes fraction
! -----------------
!
IF (PRESENT(PVEGTYPE)) THEN
  IF (LDATA_VEGTYPE) THEN
    PVEGTYPE=XPAR_VEGTYPE
  ELSE
    !classical ecoclimap case
    DO JVEGTYPE=1,NVEGTYPE
      CALL AV_PGD (PVEGTYPE(:,JVEGTYPE),PCOVER ,XDATA_VEGTYPE(:,JVEGTYPE),YNAT,'ARI')
    END DO
  ENDIF
ENDIF
!
!   VEG
!   ----
IF (PRESENT(PVEG)) THEN
  IF (LDATA_VEG) THEN
    CALL AV_PGD_PARAM(PVEG,XPAR_VEGTYPE,XPAR_VEG(:,KDECADE2,:),YNAT,'ARI')
  ELSE
    CALL AV_PGD (PVEG,PCOVER,XDATA_VEG(:,KDECADE,:),YNAT,'ARI')
  ENDIF
ENDIF
!
!   LAI
!   ----
IF (PRESENT(PLAI)) THEN
  IF (LDATA_LAI) THEN
    CALL AV_PGD_PARAM(PLAI,XPAR_VEGTYPE,XPAR_LAI(:,KDECADE2,:),YVEG,'ARI',KDECADE=KDECADE2)
  ELSE
    CALL AV_PGD(PLAI,PCOVER,XDATA_LAI(:,KDECADE,:),YVEG,'ARI',KDECADE=KDECADE)
  ENDIF
ENDIF
!
!           EMIS
!           ----
!emis needs VEG by vegtypes is changed at this step
IF (PRESENT(PEMIS_ECO)) THEN    
  IF (LDATA_EMIS) THEN
    CALL AV_PGD_PARAM(PEMIS_ECO,XPAR_VEGTYPE,XPAR_EMIS(:,KDECADE2,:),YNAT,'ARI')
  ELSE
    CALL AV_PGD (PEMIS_ECO ,PCOVER ,XDATA_EMIS_ECO (:,KDECADE,:),YNAT,'ARI')
  ENDIF
ENDIF
!
!    Z0V
!    ----
IF (PRESENT(PZ0)) THEN
  IF (LDATA_Z0) THEN
    CALL AV_PGD_PARAM(PZ0,XPAR_VEGTYPE,XPAR_Z0(:,KDECADE2,:),YNAT,'CDN')
  ELSE
    CALL AV_PGD (PZ0 ,PCOVER ,XDATA_Z0 (:,KDECADE,:),YNAT,'CDN')
  ENDIF
ENDIF
!
!* soil layers and root fraction
!  -----------------------------
!
IF ( PRESENT(PDG)) THEN
  !
  !   compute soil layers (and root fraction if DIF)
  !
  CALL SET_GRID_PARAM(SIZE(PDG,1),SIZE(PDG,2),SIZE(PDG,3),PRESENT(PDG2),&
                      PRESENT(PDROOT),PRESENT(KWG_LAYER),PRESENT(PROOTFRAC))
  !    
ENDIF
!
!        D ICE
!        -----
!
IF (PRESENT(PD_ICE).AND.HISBA/='DIF') THEN
  IF (LDATA_DICE) THEN
    CALL AV_PGD_PARAM(PD_ICE,XPAR_VEGTYPE,XPAR_DICE,YNAT,'ARI')
  ELSE
    CALL AV_PGD (PD_ICE,PCOVER,XDATA_DICE(:,:),YNAT,'ARI')
  ENDIF
ENDIF
!
!        Other parameters
!        ----------------
IF (PRESENT(PRSMIN)) THEN
  IF( SIZE(PRSMIN)>0) THEN
    IF (LDATA_RSMIN) THEN
      CALL AV_PGD_PARAM(PRSMIN,XPAR_VEGTYPE,XPAR_RSMIN,YLAI,'INV',KDECADE=KDECADE2)
    ELSE
      CALL AV_PGD (PRSMIN,PCOVER,XDATA_RSMIN,YLAI,'INV',KDECADE=KDECADE)  
    ENDIF
  ENDIF
ENDIF

IF (PRESENT(PGAMMA)) THEN
  IF (LDATA_GAMMA) THEN
    CALL AV_PGD_PARAM(PGAMMA,XPAR_VEGTYPE,XPAR_GAMMA,YVEG,'ARI',KDECADE=KDECADE2)
  ELSE
    CALL AV_PGD (PGAMMA,PCOVER,XDATA_GAMMA,YVEG,'ARI',KDECADE=KDECADE)  
  ENDIF
ENDIF

IF (PRESENT(PWRMAX_CF)) THEN
  IF (LDATA_WRMAX_CF) THEN
    CALL AV_PGD_PARAM(PWRMAX_CF,XPAR_VEGTYPE,XPAR_WRMAX_CF,YVEG,'ARI',KDECADE=KDECADE2)
  ELSE
    CALL AV_PGD (PWRMAX_CF,PCOVER,XDATA_WRMAX_CF,YVEG,'ARI',KDECADE=KDECADE)  
  ENDIF
ENDIF

IF (PRESENT(PRGL)) THEN
  IF (LDATA_RGL) THEN
    CALL AV_PGD_PARAM(PRGL,XPAR_VEGTYPE,XPAR_RGL,YVEG,'ARI',KDECADE=KDECADE2)
  ELSE
    CALL AV_PGD (PRGL,PCOVER,XDATA_RGL,YVEG,'ARI',KDECADE=KDECADE)  
  ENDIF
ENDIF

IF (PRESENT(PCV)) THEN
  IF (LDATA_CV) THEN
    CALL AV_PGD_PARAM(PCV,XPAR_VEGTYPE,XPAR_CV,YVEG,'INV',KDECADE=KDECADE2)
  ELSE
    CALL AV_PGD (PCV,PCOVER,XDATA_CV,YVEG,'INV',KDECADE=KDECADE)  
  ENDIF
ENDIF

IF (PRESENT(PZ0_O_Z0H)) THEN
  IF (LDATA_Z0_O_Z0H) THEN
    CALL AV_PGD_PARAM(PZ0_O_Z0H,XPAR_VEGTYPE,XPAR_Z0_O_Z0H,YNAT,'ARI')
  ELSE
    CALL AV_PGD (PZ0_O_Z0H,PCOVER,XDATA_Z0_O_Z0H,YNAT,'ARI')
  ENDIF
ENDIF
!
IF (PRESENT(PALBNIR_VEG)) THEN
  IF (LDATA_ALBNIR_VEG) THEN
    CALL AV_PGD_PARAM(PALBNIR_VEG,XPAR_VEGTYPE,XPAR_ALBNIR_VEG,YVEG,'ARI',KDECADE=KDECADE2)
  ELSEIF (CALBEDO=='CM13') THEN
    CALL AV_PGD (PALBNIR_VEG,PCOVER,XDATA_ALB_VEG_NIR(:,KDECADE,:),YVEG,'ARI',KDECADE=KDECADE)    
  ELSE
    CALL AV_PGD (PALBNIR_VEG,PCOVER,XDATA_ALBNIR_VEG,YVEG,'ARI',KDECADE=KDECADE)  
  ENDIF
ENDIF
!
IF (PRESENT(PALBVIS_VEG)) THEN
  IF (LDATA_ALBVIS_VEG) THEN
    CALL AV_PGD_PARAM(PALBVIS_VEG,XPAR_VEGTYPE,XPAR_ALBVIS_VEG,YVEG,'ARI',KDECADE=KDECADE2)
  ELSEIF (CALBEDO=='CM13') THEN
    CALL AV_PGD (PALBVIS_VEG,PCOVER,XDATA_ALB_VEG_VIS(:,KDECADE,:),YVEG,'ARI',KDECADE=KDECADE)      
  ELSE
    CALL AV_PGD (PALBVIS_VEG,PCOVER,XDATA_ALBVIS_VEG,YVEG,'ARI',KDECADE=KDECADE)  
  ENDIF
ENDIF
!
IF (PRESENT(PALBUV_VEG)) THEN
  IF (CALBEDO=='CM13'.AND.PRESENT(PALBVIS_VEG)) THEN
    PALBUV_VEG(:,:)=PALBVIS_VEG(:,:)
  ELSE
    IF (LDATA_ALBUV_VEG) THEN
      CALL AV_PGD_PARAM(PALBUV_VEG,XPAR_VEGTYPE,XPAR_ALBUV_VEG,YVEG,'ARI',KDECADE=KDECADE2)
    ELSE
      CALL AV_PGD (PALBUV_VEG,PCOVER,XDATA_ALBUV_VEG,YVEG,'ARI',KDECADE=KDECADE)  
    ENDIF
  ENDIF
ENDIF
IF (PRESENT(PH_TREE)) THEN
  IF (LDATA_H_TREE) THEN
    CALL AV_PGD_PARAM(PH_TREE,XPAR_VEGTYPE,XPAR_H_TREE,YTREE,'ARI')
  ELSE
    CALL AV_PGD (PH_TREE,PCOVER,XDATA_H_TREE(:,:),YTREE,'ARI')
  ENDIF
ENDIF
!
IF (HPHOTO/='NON') THEN
  !
  IF (PRESENT(PRE25)) THEN
    IF (SIZE(PRE25)>0) THEN
      IF (LDATA_RE25) THEN
        CALL AV_PGD_PARAM(PRE25,XPAR_VEGTYPE,XPAR_RE25,YNAT,'ARI')      
      ELSE
        CALL AV_PGD (PRE25,PCOVER,XDATA_RE25,YNAT,'ARI')  
      ENDIF
    ENDIF
  ENDIF
  !
  IF (PRESENT(PLAIMIN)) THEN
    IF (SIZE(PLAIMIN)>0) THEN
      IF (LDATA_LAIMIN) THEN
        CALL AV_PGD_PARAM(PLAIMIN,XPAR_VEGTYPE,XPAR_LAIMIN,YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        CALL AV_PGD (PLAIMIN,PCOVER,XDATA_LAIMIN,YVEG,'ARI',KDECADE=KDECADE)  
      ENDIF
    ENDIF
  ENDIF        
  !
  IF (PRESENT(PBSLAI)) THEN
    IF( SIZE(PBSLAI)>0) THEN
      IF (LDATA_BSLAI) THEN
        CALL AV_PGD_PARAM(PBSLAI,XPAR_VEGTYPE,XPAR_BSLAI,YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        IF (HPHOTO == 'AST' .OR. HPHOTO == 'LST' .OR. HPHOTO == 'NIT' .OR. HPHOTO == 'NCB') THEN
          CALL AV_PGD (PBSLAI,PCOVER,XDATA_BSLAI_ST,YVEG,'ARI',KDECADE=KDECADE)  
        ELSE
          CALL AV_PGD (PBSLAI,PCOVER,XDATA_BSLAI,YVEG,'ARI',KDECADE=KDECADE)
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  !  
  IF (PRESENT(PSEFOLD)) THEN
    IF (SIZE(PSEFOLD)>0) THEN
      IF (LDATA_SEFOLD) THEN
        CALL AV_PGD_PARAM(PSEFOLD,XPAR_VEGTYPE,XPAR_SEFOLD,YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        IF (HPHOTO == 'AST' .OR. HPHOTO == 'LST' .OR. HPHOTO == 'NIT' .OR. HPHOTO == 'NCB') THEN
          CALL AV_PGD (PSEFOLD,PCOVER,XDATA_SEFOLD_ST,YVEG,'ARI',KDECADE=KDECADE)  
        ELSE
          CALL AV_PGD (PSEFOLD,PCOVER,XDATA_SEFOLD,YVEG,'ARI',KDECADE=KDECADE)
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  !
  IF (PRESENT(PGMES)) THEN
    IF ( SIZE(PGMES)>0) THEN
      IF (LDATA_GMES) THEN
        CALL AV_PGD_PARAM(PGMES,XPAR_VEGTYPE,XPAR_GMES,YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        IF (HPHOTO == 'AST' .OR. HPHOTO == 'LST' .OR. HPHOTO == 'NIT' .OR. HPHOTO == 'NCB') THEN
          CALL AV_PGD (PGMES,PCOVER,XDATA_GMES_ST,YVEG,'ARI',KDECADE=KDECADE)  
        ELSE
          CALL AV_PGD (PGMES,PCOVER,XDATA_GMES,YVEG,'ARI',KDECADE=KDECADE)
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  !
  IF (PRESENT(PGC)) THEN
    IF ( SIZE(PGC)>0) THEN
      IF (LDATA_GC) THEN
        CALL AV_PGD_PARAM(PGC,XPAR_VEGTYPE,XPAR_GC,YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        IF (HPHOTO == 'AST' .OR. HPHOTO == 'LST' .OR. HPHOTO == 'NIT' .OR. HPHOTO == 'NCB') THEN
          CALL AV_PGD (PGC,PCOVER,XDATA_GC_ST,YVEG,'ARI',KDECADE=KDECADE)  
        ELSE
          CALL AV_PGD (PGC,PCOVER,XDATA_GC,YVEG,'ARI',KDECADE=KDECADE)
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  !
  IF (PRESENT(PDMAX)) THEN
    IF (SIZE(PDMAX)>0) THEN
      IF (LDATA_DMAX) THEN
        CALL AV_PGD_PARAM(PDMAX,XPAR_VEGTYPE,XPAR_DMAX,YTREE,'ARI')
      ELSE
        IF (HPHOTO == 'AST' .OR. HPHOTO == 'LST' .OR. HPHOTO == 'NIT' .OR. HPHOTO == 'NCB') THEN
          CALL AV_PGD (PDMAX,PCOVER,XDATA_DMAX_ST,YTREE,'ARI')  
        ELSE
          CALL AV_PGD (PDMAX,PCOVER,XDATA_DMAX,YTREE,'ARI')
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  !
  IF (HPHOTO/='AGS' .AND. HPHOTO/='LAI') THEN
    !
    IF (PRESENT(PF2I)) THEN
      IF (SIZE(PF2I)>0) THEN
        IF (LDATA_F2I) THEN
          CALL AV_PGD_PARAM(PF2I,XPAR_VEGTYPE,XPAR_F2I,YVEG,'ARI',KDECADE=KDECADE2)
        ELSE
          CALL AV_PGD (PF2I,PCOVER,XDATA_F2I,YVEG,'ARI',KDECADE=KDECADE)  
        ENDIF
      ENDIF
    ENDIF
    !
    IF (HPHOTO=='NIT' .OR. HPHOTO=='NCB') THEN
      !
      IF (PRESENT(PCE_NITRO)) THEN
        IF (SIZE(PCE_NITRO)>0) THEN
          IF (LDATA_CE_NITRO) THEN
            CALL AV_PGD_PARAM(PCE_NITRO,XPAR_VEGTYPE,XPAR_CE_NITRO,YVEG,'ARI',KDECADE=KDECADE2)
          ELSE
            CALL AV_PGD (PCE_NITRO,PCOVER,XDATA_CE_NITRO,YVEG,'ARI',KDECADE=KDECADE)  
          ENDIF
        ENDIF
      ENDIF
      !
      IF (PRESENT(PCF_NITRO)) THEN
        IF (SIZE(PCF_NITRO)>0) THEN
          IF (LDATA_CF_NITRO) THEN
            CALL AV_PGD_PARAM(PCF_NITRO,XPAR_VEGTYPE,XPAR_CF_NITRO,YVEG,'ARI',KDECADE=KDECADE2)
          ELSE
            CALL AV_PGD (PCF_NITRO,PCOVER,XDATA_CF_NITRO,YVEG,'ARI',KDECADE=KDECADE)  
          ENDIF
        ENDIF
      ENDIF
      !
      IF (PRESENT(PCNA_NITRO)) THEN
        IF (SIZE(PCNA_NITRO)>0) THEN
          IF (LDATA_CNA_NITRO) THEN
            CALL AV_PGD_PARAM(PCNA_NITRO,XPAR_VEGTYPE,XPAR_CNA_NITRO,YVEG,'ARI',KDECADE=KDECADE2)
          ELSE
            CALL AV_PGD (PCNA_NITRO,PCOVER,XDATA_CNA_NITRO,YVEG,'ARI',KDECADE=KDECADE)  
          ENDIF
        ENDIF
      ENDIF
      !
    ENDIF
  ENDIF
ENDIF
!
IF ((HPHOTO == 'LAI' .OR. HPHOTO == 'LST' .OR. HPHOTO == 'NIT' .OR. HPHOTO=='NCB') .AND. OAGRIP)  THEN
  !
  ! date of seeding
  ! ---------------
  !
  IF (PRESENT(TPSEED)) THEN
     IF(SIZE(TPSEED)>0) THEN
      CALL AV_PGD (TPSEED ,PCOVER ,TDATA_SEED(:,:),YVEG,'MAJ',KDECADE=KDECADE)  
     ENDIF
  END IF
  !
  ! date of reaping
  ! ---------------
  !
  IF (PRESENT(TPREAP)) THEN
    IF (SIZE(TPREAP)>0) THEN
      CALL AV_PGD (TPREAP ,PCOVER ,TDATA_REAP(:,:),YVEG,'MAJ',KDECADE=KDECADE)  
    ENDIF
  END IF
  !
  IF (PRESENT(PIRRIG)) THEN 
    IF (SIZE(PIRRIG)>0) THEN
      IF (LDATA_IRRIG) THEN
        CALL AV_PGD_PARAM(PIRRIG,XPAR_VEGTYPE,XPAR_IRRIG(:,KDECADE2,:),YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        CALL AV_PGD (PIRRIG,PCOVER,XDATA_IRRIG,YVEG,'ARI',KDECADE=KDECADE)  
      ENDIF
    ENDIF
  ENDIF

  IF (PRESENT(PWATSUP)) THEN  
    IF (SIZE(PWATSUP)>0) THEN
      IF (LDATA_WATSUP) THEN
        CALL AV_PGD_PARAM(PWATSUP,XPAR_VEGTYPE,XPAR_WATSUP(:,KDECADE2,:),YVEG,'ARI',KDECADE=KDECADE2)
      ELSE
        CALL AV_PGD (PWATSUP,PCOVER,XDATA_WATSUP,YVEG,'ARI',KDECADE=KDECADE)  
      ENDIF
    ENDIF
  ENDIF

ENDIF
!
IF (PRESENT(PALBNIR_SOIL)) THEN
  IF (LDATA_ALBNIR_SOIL) THEN
    CALL AV_PGD_PARAM(PALBNIR_SOIL,XPAR_VEGTYPE,XPAR_ALBNIR_SOIL,YBAR,'ARI',KDECADE=KDECADE2)
  ELSEIF (CALBEDO=='CM13') THEN
    CALL AV_PGD (PALBNIR_SOIL,PCOVER,XDATA_ALB_SOIL_NIR(:,KDECADE,:),YBAR,'ARI',KDECADE=KDECADE)
  ELSE
    CALL SOIL_ALBEDO (CALBEDO, XWSAT(:,1),PWG1, XALBVIS_DRY,XALBNIR_DRY,XALBUV_DRY,     &
                      XALBVIS_WET,XALBNIR_WET,XALBUV_WET, PALBNIR_SOIL=PALBNIR_SOIL )
  ENDIF
ENDIF
!
IF (PRESENT(PALBVIS_SOIL)) THEN
  IF (LDATA_ALBVIS_SOIL) THEN
    CALL AV_PGD_PARAM(PALBVIS_SOIL,XPAR_VEGTYPE,XPAR_ALBVIS_SOIL,YBAR,'ARI',KDECADE=KDECADE2)
  ELSEIF (CALBEDO=='CM13') THEN
    CALL AV_PGD (PALBVIS_SOIL,PCOVER,XDATA_ALB_SOIL_VIS(:,KDECADE,:),YBAR,'ARI',KDECADE=KDECADE)    
  ELSE
   CALL SOIL_ALBEDO (CALBEDO, XWSAT(:,1),PWG1, XALBVIS_DRY,XALBVIS_DRY,XALBUV_DRY,     &
                     XALBVIS_WET,XALBNIR_WET,XALBUV_WET, PALBVIS_SOIL=PALBVIS_SOIL )
  ENDIF
ENDIF
!
IF (PRESENT(PALBUV_SOIL)) THEN
  IF (CALBEDO=='CM13'.AND.PRESENT(PALBVIS_SOIL)) THEN
    PALBUV_SOIL(:,:)=PALBVIS_SOIL(:,:)
  ELSE
    IF (LDATA_ALBUV_SOIL) THEN
      CALL AV_PGD_PARAM(PALBUV_SOIL,XPAR_VEGTYPE,XPAR_ALBUV_SOIL,YNAT,'ARI',KDECADE=KDECADE2)
    ELSE
      CALL SOIL_ALBEDO (CALBEDO, XWSAT(:,1),PWG1, XALBVIS_DRY,XALBUV_DRY,XALBUV_DRY,     &
                        XALBVIS_WET,XALBNIR_WET,XALBUV_WET,PALBUV_SOIL=PALBUV_SOIL )
    ENDIF
  ENDIF
ENDIF
!
!       STRESS
!       --------
IF (PRESENT(OSTRESS)) THEN
  IF (SIZE(OSTRESS)>0) THEN
    CALL SET_STRESS(SIZE(OSTRESS,1),SIZE(OSTRESS,2))      
  ENDIF
ENDIF
!
IF (LHOOK) CALL DR_HOOK('CONVERT_PATCH_ISBA',1,ZHOOK_HANDLE)
!
!-------------------------------------------------------------------------------
CONTAINS
!-------------------------------------------------------------------------------
!
SUBROUTINE SET_STRESS(KSIZE1,KSIZE2)
!
IMPLICIT NONE
!
INTEGER, INTENT(IN) :: KSIZE1
INTEGER, INTENT(IN) :: KSIZE2
!
REAL, DIMENSION(KSIZE1,KSIZE2)   :: ZWORK
REAL, DIMENSION(KSIZE1,NVEGTYPE) :: ZSTRESS
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
IF (LHOOK) CALL DR_HOOK('CONVERT_PATCH_ISBA:SET_STRESS',0,ZHOOK_HANDLE)
!
IF (LDATA_STRESS) THEN
  ZSTRESS(:,:)=0.
  DO JVEGTYPE=1,NVEGTYPE
    WHERE (LPAR_STRESS(:,JVEGTYPE)) ZSTRESS(:,JVEGTYPE)=1.
  ENDDO
  CALL AV_PGD_PARAM(ZWORK,XPAR_VEGTYPE,ZSTRESS,YVEG,'ARI',KDECADE=KDECADE2)
ELSE
  CALL AV_PGD (ZWORK,PCOVER,XDATA_STRESS(:,:),YVEG,'ARI',KDECADE=KDECADE)
ENDIF
!
WHERE (ZWORK(:,:)<0.5)
  OSTRESS(:,:) = .FALSE.
ELSEWHERE
  OSTRESS(:,:) = .TRUE.
END WHERE
!
IF (LHOOK) CALL DR_HOOK('CONVERT_PATCH_ISBA:SET_STRESS',1,ZHOOK_HANDLE)
END SUBROUTINE SET_STRESS
!
!-------------------------------------------------------------------------------
SUBROUTINE SET_GRID_PARAM(KNI,KGROUND,KPATCH,LDG2,LDROOT,LWG_LAYER,LROOTFRAC)
!
USE MODD_SURF_PAR,       ONLY : XUNDEF, NUNDEF
!
USE MODI_INI_DATA_ROOTFRAC
USE MODI_INI_DATA_SOIL
USE MODI_PERMAFROST_DEPTH
USE MODI_ABOR1_SFX
!
IMPLICIT NONE
!
REAL, PARAMETER     :: ZPREC=1.0E+6
!
INTEGER, INTENT(IN) :: KNI
INTEGER, INTENT(IN) :: KGROUND
INTEGER, INTENT(IN) :: KPATCH
LOGICAL, INTENT(IN) :: LDG2
LOGICAL, INTENT(IN) :: LDROOT
LOGICAL, INTENT(IN) :: LWG_LAYER
LOGICAL, INTENT(IN) :: LROOTFRAC
!
REAL, DIMENSION (KNI,KGROUND,KPATCH) :: ZROOTFRAC
REAL, DIMENSION (KNI,KPATCH) :: ZDTOT, ZDG2, ZROOT_EXT, ZROOT_LIN, ZWORK_EXT
!--------------waiting for new vegtypes-----------------------------------!
REAL, DIMENSION (NDIM,NVEGTYPE)   :: ZPAR_ROOT_EXTINCTION
REAL, DIMENSION (SIZE(XDATA_ROOT_EXTINCTION,1),NVEGTYPE) :: ZDATA_ROOT_EXTINCTION
!--------------waiting for new vegtypes-----------------------------------!
INTEGER, DIMENSION(KNI,KPATCH) :: IWG_LAYER
INTEGER :: JJ, JL, JPATCH
REAL(KIND=JPRB) :: ZHOOK_HANDLE
!
IF (LHOOK) CALL DR_HOOK('CONVERT_PATCH_ISBA:SET_GRID_PARAM',0,ZHOOK_HANDLE)
!
IF(HISBA=='DIF')THEN
  IF(.NOT.LWG_LAYER) CALL ABOR1_SFX('CONVERT_PATCH_ISBA: SET_GRID_PARAM: KWG_LAYER must be present with DIF')
  IF(.NOT.LDROOT   ) CALL ABOR1_SFX('CONVERT_PATCH_ISBA: SET_GRID_PARAM:  PDROOT must be present with DIF')
  IF(.NOT.LDG2     ) CALL ABOR1_SFX('CONVERT_PATCH_ISBA: SET_GRID_PARAM: PDG2 must be present with DIF')   
ENDIF
!
ZROOTFRAC(:,:,:) = XUNDEF
ZDTOT    (:,:) = XUNDEF
ZDG2     (:,:) = XUNDEF
IWG_LAYER(:,:) = NUNDEF
!
!DG IN NAMELIST => GROUND_DEPTH KNOWN, ROOT_DEPTH UNKNOWN 
IF (LDATA_DG) THEN
  !
  DO JLAYER=1,KGROUND
    CALL AV_PGD_PARAM(PDG(:,JLAYER,:),XPAR_VEGTYPE,XPAR_DG(:,JLAYER,:),YNAT,'ARI')
  ENDDO
  !
ENDIF
!
!CALCULATION OF GROUND_DEPTH IN ZDTOT : ECOCLIMAP OR LDATA_GROUND_DEPTH
IF (HISBA/='2-L') THEN 
  !
  IF (LDATA_GROUND_DEPTH .AND. (HISBA=='DIF' .OR. .NOT.LDATA_DG)) THEN
    !GROUND DEPTH IN NAMELIST
    CALL AV_PGD_PARAM(ZDTOT(:,:),XPAR_VEGTYPE,XPAR_GROUND_DEPTH(:,:),YNAT,'ARI')
    !Error Due to machine precision
    WHERE(ZDTOT(:,:)/=XUNDEF)
          ZDTOT(:,:)=INT(ZDTOT(:,:)*ZPREC)/ZPREC
    ENDWHERE
    !CONSISTENCY CHECK
    IF (LDATA_DG) ZDTOT(:,:) = MIN(ZDTOT(:,:),PDG(:,KGROUND,:))
  ELSEIF (LDATA_DG) THEN
    !GROUND DEPTH FROM NAMELIST DG
    ZDTOT(:,:) = PDG(:,KGROUND,:)
  ELSE
    !GROUND DEPTH FROM ECOCLIMAP
    CALL AV_PGD (ZDTOT(:,:),PCOVER,XDATA_GROUND_DEPTH(:,:),YNAT,'ARI')
  ENDIF
  !
ENDIF
!
!CALCULATION OF GROUND_DEPTH : Permafrost depth put to 12m
IF(HISBA=='DIF'.AND.OPERM)THEN
  CALL PERMAFROST_DEPTH(KNI,KPATCH,XPERM,ZDTOT)
ENDIF
!
!IN BOTH CASES, ROOT_DEPTH IS NEEDED: PUT IN DG2
IF (HISBA=='DIF' .OR. .NOT.LDATA_DG) THEN
  !
  IF ( LDATA_ROOT_DEPTH .AND. .NOT.LDATA_ROOTFRAC ) THEN
    !ROOT_DEPTH IN NAMELIST
    CALL AV_PGD_PARAM(ZDG2(:,:),XPAR_VEGTYPE,XPAR_ROOT_DEPTH(:,:),YNAT,'ARI')
    !Error Due to machine precision
    WHERE(ZDG2(:,:)/=XUNDEF)
          ZDG2(:,:)=INT(ZDG2(:,:)*ZPREC)/ZPREC
    ENDWHERE    
    !CONSISTENCY CHECKS
    IF (LDATA_DG) ZDG2(:,:) = MIN(ZDG2(:,:),PDG(:,KGROUND,:))
    ZDTOT(:,:) = MAX(ZDG2(:,:),ZDTOT(:,:))
    IF (HISBA=='DIF') THEN
      CALL AV_PGD_PARAM(PDROOT(:,:),XPAR_VEGTYPE,XPAR_ROOT_DEPTH(:,:),YDIF,'ARI')
     !Error Due to machine precision
      WHERE(PDROOT(:,:)/=XUNDEF)
          PDROOT(:,:)=INT(PDROOT(:,:)*ZPREC)/ZPREC
      ENDWHERE      
     !CONSISTENCY CHECKS
      IF (LDATA_DG) WHERE (PDROOT(:,:).NE.XUNDEF) PDROOT(:,:) = MIN(PDROOT(:,:),PDG(:,KGROUND,:))   
    ENDIF
  ELSE 
    !ROOT_DEPTH FROM ECOCLIMAP
    CALL AV_PGD (ZDG2(:,:),PCOVER,XDATA_ROOT_DEPTH(:,:),YNAT,'ARI')  
    IF (HISBA=='DIF') CALL AV_PGD (PDROOT(:,:),PCOVER,XDATA_ROOT_DEPTH(:,:),YDIF,'ARI')
    IF ( LDATA_GROUND_DEPTH .OR. LDATA_DG ) THEN
      ZDG2  (:,:) = MIN(ZDG2  (:,:),ZDTOT(:,:))
      IF (HISBA=='DIF') WHERE (PDROOT(:,:).NE.XUNDEF) PDROOT(:,:) = MIN(PDROOT(:,:),ZDTOT(:,:))
    ENDIF
  ENDIF
  !
  !CALCULATION OF DG IF NOT IN NAMELIST
  IF (.NOT.LDATA_DG) THEN
    !
    IF (HISBA=='DIF') THEN
      IF( MAXVAL(ZDTOT,ZDTOT/=XUNDEF)>PSOILGRID(KGROUND) ) THEN
        CALL ABOR1_SFX('CONVERT_PATCH_ISBA: not enough soil layer with optimized grid')
      ENDIF
    ENDIF
    !
    WHERE(ZDG2(:,:)==XUNDEF.AND.ZDTOT(:,:)/=XUNDEF) ZDG2(:,:)=0.0 !No vegetation
    !
    !IF CISBA=DIF CALCULATES ALSO KWG_LAYER WITH USE OF SOILGRID $
    CALL INI_DATA_SOIL(HISBA, PDG,PROOTDEPTH=ZDG2, PSOILDEPTH=ZDTOT,&
                       PSOILGRID=PSOILGRID, KWG_LAYER=IWG_LAYER )
    !
  ELSEIF ( HISBA=='DIF') THEN
    !
    !CALCULATION OF KWG_LAYER IF DG IN NAMELIST
    IF(LDATA_GROUND_DEPTH)THEN
      DO JPATCH=1,KPATCH
        DO JJ=1,KNI
          DO JL=1,KGROUND
            IF( PDG(JJ,JL,JPATCH) <= ZDTOT(JJ,JPATCH) .AND. ZDTOT(JJ,JPATCH) < XUNDEF ) &
                IWG_LAYER(JJ,JPATCH) = JL
          ENDDO
        ENDDO
      ENDDO                
    ELSE
      IWG_LAYER(:,:) = KGROUND
    ENDIF
    !
  ENDIF
  !
  ! DROOT AND DG2 LIMITED BY KWG_LAYER
  IF (HISBA=='DIF' .AND. .NOT.LDATA_ROOTFRAC) THEN
    !
    DO JPATCH=1,KPATCH
      DO JJ=1,KNI
        IF(IWG_LAYER(JJ,JPATCH)/=NUNDEF) THEN
          JL = IWG_LAYER(JJ,JPATCH)
          ZDG2  (JJ,JPATCH)=MIN(ZDG2  (JJ,JPATCH),PDG(JJ,JL,JPATCH))
          IF (PDROOT(JJ,JPATCH)/=XUNDEF) PDROOT(JJ,JPATCH)=MIN(PDROOT(JJ,JPATCH),PDG(JJ,JL,JPATCH))    
        ENDIF
      ENDDO
    ENDDO
    !
  ENDIF
  !
ENDIF
!
!CALCULATION OF ROOTFRAC
IF (HISBA=='DIF') THEN
  !
  IF (LDATA_ROOTFRAC .AND. (LDG2 .OR. LDROOT .OR. LROOTFRAC)) THEN
    !
    !ROOTFRAC IN NAMELIST
    DO JL=1,KGROUND
      CALL AV_PGD_PARAM(ZROOTFRAC(:,JL,:),XPAR_VEGTYPE,XPAR_ROOTFRAC(:,JL,:),YNAT,'ARI')
    ENDDO
    IF (LROOTFRAC) PROOTFRAC(:,:,:) = ZROOTFRAC(:,:,:)
    !    
    ZDG2  (:,:)=0.0
    PDROOT(:,:)=0.0    
    DO JPATCH=1,KPATCH
      DO JJ=1,KNI
        !
        !DROOT DEPENDS ON ROOTFRAC
        DO JL=KGROUND,1,-1
          IF( ZROOTFRAC(JJ,JL,JPATCH)>=1.0 )THEN
            ZDG2  (JJ,JPATCH) = PDG(JJ,JL,JPATCH)
            PDROOT(JJ,JPATCH) = PDG(JJ,JL,JPATCH)
          ELSEIF (JL<KGROUND.AND.ZROOTFRAC(JJ,JL,JPATCH)>0.0) THEN
            IF (IWG_LAYER(JJ,JPATCH)<=JL) IWG_LAYER(JJ,JPATCH) = JL+1
            EXIT
          ENDIF
        ENDDO
        !
        IF(PDROOT(JJ,JPATCH)==0.0.AND.ZDG2(JJ,JPATCH)==0.0)THEN
          JL=IWG_LAYER(JJ,JPATCH)
          ZDG2(JJ,JPATCH)=MIN(0.6,PDG(JJ,JL,JPATCH))
        ENDIF
        !
      ENDDO
    ENDDO
    !
  ELSEIF (LROOTFRAC) THEN
    !
    !DEPENDS ON DROOT
    IF (LDATA_ROOT_EXTINCTION) THEN
      CALL AV_PGD_PARAM(ZROOT_EXT(:,:),XPAR_VEGTYPE,XPAR_ROOT_EXTINCTION(:,:),YDIF,'ARI')
    ELSE
      CALL AV_PGD (ZROOT_EXT(:,:),PCOVER,XDATA_ROOT_EXTINCTION(:,:),YDIF,'ARI')
    ENDIF
    !--------------waiting for new vegtypes-----------------------------------!
    !Jackson parameter for tundra
    IF(OPERM)THEN
      IF (LDATA_ROOT_EXTINCTION) THEN
        ZPAR_ROOT_EXTINCTION(:,:)       =XPAR_ROOT_EXTINCTION(:,:)
        ZPAR_ROOT_EXTINCTION(:,NVT_GRAS)=0.914
        CALL AV_PGD_PARAM(ZWORK_EXT(:,:),XPAR_VEGTYPE,ZPAR_ROOT_EXTINCTION(:,:),YDIF,'ARI')
      ELSE
        ZDATA_ROOT_EXTINCTION(:,:)       =XDATA_ROOT_EXTINCTION(:,:)
        ZDATA_ROOT_EXTINCTION(:,NVT_GRAS)=0.914
        CALL AV_PGD (ZWORK_EXT(:,:),PCOVER,ZDATA_ROOT_EXTINCTION(:,:),YDIF,'ARI')
      ENDIF        
      DO JPATCH=1,KPATCH
        DO JJ=1,KNI
           IF(XPERM(JJ)>=0.25.AND.ZROOT_EXT(JJ,JPATCH)/=XUNDEF)THEN
              ZROOT_EXT(JJ,JPATCH)=ZWORK_EXT(JJ,JPATCH)
           ENDIF
        ENDDO
      ENDDO
    ENDIF
    !--------------waiting for new vegtypes-----------------------------------!
    IF (LDATA_ROOT_LIN) THEN
      CALL AV_PGD_PARAM(ZROOT_LIN(:,:),XPAR_VEGTYPE,XPAR_ROOT_LIN(:,:),YDIF,'ARI')
    ELSE
      CALL AV_PGD (ZROOT_LIN(:,:),PCOVER,XDATA_ROOT_LIN(:,:),YDIF,'ARI')
    ENDIF
    !
    CALL INI_DATA_ROOTFRAC(PDG,PDROOT,ZROOT_EXT,ZROOT_LIN,PROOTFRAC)
    !
  ENDIF
  !
  IF (LDG2)      PDG2     (:,:) = ZDG2     (:,:)
  IF (LWG_LAYER) KWG_LAYER(:,:) = IWG_LAYER(:,:)
  !
ENDIF
!     
IF (LHOOK) CALL DR_HOOK('CONVERT_PATCH_ISBA:SET_GRID_PARAM',1,ZHOOK_HANDLE)
!
END SUBROUTINE SET_GRID_PARAM
!
!-------------------------------------------------------------------------------
END SUBROUTINE CONVERT_PATCH_ISBA
