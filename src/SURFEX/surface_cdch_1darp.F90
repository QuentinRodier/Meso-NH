!SFX_LIC Copyright 1994-2014 CNRS, Meteo-France and Universite Paul Sabatier
!SFX_LIC This is part of the SURFEX software governed by the CeCILL-C licence
!SFX_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt  
!SFX_LIC for details. version 1.
!     #########
SUBROUTINE SURFACE_CDCH_1DARP (PZREF, PZ0EFF, PZ0H, PVMOD, PTA, PTG, &
                                 PQA, PQS, AT, PCD, PCDN, PCH, PRI          )    
!   ###################################################################
!     Purpose :
!     ------

!     - CALCUL DES COEFFICIENTS D'ECHANGE TURBULENT Cd ET Ch
!       DE LA MEME FACON QU'ARPEGE. (cf ACHMT)

!-----------------------------------------------------------------------
!     -------
!     Auteur.
!     -------
!  Dec 2007 : M. Jidane      * MAROC METEO *
!
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------
! PZ0EFF     : LA LONGUEUR DE RUGOSITE COURANTE.
! PZ0H       : LONGUEUR DE RUGOSITE THERMIQUE COURANTE
! PTA        : TEMPERATURE.
! PTG        : TEMPERATURE DE SURFACE.
! PQA        : specific humidity
! PQS        : humidity at surface

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------
! PCD        : COEFFICIENT D'ECHANGE EN SURFACE POUR U ET V.
! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.
! PCH        : COEFFICIENT D'ECHANGE EN SURFACE POUR T ET Q

USE MODD_CSTS,        ONLY : XG, XRD, XRV, XCPD, XKARMAN,XPI
USE MODD_SURF_ATM,    ONLY : XEDB, XEDC, XEDD, XEDK, XUSURIC, XUSURID, XUSURICL
USE MODD_SURF_ATM_TURB_n, ONLY : SURF_ATM_TURB_t
!
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE PARKIND1  ,ONLY : JPRB, JPIM
!

IMPLICIT NONE

REAL, DIMENSION(:), INTENT(IN)  :: PZREF
REAL, DIMENSION(:), INTENT(IN)  :: PZ0EFF
REAL, DIMENSION(:), INTENT(IN)  :: PZ0H
REAL, DIMENSION(:), INTENT(IN)  :: PVMOD
REAL, DIMENSION(:), INTENT(IN)  :: PTA
REAL, DIMENSION(:), INTENT(IN)  :: PTG
REAL, DIMENSION(:), INTENT(IN)  :: PQA
REAL, DIMENSION(:), INTENT(IN)  :: PQS
TYPE(SURF_ATM_TURB_t), INTENT(IN )   :: AT ! atmospheric turbulence parameters
REAL, DIMENSION(:), INTENT(OUT) :: PCD
REAL, DIMENSION(:), INTENT(OUT) :: PCDN
REAL, DIMENSION(:), INTENT(OUT) :: PCH
REAL, DIMENSION(:), INTENT(OUT) :: PRI

INTEGER         :: JLON

REAL, DIMENSION(0:3,1:4) :: GCZ0H

REAL, DIMENSION(SIZE(PTA))     :: ZSTAB
!                         ZSTAB : INDICE DE STABILITE A LA SURFACE.
REAL, DIMENSION(SIZE(PTA))     :: ZCDNH, ZRTI, ZU

REAL :: ZR, ZRS
!                         ZR  : CONSTANTE DES GAZ POUR L'AIR.
!                         ZRS : CONSTANTE DES GAZ POUR L'AIR AU SOL.

REAL :: Z2B, Z3B, Z3BC, ZCD, ZCD0, ZCH, ZCH0, ZCIS, ZDID, &
          ZDIH, ZDS, ZLOI, ZLOS, ZMU, ZPD, ZPH, ZRZD, ZRZH, &
          ZUSURIC, ZSTA, ZIXP, ZSTAH, ZHS  

LOGICAL :: LLM123
INTEGER(KIND=JPIM) :: ITTYPE
REAL(KIND=JPRB) :: ZETKE_R(SIZE(PTA)),ZETKE_P(SIZE(PTA)),ZRITKE(SIZE(PTA)),&
&ZMRIFPP(SIZE(PTA)),ZCHI3TA(SIZE(PTA)),ZPHI3TA(SIZE(PTA)),ZFMTKE(SIZE(PTA)),&
&ZFTTKE(SIZE(PTA))
REAL(KIND=JPRB) :: ZXETKE_R,ZXETKE_RIFC,ZEPS8,ZSM0,ZLAMF_X,ZLAMBDA,ZSF,&
& ZXETKE_LAM1,ZXETKE_LAM2,ZXETKE_LAM3, ZXETKE_LAM4,ZSIG,Z_ETKE_RU,Z_ETKE_R,&
& Z_ETKE_PU,Z_ETKE_P,ZS_AA4,ZS_AA3,ZS_AA2,ZS_AA1,ZS_CW,ZS_CP,ZS_CQ,ZS_CDIS,&
&ZS_ZSOL,ZS_CPHI,ZS_CU1,ZS_CU2,ZS_CU3,ZS_CX1,ZS_CX2,ZS_CX3,ZRIFTKE,ZFSFTKE,&
&ZS_CTHIRD,ZPI

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!*
!     ------------------------------------------------------------------
!     I - CONSTANTES AUXILIAIRES.
!  ISBA - roughness length
IF (LHOOK) CALL DR_HOOK('SURFACE_CDCH_1DARP',0,ZHOOK_HANDLE)

! associate toucans variables
ASSOCIATE(LCOEFKTKE=>AT%LCOEFKTKE, LCOEFK_F1=>AT%LCOEFK_F1, &
 & CGTURS=>AT%CGTURS, &
 & NUPTKE=>AT%XNUPTKE, C3TKEFREE=>AT%XC3TKEFREE, &
 & ETKE_OLAM=>AT%XETKE_OLAM, EFB_UR=>AT%XEFB_UR, &
 & EFB_AZ0=>AT%XEFB_AZ0, REFB_1=>AT%XREFB_1, & 
 & REFB_2=>AT%XREFB_2, REFB_3=>AT%XREFB_3, &
 & RLOUIS_S0=>AT%XRLOUIS_S0, PLOUIS_S0=>AT%XPLOUIS_S0, &
 & RLOUIS_GU1=>AT%XRLOUIS_GU1, RLOUIS_GU2=>AT%XRLOUIS_GU2, &
 & PLOUIS_GU1=>AT%XPLOUIS_GU1, PLOUIS_GU2=>AT%XPLOUIS_GU2, &
 & RLOUIS_GS1=>AT%XRLOUIS_GS1, RLOUIS_GS2=>AT%XRLOUIS_GS2, &
 & RLOUIS_GS3=>AT%XRLOUIS_GS3, RLOUIS_GS4=>AT%XRLOUIS_GS4, &
 & PLOUIS_GS1=>AT%XPLOUIS_GS1, PLOUIS_GS2=>AT%XPLOUIS_GS2, &
 & PLOUIS_GS3=>AT%XPLOUIS_GS3, PLOUIS_GS4=>AT%XPLOUIS_GS4, &
 & RQNSE_S0=>AT%XRQNSE_S0, RQNSE_GU1=>AT%XRQNSE_GU1, &
 & RQNSE_GU2=>AT%XRQNSE_GU2, RQNSE_GS1=>AT%XRQNSE_GS1, &
 & RQNSE_GS2=>AT%XRQNSE_GS2, RQNSE_GS3=>AT%XRQNSE_GS3, &
 & RQNSE_GS4=>AT%XRQNSE_GS4 )

GCZ0H(0,1)=7.5
GCZ0H(1,1)=2.39037
GCZ0H(2,1)=-.28583
GCZ0H(3,1)=.01074
GCZ0H(0,2)=0.5
GCZ0H(1,2)=-.07028
GCZ0H(2,2)=.01023
GCZ0H(3,2)=-.00067
GCZ0H(0,3)=5.0
GCZ0H(1,3)=4.51268
GCZ0H(2,3)=.34012
GCZ0H(3,3)=-.05330
GCZ0H(0,4)=0.5
GCZ0H(1,4)=-.09421
GCZ0H(2,4)=.01463
GCZ0H(3,4)=-.00099


ZUSURIC=XUSURIC*XUSURICL

IF(LCOEFKTKE) THEN
  ZEPS8=1E-8_JPRB
  ZS_CTHIRD=1.0_JPRB/3.0_JPRB
  IF     (TRIM(CGTURS) == 'RMC') THEN
    ITTYPE=1
  ELSEIF (TRIM(CGTURS) == 'MD1') THEN
    ITTYPE=2
  ELSEIF (TRIM(CGTURS) == 'MD2') THEN
    ITTYPE=3
  ELSEIF (TRIM(CGTURS) == 'QNSE') THEN
    ITTYPE=4
  ELSEIF (TRIM(CGTURS) == 'EFB') THEN
    ITTYPE=5
  ELSEIF (TRIM(CGTURS) == 'LOUIS') THEN
    ITTYPE=6
  ENDIF


  LLM123=(ITTYPE == 1).OR.(ITTYPE == 2).OR.(ITTYPE == 3)

  ZSM0=NUPTKE**4/2._JPRB
  ZLAMF_X=SQRT(375.0_JPRB*C3TKEFREE**2*(C3TKEFREE**2+2._JPRB*C3TKEFREE+1._JPRB)*ZSM0**2 &
   & -20.0_JPRB*(C3TKEFREE**2+C3TKEFREE-1._JPRB)*ZSM0+1.0_JPRB)
  ZLAMBDA=SQRT(5.0_JPRB)*3.0_JPRB/4.0_JPRB*ZLAMF_X-30.0_JPRB*C3TKEFREE &
   & *(C3TKEFREE+1._JPRB)*ZSM0+0.25_JPRB
  ZSF=(10.0_JPRB-75.0_JPRB*C3TKEFREE*(C3TKEFREE+1._JPRB)*ZSM0)/(8.0_JPRB*ZLAMBDA)

  ZXETKE_LAM1=4.0_JPRB/15.0_JPRB*ZLAMBDA
  ZXETKE_LAM3=(14.0_JPRB*ZLAMBDA*ZSF+5.0_JPRB*ZLAMBDA)/75.0_JPRB
  ZXETKE_LAM2=(5.0_JPRB*ZLAMBDA-6.0_JPRB*ZLAMBDA*ZSF)/25.0_JPRB

  IF(ITTYPE==1) THEN
   !RMC01
   ZXETKE_R=1.0_JPRB
   ZXETKE_LAM4=0.0_JPRB
   ZXETKE_RIFC=0.544_JPRB
  ELSE IF (ITTYPE==2) THEN
   ! Model I

   ZXETKE_R=1.0_JPRB-2.0_JPRB*ZLAMBDA*(44.0_JPRB*ZSF**2+160.0_JPRB*ZSF-25.0_JPRB)/375.0_JPRB
   ZXETKE_LAM4=0.0_JPRB
   ZXETKE_RIFC=(15.0_JPRB*ZSM0*C3TKEFREE**2*(C3TKEFREE+1.0_JPRB))&
   & /(6.0_JPRB*ETKE_OLAM+2.0_JPRB*C3TKEFREE)
  ELSE IF ((ITTYPE==3).OR.(ITTYPE==4)) THEN
   ! Model II or QNSE


   ZXETKE_R=-4.0_JPRB*C3TKEFREE/75.0_JPRB*&
    &(88.0_JPRB*ZLAMBDA*ZSF**2+320.0_JPRB*ZLAMBDA*ZSF-50.0_JPRB*ZLAMBDA-375.0_JPRB)/&
    &(8.0_JPRB*ZLAMBDA*C3TKEFREE*ZSF+10.0_JPRB*ZLAMBDA*C3TKEFREE+20.0_JPRB*C3TKEFREE+15.0_JPRB)


   


   ZXETKE_LAM4=ZLAMBDA/4.0_JPRB
   ZXETKE_RIFC=(30_JPRB*C3TKEFREE**3*ZSM0)/&
    &(12.0_JPRB*ETKE_OLAM+(-60.0_JPRB*C3TKEFREE**2*ZXETKE_LAM3-20.0_JPRB*C3TKEFREE**2&
    &*ZXETKE_LAM2+4.0_JPRB*C3TKEFREE)*ZXETKE_LAM4-15.0_JPRB*C3TKEFREE**2*ZXETKE_LAM1     &
    & +4.0_JPRB*C3TKEFREE)
  ELSE IF (ITTYPE==5) THEN
  !EFB
   ZXETKE_R=EFB_UR
   ZXETKE_LAM4=0.0_JPRB
   ZXETKE_RIFC=0.25_JPRB
  ELSE IF (ITTYPE==6) THEN
  !LOUIS
   ZXETKE_R=RLOUIS_S0
   ZXETKE_LAM4=0.0_JPRB
   ZXETKE_RIFC=0.91_JPRB
  ENDIF

ENDIF

!*
!     ------------------------------------------------------------------
!     II - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     LE CARRE DU CISAILLEMENT DE VENT).

!          COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANT (FOR
!     THE SQUARE OF THE WIND SHEAR).

Z2B=2.0*XEDB
Z3B=3.*XEDB
Z3BC=3.*XEDB*XEDC

!*
!     ------------------------------------------------------------------
!     IV - CALCUL DE L EPAISSEUR DE LA CLS (EN GEOPOTENTIEL).

!          COMPUTATION OF THE THICKNESS OF THE SBL (IN GEOPOTENTIAL).

! - TEMPORAIRE(S) 1D .

!*
!     ------------------------------------------------------------------
!     VI - CALCULS PROPREMENT DITS DES CARACTERISTIQUES DE SURFACE.

!          EFFECTIVE CALCULATIONS OF THE SURFACE CHARACTERISTICS.

IF(XUSURID == 0.0) THEN
  ZIXP=1.0
ELSE
  ZIXP=3.
ENDIF

DO JLON=1, SIZE(PTA)

  ZR  = XRD + (XRV-XRD)*PQA(JLON)
  ZRS = XRD + (XRV-XRD)*PQS(JLON)

!     CALCULS GEOMETRIQUES.
!     GEOMETRIC CALCULATIONS.

  ZRZD=1.0+PZREF(JLON)/PZ0EFF(JLON)
  PCDN(JLON)=(XKARMAN/LOG(ZRZD))**2

!     CALCUL PRELIMINAIRES NECESSAIRES A LA DETERMINATION DES
!     COEFFICIENTS D'ECHANGE CD ET CH DANS LE CAS DE DEUX RUGOSITES
!     DYNAMIQUE Z0 ET THERMIQUE ZOH.

!     PRELIMINARY COMPUTATIONS NECESSARY FOR THE DETERMINATION OF
!     EXCHANGE COEFFICIENTS CD AND CH IN THE CASE OF TWO ROUGHNESS
!     LENGTHS Z0 DYNAMICAL AND ZOH THERMAL.

! - TEMPORAIRE(S) 1D .

! ZCDNH     : COEFFICIENT THERMIQUE NEUTRE D'ECHANGE EN SURFACE.
!           : NEUTRAL SURFACE THERMAL EXCHANGE COEFFICIENT.

  ZRZH=1.0+PZREF(JLON)/PZ0H(JLON)
  ZCDNH(JLON)=XKARMAN**2/(LOG(ZRZH)*LOG(ZRZD))
  ZMU=LOG(PZ0EFF(JLON)/PZ0H(JLON))
  ZCD0=(GCZ0H(0,1)+ZMU*(GCZ0H(1,1)+ZMU*(GCZ0H(2,1)+ZMU &
        *GCZ0H(3,1))))/(1.5*XEDC)    
  ZPD=(GCZ0H(0,2)+ZMU*(GCZ0H(1,2)+ZMU*(GCZ0H(2,2)+ZMU*GCZ0H(3,2))))-0.5
  ZCH0=(GCZ0H(0,3)+ZMU*(GCZ0H(1,3)+ZMU*(GCZ0H(2,3)+ZMU*GCZ0H(3,3))))/XEDC
  ZPH=(GCZ0H(0,4)+ZMU*(GCZ0H(1,4)+ZMU*(GCZ0H(2,4)+ZMU*GCZ0H(3,4))))-0.5
  ZCD=ZCD0*ZRZD**ZPD
  ZCH=ZCH0*ZRZH**ZPH

!     CISAILLEMENT DE VENT.
!     WIND SHEAR.

! - TEMPORAIRE(S) 1D .

! ZU        : MODULE DU VENT DE SURFACE.
!           : SURFACE WIND SPEED.

  ZCIS=PVMOD(JLON)**2
  ZU(JLON)=PVMOD(JLON)

!     CALCUL DE STABILITE.
!     STABILITY COMPUTATION.

! - TEMPORAIRE(S) 1D .

! ZRTI      : INVERSE DE R*T.
!           : INVERSE OF R*T.
! ZSTA      : APPROXIMATION DE DELTA(PHI)*DELTA(LN(THETA)).
!           : APPROXIMATION OF DELTA(PHI)*DELTA(LN(THETA)).

  ZRTI(JLON)=2.0/(ZR*PTA(JLON)+(XRD/XCPD)*XG*PZREF(JLON)&
        +ZRS*PTG(JLON))    
  ZSTA=XG*PZREF(JLON)*(ZR*PTA(JLON)+(XRD/XCPD) &
        *XG*PZREF(JLON)-ZRS*PTG(JLON))*ZRTI(JLON)    
  ZSTAH=ZSTA/(1.0+ZIXP*ZUSURIC*MAX(0.0,ZSTA)/ZCIS)**(1.0/ZIXP)
  ZSTA=ZSTA/(1.0+ZUSURIC*MAX(0.0,ZSTA)/ZCIS)
  ZSTAB(JLON)=MAX(0.0,SIGN(1.0,ZSTA))

  !TOUCANS
  ZRITKE(JLON)=ZSTA/ZCIS

!     CALCULS COMMUNS POUR QUANTITE DE MOUVEMENT ET ENERGIE.
!     COMMON COMPUTATIONS FOR MOMENTUM AND ENERGY.

  ZDS=SQRT(ZCIS+XEDD/XEDK*ABS(ZSTA))
  ZHS=SQRT(ZCIS+XEDD*XEDK*ABS(ZSTAH))
  ZDID=1.0/(ZU(JLON)+ZCD*Z3BC*PCDN(JLON)*SQRT(ABS(ZSTA)*ZRZD))
  ZDIH=1.0/(ZU(JLON)+ZCH*Z3BC*ZCDNH(JLON)*SQRT(ABS(ZSTA)*ZRZH))

!     CALCULS POUR LES COMPOSANTES DU VENT DE CD ET DE SON PRODUIT PAR
!     DENSITE FOIS MODULE DU VENT.
!     COMPUTATIONS FOR MOMENTUM OF CD AND OF ITS PRODUCT BY DENSITY
!     TIME WIND SPEED.

  ZLOS=ZCIS*ZDS/(ZU(JLON)*ZDS+Z2B*ABS(ZSTA))
  ZLOI=ZU(JLON)-Z2B*ZSTA*ZDID
  PCD(JLON)=(ZLOI+ZSTAB(JLON)*(ZLOS-ZLOI))*PCDN(JLON)/ZU(JLON)

!     IDEM POUR LA TEMPERATURE ET L'HUMIDITE.
!     THE SAME FOR TEMPERATURE AND HUMIDITY.

  ZLOS=ZCIS**2/(ZU(JLON)*ZCIS+Z3B*ABS(ZSTAH)*ZHS)
  ZLOI=ZU(JLON)-Z3B*ZSTA*ZDIH
  PCH(JLON)=(ZLOI+ZSTAB(JLON)*(ZLOS-ZLOI))*ZCDNH(JLON)/ZU(JLON)

ENDDO

!TOUCANS - overwrites above computation of CD/CH
IF(LCOEFKTKE) THEN

  IF (LLM123) THEN
    !Ri ->Rif surface
    DO JLON=1,SIZE(PTA)
      !modified CCH02 or RMC01
      ZETKE_R(JLON)=ZXETKE_R
      ZETKE_P(JLON)=ZXETKE_RIFC
      ZSIG=SIGN(0.5_JPRB,ZRITKE(JLON))+0.5_JPRB

      ZMRIFPP(JLON)=-(sqrt((ZETKE_R(JLON)*                            &
      & (ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON)))**2                          &
      & -4.0_JPRB*ZRITKE(JLON)*C3TKEFREE*ZETKE_R(JLON)*ZETKE_P(JLON)**2)    &
      & -(ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON))*ZETKE_R(JLON))/             &
      & (2.0_JPRB*ZETKE_P(JLON))
      !security
      ZMRIFPP(JLON)=MIN(ZMRIFPP(JLON),ZXETKE_RIFC-ZEPS8)
    ENDDO
  ENDIF



  IF (ITTYPE == 4) THEN

    DO JLON=1,SIZE(PTA)
      ZSIG=SIGN(0.5_JPRB,ZRITKE(JLON))+0.5_JPRB  
      !  UNSTABLE
      Z_ETKE_RU=(RQNSE_GU1*ZRITKE(JLON)+RQNSE_S0)/&
       &(RQNSE_GU2*ZRITKE(JLON)+1.0_JPRB)
      !  STABLE
      Z_ETKE_R=(ZRITKE(JLON)*(RQNSE_GS1*ZRITKE(JLON)+&
       & RQNSE_GS2)+RQNSE_S0)/&
       &(ZRITKE(JLON)*(RQNSE_GS3*ZRITKE(JLON)+RQNSE_GS4)+1.0_JPRB)
      ZETKE_R(JLON)=ZSIG*Z_ETKE_R+(1.0_JPRB-ZSIG)*Z_ETKE_RU
      ZETKE_P(JLON)=ZXETKE_RIFC

      ZMRIFPP(JLON)=-(sqrt((ZETKE_R(JLON)*                            &
      & (ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON)))**2                          &
      & -4.0_JPRB*ZRITKE(JLON)*C3TKEFREE*ZETKE_R(JLON)*ZETKE_P(JLON)**2)    &
      & -(ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON))*ZETKE_R(JLON))/             &
      & (2.0_JPRB*ZETKE_P(JLON))
      !security
      ZMRIFPP(JLON)=MIN(ZMRIFPP(JLON),ZXETKE_RIFC-ZEPS8)
    ENDDO

  ENDIF ! ITTYPE==4 QNSE



  IF (ITTYPE == 5) THEN
    !EFB
    !  UNSTABLE
    Z_ETKE_RU=EFB_UR
    !  STABLE
    ZS_AA4 = EFB_AZ0*REFB_2

    DO JLON=1,SIZE(PTA)
      ZSIG=SIGN(0.5_JPRB,ZRITKE(JLON))+0.5_JPRB  

      ZS_AA3 = EFB_AZ0*(REFB_3-REFB_1-ZRITKE(JLON)*REFB_2*C3TKEFREE)&
       &      - ETKE_OLAM*REFB_1*ZRITKE(JLON)
      ZS_AA2 = C3TKEFREE*EFB_AZ0*ZRITKE(JLON)*(REFB_1-REFB_3)&
       &      - REFB_3*EFB_UR*(ETKE_OLAM*ZRITKE(JLON)+EFB_AZ0)
      ZS_AA1 = ZRITKE(JLON)*EFB_AZ0*EFB_UR*REFB_3*C3TKEFREE

      ! cubic problem only
      ZS_CW=ZS_AA3/ZS_AA4*ZS_CTHIRD
      ZS_CP=(ZS_AA2/ZS_AA4*ZS_CTHIRD-ZS_CW**2)**3
      ZS_CQ=-0.5_JPRB*(2.0_JPRB*ZS_CW**3-(ZS_AA2*ZS_CW-ZS_AA1)/ZS_AA4)
      ZS_CDIS=ZS_CQ**2+ZS_CP

      ZS_ZSOL = +1.E+99_JPRB

      IF (ZS_CDIS < 0.0_JPRB) THEN
      !         three real solutions!
      !         Confine the argument of ACOS to the interval [-1;1]!
        ZS_CPHI=ACOS(MIN(1._JPRB,MAX(-1._JPRB,ZS_CQ/SQRT(-ZS_CP))))
        ZS_CP=2.0_JPRB*(-ZS_CP)**(0.5_JPRB*ZS_CTHIRD)
        ZS_CU1=ZS_CP*COS((ZS_CPHI+2.0_JPRB*XPI)*ZS_CTHIRD)-ZS_CW
        ZS_CU2=ZS_CP*COS((ZS_CPHI+4.0_JPRB*XPI)*ZS_CTHIRD)-ZS_CW
        ZS_CU3=ZS_CP*COS((ZS_CPHI+6.0_JPRB*XPI)*ZS_CTHIRD)-ZS_CW
        ZS_CX1=MIN(ZS_CU1,ZS_CU2,ZS_CU3)
        ZS_CX2=MAX(MIN(ZS_CU1,ZS_CU2),MIN(ZS_CU1,ZS_CU3), &
         &  MIN(ZS_CU2,ZS_CU3))
        ZS_CX3=MAX(ZS_CU1,ZS_CU2,ZS_CU3)
        ZS_ZSOL = MIN(ZS_ZSOL,ZS_CX1,ZS_CX2,ZS_CX3)
      ELSE
      !      only one real solution!
        ZS_CPHI=SQRT(ZS_CDIS)
        ZS_CX1=SIGN(ABS(ZS_CQ+ZS_CPHI)**ZS_CTHIRD,ZS_CQ+ZS_CPHI)+ &
         &  SIGN(ABS(ZS_CQ-ZS_CPHI)**ZS_CTHIRD,ZS_CQ-ZS_CPHI)-ZS_CW
        ZS_ZSOL = MAX(ZS_ZSOL,ZS_CX1)
      ENDIF

      ZRIFTKE=ZS_ZSOL
      Z_ETKE_R=(ZRIFTKE*REFB_1+EFB_UR*REFB_3)/&
       &(ZRIFTKE*REFB_2+REFB_3)
      ZETKE_R(JLON)=ZSIG*Z_ETKE_R+(1.0_JPRB-ZSIG)*Z_ETKE_RU
      ZETKE_P(JLON)=ZETKE_R(JLON)&
       & /(ETKE_OLAM*ZETKE_R(JLON)/(EFB_AZ0*C3TKEFREE)+1.0_JPRB)

      ZMRIFPP(JLON)=-(sqrt((ZETKE_R(JLON)*                            &
      & (ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON)))**2                          &
      & -4.0_JPRB*ZRITKE(JLON)*C3TKEFREE*ZETKE_R(JLON)*ZETKE_P(JLON)**2)    &
      & -(ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON))*ZETKE_R(JLON))/             &
      & (2.0_JPRB*ZETKE_P(JLON))
      !security
      ZMRIFPP(JLON)=MIN(ZMRIFPP(JLON),ZXETKE_RIFC-ZEPS8)
    ENDDO

  ENDIF ! ITTYPE==5 EFB



  IF (ITTYPE == 6) THEN

    DO JLON=1,SIZE(PTA)
      ZSIG=SIGN(0.5_JPRB,ZRITKE(JLON))+0.5_JPRB  
      !  UNSTABLE
      Z_ETKE_RU=(RLOUIS_GU1*ZRITKE(JLON)+RLOUIS_S0)/&
       &(RLOUIS_GU2*ZRITKE(JLON)+1.0_JPRB)
      !  STABLE
      Z_ETKE_R=(ZRITKE(JLON)*(RLOUIS_GS1*ZRITKE(JLON)+&
       & RLOUIS_GS2)+RLOUIS_S0)/&
       &(ZRITKE(JLON)*(RLOUIS_GS3*ZRITKE(JLON)+RLOUIS_GS4)+1.0_JPRB)
      ZETKE_R(JLON)=ZSIG*Z_ETKE_R+(1.0_JPRB-ZSIG)*Z_ETKE_RU

      !UNSTABLE
      Z_ETKE_PU=(PLOUIS_GU1*ZRITKE(JLON)+PLOUIS_S0)/&
       &(PLOUIS_GU2*ZRITKE(JLON)+1.0_JPRB)
      !STABLE
      Z_ETKE_P=(ZRITKE(JLON)*(PLOUIS_GS1*ZRITKE(JLON)+&
       & PLOUIS_GS2)+PLOUIS_S0)/&
       &(ZRITKE(JLON)*(PLOUIS_GS3*ZRITKE(JLON)+PLOUIS_GS4)+1.0_JPRB)
      ZETKE_P(JLON)=ZSIG*Z_ETKE_P+(1.0_JPRB-ZSIG)*Z_ETKE_PU

      ZMRIFPP(JLON)=-(sqrt((ZETKE_R(JLON)*                            &
      & (ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON)))**2                          &
      & -4.0_JPRB*ZRITKE(JLON)*C3TKEFREE*ZETKE_R(JLON)*ZETKE_P(JLON)**2)    &
      & -(ZRITKE(JLON)*C3TKEFREE+ZETKE_P(JLON))*ZETKE_R(JLON))/             &
      & (2.0_JPRB*ZETKE_P(JLON))
      !security
      ZMRIFPP(JLON)=MIN(ZMRIFPP(JLON),ZXETKE_RIFC-ZEPS8)
    ENDDO

  ENDIF ! ITTYPE==6 LOUIS

  DO JLON=1,SIZE(PTA)
    ZCHI3TA(JLON)=(1.0_JPRB-ZMRIFPP(JLON)/ZETKE_R(JLON))&
    & /(1.0_JPRB-ZMRIFPP(JLON))
    ZPHI3TA(JLON)=(1.0_JPRB-ZMRIFPP(JLON)/ZETKE_P(JLON))&
    & /(1.0_JPRB-ZMRIFPP(JLON))
  ENDDO

  IF(LCOEFK_F1) THEN
    DO JLON=1,SIZE(PTA)
     ZSIG=MAX(0._JPRB,SIGN(1._JPRB,ZMRIFPP(JLON)))
     
     ZFSFTKE=(1.0_JPRB-ZSIG)+ZSIG*SQRT(ZCHI3TA(JLON)-&
     & C3TKEFREE*ZPHI3TA(JLON)*ZRITKE(JLON))
     ZFMTKE(JLON)=ZCHI3TA(JLON)*ZFSFTKE
     ZFTTKE(JLON)=ZPHI3TA(JLON)*ZFSFTKE
    ENDDO
  ELSE 
    DO JLON=1,SIZE(PTA)     
      ZFMTKE(JLON)=ZCHI3TA(JLON)*SQRT(ZCHI3TA(JLON)*(1._JPRB-ZMRIFPP(JLON)))
      ZFTTKE(JLON)=ZFMTKE(JLON)*ZPHI3TA(JLON)/ZCHI3TA(JLON)
    ENDDO
  ENDIF



  DO JLON=1,SIZE(PTA)
    PRI(JLON)=ZMRIFPP(JLON)
    PCD(JLON)=ZFMTKE(JLON)*PCDN(JLON)
    PCH(JLON)=ZFTTKE(JLON)*ZCDNH(JLON)
  ENDDO

ENDIF

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SURFACE_CDCH_1DARP',1,ZHOOK_HANDLE)

END SUBROUTINE SURFACE_CDCH_1DARP
